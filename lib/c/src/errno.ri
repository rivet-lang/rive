// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import c/libc;

#[boxed]
public struct ErrnoError < Throwable {
    public msg: string;
    public code: int32;

    public func to_string(self) -> string {
        return "errno: {} (code: {})".fmt(self.msg, self.code);
    }
}

#[inline]
public func errno() -> int32 {
    return unsafe {
#if _LINUX_
        libc.__errno_location()?.*
#else
        libc._errno().*
#endif
    };
}

#[inline]
public func errno_msg(code: int32 := errno()) -> string {
    unsafe {
        return if s := libc.strerror(code) {
            string.from_raw(s)
        } else {
            ""
        };
    }
}

#[inline]
public func last_errno_error() -> ErrnoError {
    return ErrnoError(errno_msg(), errno());
}
