// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import c/libc;

#[boxed]
pub struct StringBuilder < Stringable {
    mut inner: Vector;

    /// Returns a string builder with capacity `cap`.
    #[inline]
    pub func new(cap: uint := 0) -> Self {
        return Self(unsafe { Vector.new(@size_of(uint8), cap) });
    }

    #[inline]
    pub func from_string(s: string) -> Self {
        return if s.len == 0 {
            Self.new()
        } else {
            Self(unsafe {
                Vector.from_array(
                    @as(&mut uint8, s.ptr), @size_of(uint8), s.len
                )
            })
        };
    }

    #[inline]
    pub func write_byte(mut self, val: uint8) {
        self.inner.push(&val);
    }

    #[unsafe; inline]
    pub func write_raw(mut self, s: [&]uint8) {
        unsafe {
            self.write_raw_with_len(s, libc.strlen(s));
        }
    }

    #[unsafe]
    pub func write_raw_with_len(mut self, s: [&]uint8, len: uint) {
        self.inner.reserve(self.inner.len + len);
        mem_copy(
            unsafe { @ptr_add(self.inner.ptr, self.inner.elem_size * self.inner.len) },
            s, len
        );
        self.inner.len += len;
    }

    #[inline]
    pub func write_string(mut self, s: string) {
        unsafe {
            self.write_raw_with_len(s.ptr, s.len);
        }
    }

    pub func writeln(mut self, s: string := "") {
        if s.len > 0 {
            unsafe {
                self.write_raw_with_len(s.ptr, s.len);
            }
        }
        self.write_byte(b'\n');
    }

    pub func write_join(mut self, ss: []string, sep: string := "") {
        if ss.len == 1 {
            self.write_string(ss[0]);
        } else {
            for i, s in ss {
                self.write_string(s);
                if i < ss.len - 1 {
                    self.write_string(sep);
                }
            }
        }
    }

    #[inline]
    pub func write_fmt(mut self, s: string, args: ...Stringable) {
        self.write_string(s.fmt(args));
    }

    pub func writeln_fmt(mut self, s: string, args: ...Stringable) {
        self.write_string(s.fmt(args));
        self.write_byte(b'\n');
    }

    /// Returns the number of runes contained in `self`.
    #[inline]
    pub func runes_count(self) -> uint {
        return cstr_runes_count(unsafe { @as([&]uint8, self.inner.ptr) }, self.inner.len);
    }

    #[inline]
    pub func clear(mut self) {
        self.inner.clear();
    }

    #[inline]
    pub func len(self) -> uint {
        return self.inner.len;
    }

    #[inline]
    pub func cap(self) -> uint {
        return self.inner.cap;
    }

    #[inline]
    pub func is_empty(self) -> bool {
        return self.inner.is_empty();
    }

    #[inline]
    pub func to_string(mut self) -> string {
        self.write_byte(0);
        return string(unsafe { @as([&]uint8, self.inner.ptr) }, self.inner.len - 1);
    }
}
