// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

using pkg::demangle;

struct backtrace_state;

type BacktraceErrorCB = fn(*mut void, *u8, i32);
type BacktraceFullCB = fn(*mut void, i32, *u8, i32, *u8) i32;

extern (C) {
    fn backtrace_create_state(
        filename: *u8, threaded: i32, error_cb: BacktraceErrorCB,
        data: *mut void
    ) *mut backtrace_state;

    fn backtrace_full(
        state: *backtrace_state, skip: i32, cb: BacktraceFullCB,
        error_cb: BacktraceErrorCB, data: *mut void
    ) i32;
}

var bt_state: *mut backtrace_state = unsafe {
    backtrace_create_state(ARGS[0].ptr, 1, bt_error_handler, 0)
};

struct BacktraceData {
    mut frame_nr: usize;
    mut has_error: bool;
}

fn bt_print_callback(
    data: *mut void, pc: i32, filename_ptr: *mut u8, lineno: i32, fn_ptr: *u8
) i32 {
    unsafe {
        if (
            fn_ptr != none
            and stdc::memcmp(fn_ptr, c"_R4core10rivet_mainF", 20) == 0
        ) {
            return 1; // stop backtracing
        }

        var file_name = if (filename_ptr == none) {
            "???"
        } else {
            string::from_raw(filename_ptr)
        };

        var fn_name = if (fn_ptr == none) {
            "??"
        } else {
            demangle::demangle_symbol(str::from_raw(fn_ptr))
        };

        var bdata = as(*mut BacktraceData, data);
        eprintln(
            "   {} {} in {}:{}", if (bdata.frame_nr == 0) "at" else "by",
            fn_name, file_name, lineno
        );
        bdata.frame_nr++;
    }
    return 0;
}

fn bt_error_handler(data: *mut void, msg_ptr: *u8, errnum: i32) {
    unsafe {
        var bdata = as(*mut BacktraceData, data);
        if (!bdata.has_error) {
            eprint("   libbacktrace error: ");
            eprint(str::from_raw(msg_ptr));
            if (errnum > 0) {
                eprint(" (");
                eprint(str::from_raw(stdc::strerror(errnum)));
                eprint(")");
            }
            eprintln();
            bdata.has_error = true;
        }
    }
}

pub fn print(frames_to_skip: i32 = 0) {
    unsafe {
        _ = backtrace_full(
            bt_state, frames_to_skip, bt_print_callback, bt_error_handler,
            &mut BacktraceData{}
        );
    }
}
