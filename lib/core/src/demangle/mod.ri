// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

use pkg::StringBuilder;
use pkg::utils::StaticBuffer;

pub func demangle_symbol(name: string) string {
    if (!name.starts_with("_R")) {
        return name;
    }

    let name_ptr = unsafe { name.ptr };
    let mut idx: usize = 2;
    let buf = StaticBuffer();
    let res = StringBuilder::with_capacity(name.len);
    while (idx < name.len) {
        let byte = unsafe { name_ptr[idx] };
        if (byte.is_digit()) {
            buf.push(byte);
            idx++;
            continue;
        }

        if (!res.is_empty()) {
            res.write_string("::");
        }

        for (_ in 0..as(usize, buf.as_u64())) {
            res.write_byte(unsafe { name_ptr[idx++] });
        }

        buf.clear();

        if (!unsafe { name_ptr[idx] }.is_digit()) {
            break; // end
        }
    }
    return res.to_string();
}
