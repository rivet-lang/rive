// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

extern (Rivet) {
    public static mut ARGS: []string;

    func init_string_lits();
    func init_globals();
    func drop_globals();
}

extern (C) func init_args(_argc: usize, _argv: [*][*]uint8) {
    unsafe {
        ARGS = @vec(string, _argc);
        mut i: usize := 0;
        while i < _argc : i += 1 {
            ARGS.push(string.from_raw(_argv[i]));
        }
    }
}

func main(
    _argc: int32, _argv: [*][*]uint8,
#if _TESTS_
    mut test_runner: TestRunner
#else
    mod_main: func()
#endif
) {
    unsafe {
        setup_signals_handler();
        init_args(@cast(usize, _argc), _argv);
#if !_TESTS_ // `init_string_lits` is called after
        init_string_lits();
#endif
        init_globals();
        libcoreIsStarted = true;
#if _TESTS_
        if test_runner.tests.len > 0 {
            test_runner.run();
            test_runner.print_summary_tests();
        }
#else
        mod_main();
#endif
        drop_globals();
    }
}
