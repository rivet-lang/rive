// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

static returnTrace = ReturnTrace();

/// This trait is used for errors throwed with result types (!T).
public trait Throwable < Stringable { }

#[inline]
func uncatched_error(err: Throwable) {
    console_eprintln("uncatched error: {}", err.to_string());
    returnTrace.print();
    process_exit(102);
}

struct CallTrace {
    name: string;
    file: string;
    line: usize;
}

// NOTE: 50 should be fine for now.
const RETURN_TRACE_MAX_SIZE = 50;

struct ReturnTrace {
    traces: [RETURN_TRACE_MAX_SIZE]mut CallTrace;
    mut cur_idx: usize;

    #[inline]
    func add(mut self, trace: CallTrace) {
        if self.cur_idx == RETURN_TRACE_MAX_SIZE {
            process_panic("maximum return trace size exceeded");
        }
        self.traces[self.cur_idx] = trace;
        self.cur_idx += 1;
    }

    func print(self) {
        mut i: usize := 0;
        while i < self.cur_idx : i += 1 {
            trace := self.traces[i];
            console_eprintln(
                "   from {} in \"{}\":{}", demangle_symbol(trace.name),
                trace.file, trace.line
            );
        }
    }

    func clear(mut self) {
        self.cur_idx = 0;
    }
}

#[boxed]
public struct InvalidArgumentError < Throwable {
    msg: string;

    #[inline]
    public func to_string(self) -> string {
        return self.msg;
    }
}

#[boxed]
public struct OutOfMemoryError < Throwable {
    msg: string;

    #[inline]
    public func to_string(self) -> string {
        return self.msg;
    }
}

#[boxed]
public struct ReadFailedError < Throwable {
    msg: string;

    #[inline]
    public func to_string(self) -> string {
        return self.msg;
    }
}
