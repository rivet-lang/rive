// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

use pkg::traits::ToString;

let DIGIT_PAIRS: string = "0010203040506070809001112131415161718191021222324252627"
    "28292031323334353637383930414243444546474849405152535455565758595061626364656667"
    "68696071727374757677787970818283848586878889809192939495969798999";

extend untyped_int for ToString {
    /// Returns the value of `self` as a `string`.
    pub func to_string(self) string {
        return as(i64, self).to_string();
    }
}

extend i8 {
    pub const MAX: i8 = 127;
    pub const MIN: i8 = -128;
}

extend i8 for ToString {
    /// Returns the value of `self` as a `string`.
    pub func to_string(self) string {
        return as(i32, self).str_l(5);
    }
}

extend i16 {
    pub const MAX: i16 = 32767;
    pub const MIN: i16 = -32768;
}

extend i16 for ToString {
    /// Returns the value of `self` as a `string`.
    pub func to_string(self) string {
        return as(i32, self).str_l(7);
    }
}

extend i32 {
    pub const MAX: i32 = 2147483647;
    pub const MIN: i32 = -2147483648;

    /// Returns the string representation of `self` with `max` chars.
    pub func str_l(self, max: i32) string {
        unsafe {
            if (self == 0) {
                return "0";
            }
            let mut n = as(i64, self);
            let mut d: u32 = 0;

            let mut is_neg = false;
            if (n < 0) {
                n = -n;
                is_neg = true;
            }

            let mut index = as(usize, max);
            let buf = as(*mut u8, mem::alloc(as(usize, max + 1)) catch unreachable!());
            buf[index--] = 0;

            while (n > 0) {
                let n1 = as(i32, n / 100);
                d = as(u32, as(i32, n) - (n1 * 100)) << 1;
                n = n1;
                buf[index--] = DIGIT_PAIRS.ptr[d];
                d++;
                buf[index--] = DIGIT_PAIRS.ptr[d];
            }
            index++;

            if (d < 20) {
                // remove head zero
                index++;
            }

            if (is_neg) {
                // prepend `-` if it's negative
                index--;
                buf[index] = b'-';
            }

            let diff = as(usize, max) - index;
            mem::move(buf, buf + index, diff + 1);
            return string::from_raw_with_len(buf, diff);
        }
    }
}

extend i32 for ToString {
    /// Returns the value of `self` as a `string`.
    pub func to_string(self) string {
        return self.str_l(12);
    }
}

extend i64 {
    pub const MAX: i64 = 9223372036854775807;
    pub const MIN: i64 = -9223372036854775808;
}

extend i64 for ToString {
    pub func to_string(self) string {
        unsafe {
            if (self == 0) {
                return "0";
            }
            let mut n = self;
            let mut d: i64 = 0;

            let mut is_neg = false;
            if (n < 0) {
                n = -n;
                is_neg = true;
            }

            let max = 20;
            let buf = as(*mut u8, mem::alloc(as(usize, max + 1)) catch unreachable!());
            let mut index = as(usize, max);
            buf[index--] = 0;

            while (n > 0) {
                let n1 = as(i64, n / 100);
                d = as(i64, as(u32, n - (n1 * 100))) << as(i64, 1);
                n = n1;
                buf[index--] = DIGIT_PAIRS.ptr[as(usize, d)];
                d++;
                buf[index--] = DIGIT_PAIRS.ptr[as(usize, d)];
            }
            index++;

            if (d < 20) {
                // remove head zero
                index++;
            }

            if (is_neg) {
                // prepend `-` if it's negative
                index--;
                buf[index] = b'-';
            }

            let diff = as(usize, max) - index;
            mem::move(buf, buf + index, diff + 1);
            return string::from_raw_with_len(buf, diff);
        }
    }
}

extend isize for ToString {
    /// Returns the value of `self` as a `string`.
    pub func to_string(self) string {
        return as(i64, self).to_string();
    }
}
