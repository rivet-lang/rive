// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ./mem;

var digit_pairs := "0010203040506070809001112131415161718191021222324252627"
    "28292031323334353637383930414243444546474849405152535455565758595061626364656667"
    "68696071727374757677787970818283848586878889809192939495969798999";

extend comptime_int < Stringable {
    /// Returns the value of `self` as a string.
    #[inline]
    pub func to_string(&self) -> string {
        return @as(int64, self.*).to_string();
    }
}

extend int8 < Stringable {
    pub const MAX: int8 := 127;
    pub const MIN: int8 := -128;

    /// Returns the value of `self` as a string.
    #[inline]
    pub func to_string(&self) -> string {
        return @as(int32, self.*).str_l(5);
    }
}

extend int16 < Stringable {
    pub const MAX: int16 := 32767;
    pub const MIN: int16 := -32768;

    /// Returns the value of `self` as a string.
    #[inline]
    pub func to_string(&self) -> string {
        return @as(int32, self.*).str_l(7);
    }
}

extend int32 < Stringable {
    pub const MAX: int32 := 2147483647;
    pub const MIN: int32 := -2147483648;

    /// Returns the string representation of `self` with `max` chars.
    pub func str_l(self, max: int32) -> string {
        if self == 0 {
            return "0";
        }
        mut n := @as(int64, self);
        mut d: uint32 := 0;
        mut is_neg := false;
        if n < 0 {
            n = -n;
            is_neg = true;
        }
        unsafe {
            mut index := @as(uint, max);
            buf := @as([&]mut uint8, mem.boxed_alloc(index + 1));
            buf[index] = 0;
            index -= 1;

            while n > 0 {
                n1 := @as(int32, n / 100);
                d = @as(uint32, @as(int32, n) - (n1 * 100)) << 1;
                n = @as(int64, n1);
                buf[index] = digit_pairs.ptr[d];
                index -= 1;
                d += 1;
                buf[index] = digit_pairs.ptr[d];
                index -= 1;
            }
            index += 1;

            if d < 20 {
                // remove head zero
                index += 1;
            }

            if is_neg {
                // prepend `-` if it's negative
                index -= 1;
                buf[index] = '-';
            }

            diff := @as(uint, max) - index;
            mem.move(buf, @ptr_add(buf, index), diff + 1);
            return string.from_raw_with_len(buf, diff);
        }
    }

    /// Returns the value of `self` as a string.
    #[inline]
    pub func to_string(&self) -> string {
        return self.*.str_l(12);
    }
}

extend int64 < Stringable {
    pub const MAX: int64 := 9223372036854775807;
    pub const MIN: int64 := -9223372036854775808;

    /// Returns the value of `self` as a string.
    pub func to_string(&self) -> string {
        if self.* == 0 {
            return "0";
        }
        mut n := self.*;
        mut d: int64 := 0;

        mut is_neg := false;
        if n < 0 {
            n = -n;
            is_neg = true;
        }
        unsafe {
            max := 20;
            mut index := @as(uint, max);
            buf := @as([&]mut uint8, mem.boxed_alloc(index + 1));
            buf[index] = 0;
            index -= 1;

            while n > 0 {
                n1 := n / 100;
                d = @as(int64, @as(uint32, n - (n1 * 100))) << 1;
                n = n1;
                buf[index] = digit_pairs.ptr[@as(uint, d)];
                index -= 1;
                d += 1;
                buf[index] = digit_pairs.ptr[@as(uint, d)];
                index -= 1;
            }
            index += 1;

            if d < 20 {
                // remove head zero
                index += 1;
            }

            if is_neg {
                // prepend `-` if it's negative
                index -= 1;
                buf[index] = '-';
            }

            diff := @as(uint, max) - index;
            mem.move(buf, @ptr_add(buf, index), diff + 1);
            return string.from_raw_with_len(buf, diff);
        }
    }
}

extend int < Stringable {
    pub const MIN := @as(int, $if _x64_ { int64.MIN } else { int32.MIN });
    pub const MAX := @as(int, $if _x64_ { int64.MAX } else { int32.MAX });

    #[inline]
    pub func bits() -> uint32 {
        return $if _x64_ { 64 } else { 32 };
    }

    /// Returns the value of `self` as a string.
    #[inline]
    pub func to_string(&self) -> string {
        return @as(int64, self.*).to_string();
    }
}
