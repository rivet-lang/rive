// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

pub mod stdc;
pub mod traits;

pub static ARGS: [str] = [""][..];

/// Prints a message to stdout. Unlike `println` stdout is not automatically
/// flushed.
pub fn print(s: str) {
    unsafe {
        write_buf_to_fd(1, s.ptr, s.len);
    }
}

/// Prints a message with a line end to stdout. stdout is flushed.
pub fn println(s: str = "") {
    unsafe {
        writeln_to_fd(1, s);
    }
}

/// Prints a message to stderr. Both stderr and stdout are flushed.
pub fn eprint(s: str) {
    unsafe {
        stdc::fflush(stdc::stdout);
        stdc::fflush(stdc::stderr);
        write_buf_to_fd(2, s.ptr, s.len);
        stdc::fflush(stdc::stderr);
    }
}

/// Prints a message with a line end, to stderr. Both stderr and stdout are
/// flushed.
pub fn eprintln(s: str = "") {
    unsafe {
        stdc::fflush(stdc::stdout);
        stdc::fflush(stdc::stderr);
        writeln_to_fd(2, s);
        stdc::fflush(stdc::stderr);
    }
}

#[used]
fn assert(cond: bool, msg: str) {
    if (!cond) {
        eprint("panic: assertion failed: ");
        eprintln(msg);
        backtrace::print(2);
        process::exit(1);
    }
}

pub fn panic(s: str) no_return {
    eprint("panic: ");
    eprintln(s);
    backtrace::print(1);
    process::exit(1);
}

#[used]
fn panic_if(cond: bool, msg: str) {
    if (cond) {
        eprint("panic: ");
        eprintln(msg);
        backtrace::print(2);
        process::exit(1);
    }
}

unsafe fn write_buf_to_fd(fd: i32, buf_: *const u8, len: usize) {
    let mut buf = buf_;
    let mut remaining_bytes = len;
    while (remaining_bytes > 0) {
        unsafe {
            let x = stdc::write(fd, buf, remaining_bytes);
            buf += x;
            remaining_bytes -= as(usize, x);
        }
    }
}

unsafe fn writeln_to_fd(fd: i32, s: str) {
    unsafe {
        write_buf_to_fd(fd, s.ptr, s.len);
        write_buf_to_fd(fd, c"\n", 1);
    }
}
