// Copyright (C) 2023 Jose Mendoza. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import c/libc;

/// Allocates dynamically a `size` bytes block of memory on the heap.
/// Returns a pointer to the memory address of the allocated space.
///
/// NOTE: Unlike the `mem.zeroed` function, `mem.alloc` will
/// not zero the memory block.
pub func mem_alloc(size: uint) -> rawptr {
    if new := unsafe { libc.malloc(size) } {
        return new;
    }
    runtime_error("cannot allocate memory" #if _DEBUG_ " (size: {})", size #endif);
}

/// Allocates dynamically a zeroed `size` bytes block of memory on the heap.
/// Returns a pointer to the memory address of the allocated space.
pub func mem_zeroed(size: uint) -> rawptr {
    if ptr := unsafe { libc.calloc(1, size) } {
        return ptr;
    }
    runtime_error("cannot allocate zeroed memory" #if _DEBUG_ " (size: {})", size #endif);
}

/// Resizes the memory block `ptr` with `sz` bytes.
///
/// NOTE: The `ptr` must be a pointer to an existing memory block previously
/// allocated with `mem.alloc` or `mem.zeroed`.
pub func mem_resize(ptr: ?rawptr, sz: uint) -> rawptr {
    if sz == 0 {
        if safe_ptr := ptr {
            unsafe {
                mem_dealloc(safe_ptr);
            }
        }
        runtime_error("argument `sz` is 0");
    }
    if new := unsafe { libc.realloc(ptr, sz) } {
        return new;
    }
    runtime_error("cannot resize memory" #if _DEBUG_ " (size: {})", sz #endif);
}

/// Fills the first `n` bytes of the memory area pointed to by `s`, with the
/// byte `c`.
#[inline]
pub func mem_set(s: rawptr, c: uint8, n: uint) {
    unsafe {
        _ = libc.memset(s, @as(int32, c), n);
    }
}

/// Copies `sz` bytes from memory area `src` to memory area `dest`.
///
/// NOTE: The memory areas *MUST NOT OVERLAP*. Use `mem.move`, if the memory
/// areas do overlap.
#[inline]
pub func mem_copy(dest: rawptr, src: rawptr, sz: uint) {
    unsafe {
        _ = libc.memcpy(dest, src, sz);
    }
}

/// Copies `sz` bytes from memory area `src` to memory area `dest`.
///
/// NOTE: The memory areas *MAY* overlap: copying takes place as though the bytes
/// in `src` are first copied into a temporary array that does not overlap
/// `src` or `dest`, and the bytes are then copied from the temporary array to
/// `dest`.
#[inline]
pub func mem_move(dest: rawptr, src: rawptr, sz: uint) {
    unsafe {
        _ = libc.memmove(dest, src, sz);
    }
}

/// Allocates dynamically a `sz` bytes block of memory on the heap, then copies
/// the contents of `src` into the allocated space and returns a pointer to
/// the newly allocated space.
pub func mem_dup(src: rawptr, sz: uint) -> rawptr {
    if sz == 0 {
        return mem_zeroed(1);
    }
    new := mem_alloc(sz);
    mem_copy(new, src, sz);
    return new;
}

/// Compares the first `n` bytes (each interpreted as `uint8`) of the memory areas
/// `s1` and `s2`. It returns an integer less than, equal to, or greater than
/// zero, if the first n bytes of `s1` is found, respectively, to be less than,
/// to match, or be greater than the first n bytes of `s2`.
///
/// For a nonzero return value, the sign is determined by the sign of the
/// difference between the first pair of bytes (interpreted as `uint8`) that
/// differ in `s1` and `s2`.
///
/// If n is zero, the return value is zero.
///
/// NOTE: Do NOT use `mem.cmp` to compare security critical data, such
/// as cryptographic secrets, because the required CPU time depends on the
/// number of equal bytes. You should use a function that performs comparisons
/// in constant time for this.
#[inline]
pub func mem_cmp(s1: rawptr, s2: rawptr, n: uint) -> int32 {
    return unsafe { libc.memcmp(s1, s2, n) };
}

/// Deallocates manually the memory referenced by `ptr`.
#[unsafe; inline]
pub func mem_dealloc(ptr: rawptr) {
    unsafe {
        libc.free(ptr);
    }
}
