// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import c/libc;

/// Allocates dynamically a `size` bytes block of memory on the heap.
/// Returns a pointer to the memory address of the allocated space.
///
/// NOTE: Unlike the `mem.zeroed` function, `mem.alloc` will
/// not zero the memory block.
public func mem_alloc(size: usize) !mut_anyptr {
    if new := unsafe { libc.malloc(size) } {
        return new;
    }
    return OutOfMemoryError("insufficient memory is available");
}

/// Allocates dynamically a zeroed `n` bytes block of memory on the heap.
/// Returns a pointer to the memory address of the allocated space.
public func mem_zeroed(n: usize) !mut_anyptr {
    new := mem_alloc(n)!;
    mem_set(new, 0, n);
    return new;
}

/// Resizes the memory block `ptr` with `sz` bytes.
///
/// NOTE: The `ptr` must be a pointer to an existing memory block previously
/// allocated with `mem.alloc` or `mem.zeroed`.
public func mem_resize(ptr: ?mut_anyptr, sz: usize) !mut_anyptr {
    if sz == 0 {
        if safe_ptr := ptr {
            unsafe {
                mem_dealloc(safe_ptr);
            }
        }
        return InvalidArgumentError("argument `sz` is 0");
    }
    if new := unsafe { libc.realloc(ptr, sz) } {
        return new;
    }
    return OutOfMemoryError("insufficient memory is available");
}

/// Fills the first `n` bytes of the memory area pointed to by `s`, with the
/// byte `c`.
[inline]
public func mem_set(s: mut_anyptr, c: uint8, n: usize) {
    unsafe {
        _ = libc.memset(s, @cast(int32, c), n);
    }
}

/// Copies `sz` bytes from memory area `src` to memory area `dest`.
///
/// NOTE: The memory areas *MUST NOT OVERLAP*. Use `mem.move`, if the memory
/// areas do overlap.
[inline]
public func mem_copy(dest: mut_anyptr, src: anyptr, sz: usize) {
    unsafe {
        _ = libc.memcpy(dest, src, sz);
    }
}

/// Copies `sz` bytes from memory area `src` to memory area `dest`.
///
/// NOTE: The memory areas *MAY* overlap: copying takes place as though the bytes
/// in `src` are first copied into a temporary array that does not overlap
/// `src` or `dest`, and the bytes are then copied from the temporary array to
/// `dest`.
[inline]
public func mem_move(dest: mut_anyptr, src: anyptr, sz: usize) {
    unsafe {
        _ = libc.memmove(dest, src, sz);
    }
}

/// Allocates dynamically a `sz` bytes block of memory on the heap, then copies
/// the contents of `src` into the allocated space and returns a pointer to
/// the newly allocated space.
public func mem_dup(src: anyptr, sz: usize) !mut_anyptr {
    if sz == 0 {
        return mem_zeroed(1)!;
    }
    new := mem_alloc(sz)!;
    mem_copy(new, src, sz);
    return new;
}

/// Compares the first `n` bytes (each interpreted as `uint8`) of the memory areas
/// `s1` and `s2`. It returns an integer less than, equal to, or greater than
/// zero, if the first n bytes of `s1` is found, respectively, to be less than,
/// to match, or be greater than the first n bytes of `s2`.
///
/// For a nonzero return value, the sign is determined by the sign of the
/// difference between the first pair of bytes (interpreted as `uint8`) that
/// differ in `s1` and `s2`.
///
/// If n is zero, the return value is zero.
///
/// NOTE: Do NOT use `mem.cmp` to compare security critical data, such
/// as cryptographic secrets, because the required CPU time depends on the
/// number of equal bytes. You should use a function that performs comparisons
/// in constant time for this.
[inline]
public func mem_cmp(s1: anyptr, s2: anyptr, n: usize) int32 {
    return unsafe { libc.memcmp(s1, s2, n) };
}

/// Deallocates manually the memory referenced by `ptr`.
[unsafe; inline]
public func mem_dealloc(ptr: anyptr) {
    unsafe {
        libc.free(ptr);
    }
}
