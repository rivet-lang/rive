// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

const MAX_ONE_B: uint32 = 0x80;
const MAX_TWO_B: uint32 = 0x800;
const MAX_THREE_B: uint32 = 0x10000;

extend rune : Stringable {
    /// The highest valid codepoint a `rune` can have.
    public const MAX: rune = 0x10FFFF;

    /// `U+FFFD REPLACEMENT CHARACTER` (ï¿½) is used in Unicode to represent a
    /// decoding error.
    public const REPLACEMENT_CHARACTER: rune = 0xFFFD;

    public func from_uint32(i: uint32) !rune {
        if (i > @cast(uint32, Self.MAX)) or (i >= 0xD800 and i <= 0xDFFF) {
            return RuneFromU32Error();
        }
        return @cast(rune, i);
    }

    /// Converts a digit in the given radix to a `rune`.
    ///
    /// A 'radix' here is sometimes also called a 'base'. A radix of two
    /// indicates a binary number, a radix of ten, decimal, and a radix of
    /// sixteen, hexadecimal, to give some common values. Arbitrary
    /// radices are supported.
    ///
    /// `rune_from_digit()` will return `none` if the input is not a digit
    /// in the given radix.
    ///
    /// Panics if given a radix larger than 36.
    public func from_digit(num: uint32, radix: uint32) ?rune {
        if radix > 36 {
            process_panic("rune.from_digit: radix is too high (maximum 36)");
        }
        if num < radix {
            num_ := @cast(uint8, num);
            return if num_ < 10 {
                @cast(rune, b'0' + num_)
            } else {
                @cast(rune, b'a' + num_ - 10)
            };
        }
        return none;
    }

    public func as_bytes(self) []uint8 {
        res := @vec(uint8, 5);
        res_v := @cast(Vector, res);
        res_v.len = utf32_decode_to_buffer(self, @cast([*]mut uint8, res_v.ptr));
        return res;
    }

    [inline]
    public func len_utf8(self) usize {
        return len_utf8(@cast(uint32, self));
    }

    public func to_string(&self) string {
        len := self.len_utf8();
        res := @cast([*]mut uint8, internal_alloc(len + 1));
        _ = unsafe { utf32_decode_to_buffer(self.*, res) };
        return string(unsafe { res }, len);
    }
}

func utf32_decode_to_buffer(code: rune, buffer: [*]mut uint8) usize {
    icode := @cast(int32, code);
    unsafe {
        return switch {
            (icode <= 127) => { // 0x7F
                buffer[0] = @cast(uint8, icode);
                1
            },
            (icode <= 2047) => { // 0x7FF
                buffer[0] = @cast(uint8, 192) | @cast(uint8, icode >> 6); // 0xC0 - 110xxxxx
                buffer[1] = @cast(uint8, 128) | @cast(uint8, icode & 63); // 0x80 - 0x3F - 10xxxxxx
                2
            },
            (icode <= 65535) => { // 0xFFFF
                buffer[0] = @cast(uint8, 224) | @cast(uint8, icode >> 12); // 0xE0 - 1110xxxx
                buffer[1] = @cast(uint8, 128) | (@cast(uint8, icode >> 6) & 63); // 0x80 - 0x3F - 10xxxxxx
                buffer[2] = @cast(uint8, 128) | @cast(uint8, icode & 63); // 0x80 - 0x3F - 10xxxxxx
                3
            },
            (icode <= 1114111) => { // 0x10FFFF
                buffer[0] = @cast(uint8, 240) | @cast(uint8, icode >> 18); // 0xF0 - 11110xxx
                buffer[1] = @cast(uint8, 128) | (@cast(uint8, icode >> 12) & 63); // 0x80 - 0x3F - 10xxxxxx
                buffer[2] = @cast(uint8, 128) | (@cast(uint8, icode >> 6) & 63); // 0x80 - 0x3F - 10xxxxxx
                buffer[3] = @cast(uint8, 128) | @cast(uint8, icode & 63); // 0x80 - 0x3F - 10xxxxxx
                4
            },
            else => 0
        };
    }
}

[inline]
func len_utf8(code: uint32) usize {
    return if code < MAX_ONE_B {
        1
    } else if code < MAX_TWO_B {
        2
    } else if code < MAX_THREE_B {
        3
    } else {
        4
    };
}

[boxed]
public struct TooManyBytesError : Error {
    msg: string;

    public func to_string(self) string {
        return self.msg;
    }
}

// convert array of utf8 bytes to single utf32 value will error if more than
// 4 bytes are submitted
func utf8_to_utf32(bytes: []uint8) !rune {
    if bytes.len == 0 {
        return 0;
    }
    // return ASCII unchanged
    if bytes.len == 1 {
        return @cast(rune, bytes[0]);
    }
    if bytes.len > 4 {
        return TooManyBytesError(
            "attempted to decode too many bytes, utf-8 is limited to four bytes maximum"
        );
    }
    mut res := @cast(rune, bytes[0] << bytes.len);
    mut shift := 6 - bytes.len;
    mut i: usize := 1;
    while i < bytes.len : i += 1 {
        c := @cast(rune, bytes[i]);
        res = @cast(rune, @cast(uint32, res) << shift);
        res |= c & 63; // 0x3F
        shift = 6;
    }
    return res;
}
