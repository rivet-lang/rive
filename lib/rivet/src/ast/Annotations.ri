// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import ../token;

#[boxed]
public struct AttributeDuplicatedError < Throwable {
    name: string;

    public func to_string(self) -> string {
        return "duplicated attribute `{}`".fmt(self.name);
    }
}

#[boxed]
public struct AttributeArgument {
    public name: string;
    public expr: Expr;
    public is_named: bool;
}

#[boxed]
public struct Attribute {
    public name: string;
    public args: []AttributeArgument;
    public pos: token.Pos;

    public func find_argument(self, name: string) -> ?AttributeArgument {
        for arg in self.args {
            if arg.name == name {
                return arg;
            }
        }
        return none;
    }
}

#[boxed]
public struct Attributes {
    public mut attributes: []Attribute;

    public func add(mut self, attribute: Attribute) -> ! {
        if self.has(attribute.name) {
            throw AttributeDuplicatedError(attribute.name);
        }
        self.attributes.push(attribute);
    }

    public func find(self, name: string) -> ?Attribute {
        for attribute in self.attributes {
            if attribute.name == name {
                return attribute;
            }
        }
        return none;
    }

    #[inline]
    public func has(self, name: string) -> bool {
        return self.find(name) !is none;
    }

    #[inline]
    public func is_empty(self) -> bool {
        return self.attributes.is_empty();
    }
}
