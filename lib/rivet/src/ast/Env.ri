// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import std/{ sys, fs.Path };

import ../{ prefs, report, utils };

#[boxed]
pub struct Env {
    pub prefs: +mut prefs.Prefs;
    pub mut source_files: []+mut SourceFile;

    pub pointer_size: uint;

    /// `.universe` is the mega-module where all the modules being
    /// compiled reside.
    pub universe: +mut Module;

    pub mut c_defines: CDefines;
    pub mut c_include_paths: []string;
    pub mut c_user_include_paths: []string;

    pub core_mod: +mut Module;
    pub mut throwable_sym_is_set: bool;

    pub mut builtins: []Builtin;

    /// Primitive type, Throwable and DynArray symbols.
    pub mut bool_sym: +mut TypeSym;
    pub mut rune_sym: +mut TypeSym;
    pub mut int8_sym: +mut TypeSym;
    pub mut int16_sym: +mut TypeSym;
    pub mut int32_sym: +mut TypeSym;
    pub mut int64_sym: +mut TypeSym;
    pub mut int_sym: +mut TypeSym;
    pub mut uint8_sym: +mut TypeSym;
    pub mut uint16_sym: +mut TypeSym;
    pub mut uint32_sym: +mut TypeSym;
    pub mut uint64_sym: +mut TypeSym;
    pub mut uint_sym: +mut TypeSym;
    pub mut comptime_int_sym: +mut TypeSym;
    pub mut comptime_float_sym: +mut TypeSym;
    pub mut float32_sym: +mut TypeSym;
    pub mut float64_sym: +mut TypeSym;
    pub mut string_sym: +mut TypeSym;
    pub mut throwable_sym: +mut TypeSym;
    pub mut dyn_array_sym: +mut TypeSym; // from `core` module

    /// Primitive types.
    pub mut bool_t: +mut Type;
    pub mut rune_t: +mut Type;
    pub mut int8_t: +mut Type;
    pub mut int16_t: +mut Type;
    pub mut int32_t: +mut Type;
    pub mut int64_t: +mut Type;
    pub mut int_t: +mut Type;
    pub mut uint8_t: +mut Type;
    pub mut uint16_t: +mut Type;
    pub mut uint32_t: +mut Type;
    pub mut uint64_t: +mut Type;
    pub mut uint_t: +mut Type;
    pub mut comptime_int_t: +mut Type;
    pub mut comptime_float_t: +mut Type;
    pub mut float32_t: +mut Type;
    pub mut float64_t: +mut Type;
    pub mut string_t: +mut Type;
    pub mut throwable_t: +mut Type;

    #[inline]
    pub func new(prefs_: +mut prefs.Prefs) -> +mut Self {
        report.report_table.prefs = prefs_;
        universe_ := universe();
        env := +mut Env(
            prefs: prefs_,
            universe: universe_,
            pointer_size: if prefs_.target_is_64bit {
                8
            } else {
                4
            }
        );
        env.setup_primitives();
        env.setup_builtins();
        return env;
    }

    pub func setup_primitives(+mut self) {
        self.bool_sym = self.universe.scope.find_type_symbol_by_index_or_panic(0);
        self.rune_sym = self.universe.scope.find_type_symbol_by_index_or_panic(1);
        self.int8_sym = self.universe.scope.find_type_symbol_by_index_or_panic(2);
        self.int16_sym = self.universe.scope.find_type_symbol_by_index_or_panic(3);
        self.int32_sym = self.universe.scope.find_type_symbol_by_index_or_panic(4);
        self.int64_sym = self.universe.scope.find_type_symbol_by_index_or_panic(5);
        self.int_sym = self.universe.scope.find_type_symbol_by_index_or_panic(6);
        self.uint8_sym = self.universe.scope.find_type_symbol_by_index_or_panic(7);
        self.uint16_sym = self.universe.scope.find_type_symbol_by_index_or_panic(8);
        self.uint32_sym = self.universe.scope.find_type_symbol_by_index_or_panic(9);
        self.uint64_sym = self.universe.scope.find_type_symbol_by_index_or_panic(10);
        self.uint_sym = self.universe.scope.find_type_symbol_by_index_or_panic(11);
        self.comptime_int_sym = self.universe.scope.find_type_symbol_by_index_or_panic(12);
        self.comptime_float_sym = self.universe.scope.find_type_symbol_by_index_or_panic(13);
        self.float32_sym = self.universe.scope.find_type_symbol_by_index_or_panic(14);
        self.float64_sym = self.universe.scope.find_type_symbol_by_index_or_panic(15);
        self.string_sym = self.universe.scope.find_type_symbol_by_index_or_panic(16);

        self.bool_t = +mut .Basic(self.bool_sym);
        self.rune_t = +mut .Basic(self.rune_sym);
        self.int8_t = +mut .Basic(self.int8_sym);
        self.int16_t = +mut .Basic(self.int16_sym);
        self.int32_t = +mut .Basic(self.int32_sym);
        self.int64_t = +mut .Basic(self.int64_sym);
        self.int_t = +mut .Basic(self.int_sym);
        self.uint8_t = +mut .Basic(self.uint8_sym);
        self.uint16_t = +mut .Basic(self.uint16_sym);
        self.uint32_t = +mut .Basic(self.uint32_sym);
        self.uint64_t = +mut .Basic(self.uint64_sym);
        self.uint_t = +mut .Basic(self.uint_sym);
        self.comptime_int_t = +mut .Basic(self.comptime_int_sym);
        self.comptime_float_t = +mut .Basic(self.comptime_float_sym);
        self.float32_t = +mut .Basic(self.float32_sym);
        self.float64_t = +mut .Basic(self.float64_sym);
        self.string_t = +mut .Basic(self.string_sym);
    }

    pub func filter_files(+self, inputs: []string) -> []string {
        mut new_inputs := []string(cap: inputs.len);
        for input in inputs {
            base_name_input := Path.base_name(input);
            if base_name_input.count(".") == 1 {
                new_inputs.push(input);
                continue;
            }
            exts := base_name_input.substr(end: base_name_input.len - 3).split(".")[1:];
            mut already_exts := []string(cap: exts.len);
            mut should_compile := false;
            for ext in exts {
                if ext in already_exts {
                    utils.error("{}: duplicate special extension `{}`", input, ext);
                }
                already_exts.push(ext);
                if ext.starts_with("d_") || ext.starts_with("notd_") {
                    should_compile = if ext.starts_with("d_") {
                        ext.substr(2) in self.prefs.flags
                    } else {
                        ext.substr(4) !in self.prefs.flags
                    };
                } else if os := sys.OS.from_string(ext) {
                    should_compile = self.prefs.target_os == os;
                } else if arch := sys.Arch.from_string(ext) {
                    should_compile = self.prefs.target_arch == arch;
                } else if ext in ["x32", "x64"] {
                    should_compile = if ext == "x32" {
                        !self.prefs.target_is_64bit
                    } else {
                        self.prefs.target_is_64bit
                    };
                } else if ext in ["little_endian", "big_endian"] {
                    should_compile = if ext == "little_endian" {
                        self.prefs.target_is_little_endian
                    } else {
                        !self.prefs.target_is_little_endian
                    };
                } else if backend := prefs.Backend.from_string(ext) {
                    should_compile = backend == self.prefs.target_backend;
                } else {
                    utils.error("{}: unknown special extension `{}`", input, ext);
                }
                if !should_compile {
                    break;
                }
            }
            if should_compile {
                new_inputs.push(input);
            }
        }
        return new_inputs;
    }
}
