// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import ../token;

pub enum Stmt {
    Empty: token.Pos,
    Comment: Comment,
    Expr: Expr,
    VarDecl {
        lefts: []ObjectData;
        mut right: Expr;
        mut scope: Scope;
        pos: token.Pos;
    },
    While {
        mut cond: Expr;
        mut continue_expr: Expr;
        has_continue_expr: bool;
        mut stmt: Stmt;
        mut else_stmt: Stmt;
        has_else_stmt: bool;
        is_inf: bool;
        pos: token.Pos;
    },
    For {
        mut index: ObjectData;
        has_index: bool;
        values: []ObjectData;
        mut iterable: Expr;
        mut stmt: Stmt;
        mut scope: Scope;
        pos: token.Pos;
    },
    Defer {
        pub enum Mode {
            Normal,
            Error,
            Success
        }
        mut expr: Expr;
        mode: Mode;
        pos: token.Pos;
    };

    pub func position(self) -> token.Pos {
        return match self is {
            .Empty as empty_pos -> empty_pos,
            .Comment as comment -> comment.pos,
            .Expr as expr -> expr.position(),
            .VarDecl as var_decl -> var_decl.pos,
            .While as while_stmt -> while_stmt.pos,
            .For as for_stmt -> for_stmt.pos,
            .Defer as defer_stmt -> defer_stmt.pos
        };
    }
}
