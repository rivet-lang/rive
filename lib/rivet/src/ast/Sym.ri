// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import std/traits;
import std/strings;

import ../token;

var mut symIds: uint := 0;

func new_sym_id() -> uint {
    res := symIds;
    symIds += 1;
    return res;
}

#[boxed]
pub struct DuplicateSymbolError < Throwable {
    msg: string;

    pub func to_string(self) -> string {
        return self.msg;
    }
}

pub enum ABI as uint8 < traits.Stringable {
    Rivet,
    C;

    #[inline]
    pub func from_string(abi: string) -> ?ABI {
        return match abi {
            "C" -> .C,
            "Rivet" -> .Rivet,
            else -> none
        };
    }

    // TODO: remove, use `.to_lower()` instead
    #[inline]
    pub func lower(&self) -> string {
        return match self.* {
            .C -> "c",
            .Rivet -> "rivet"
        };
    }

    #[inline]
    pub func to_string(&self) -> string {
        return match self.* {
            .C -> "C",
            .Rivet -> "Rivet"
        };
    }
}

#[default_value(InvalidSym())]
pub trait Sym {
    id: uint := new_sym_id();
    abi: ABI;
    mut is_public: bool;
    mut parent: ?Sym;
    name: string;
    mut scope: Scope;
    mut mangled_name: string;
    mut qualified_name: string;
    is_universe: bool;
    is_root: bool;
    mut is_generated: bool;

    func module(self) -> ?Module {
        mut p := self;
        while {
            if p is Module as mod {
                return mod;
            } else if p_ := p.parent {
                p = p_;
            } else {
                break;
            }
        }
        return none;
    }

    func has_access_to(self, other: Sym) -> bool {
        self_mod := self.module() ?? return false;
        other_mod := other.module() ?? return false;
        return (
            other_mod.is_universe or self_mod.id == other.id
            or self_mod.id == other_mod.id
            or self_mod.id == other_mod.parent?.id
            or self_mod.parent?.id == other.parent?.id
        );
    }

    func type_of(self) -> string {
        return match self is {
            SymRef as sym_ref -> if sym_ref.ref_resolved {
                sym_ref.ref.type_of()
            } else {
                "alias"
            },
            Module -> "module",
            Const -> "constant",
            Var as obj -> match obj.level {
                .Receiver, .Argument -> "argument",
                else -> "variable"
            },
            TypeSym as type_sym -> if type_sym.info.is_compound() {
                type_sym.info.to_string()
            } else {
                "type"
            },
            Func as func_info -> func_info.kind(),
            else -> "unknown symbol kind"
        };
    }

    func qualname(self) -> string {
        return if parent := self.parent {
            if parent.is_universe {
                self.name
            } else {
                parent.qualname().concat(".", self.name)
            }
        } else {
            self.name
        };
    }

    func ==(self, other: Self) -> bool {
        return self.id == other.id;
    }

    func !=(self, other: Self) -> bool {
        return self.id != other.id;
    }
}

#[boxed]
pub struct InvalidSym < Sym {}

#[boxed]
pub struct SymRef < Sym {
    pub mut ref: Sym;
    pub mut ref_expr: Expr;
    pub mut ref_resolved: bool;
}

#[boxed]
pub struct Module < Sym {
    pub func add_or_get_array(mut self, elem_typ: Type, size: uint, is_mut: bool) -> TypeSym {
        unique_name := if is_mut {
            "[{}]mut {}"
        } else {
            "[{}]{}"
        }.fmt(size, elem_typ.to_qualstring());
        if type_sym := self.scope.find(unique_name) {
            return @as(TypeSym, type_sym);
        }
        type_sym := TypeSym(
            is_public: true,
            name: unique_name,
            info: .Array(elem_typ, size, is_mut),
            fields: [
                Field(
                    name: "len", is_public: true,
                    type: .Basic(self.scope.find_type_symbol_by_index_or_panic(11))
                )
            ]
        );
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    pub func add_or_get_vec(mut self, elem_type: Type, is_mut: bool) -> TypeSym {
        unique_name := if is_mut { "[]mut " } else { "[]" }.concat(elem_type.to_qualstring());
        if type_sym := self.scope.find(unique_name) {
            return @as(TypeSym, type_sym);
        }
        type_sym := TypeSym(
            is_public: true,
            name: unique_name,
            info: .DynArray(elem_type, is_mut),
            fields: [
                Field(
                    name: "len", is_public: true,
                    type: .Basic(self.scope.find_type_symbol_by_index_or_panic(11))
                ),
                Field(
                    name: "cap", is_public: true,
                    type: .Basic(self.scope.find_type_symbol_by_index_or_panic(11))
                )
            ]
        );
        type_sym.scope.add(Func(
            name: "push",
            is_public: true,
            is_method: true,
            self_type: .Basic(type_sym),
            self_is_mut: true,
            args: [Arg("value", type: elem_type, pos: token.noPos)],
            ret_type: .Void(),
            has_body: true
        )) catch {};
        type_sym.scope.add(Func(
            name: "pop",
            is_public: true,
            is_method: true,
            self_type: .Basic(type_sym),
            self_is_mut: true,
            ret_type: elem_type,
            has_body: true
        )) catch {};
        type_sym.scope.add(Func(
            name: "clone",
            is_public: true,
            is_method: true,
            self_type: .Basic(type_sym),
            ret_type: .Basic(type_sym),
            has_body: true
        )) catch {};
        if dyn_array_sym := self.scope.find("core")?.scope.find("DynArray") {
            type_sym.scope.add(dyn_array_sym.scope.find("is_empty")?) catch {};
            type_sym.scope.add(dyn_array_sym.scope.find("delete")?) catch {};
            type_sym.scope.add(dyn_array_sym.scope.find("trim")?) catch {};
            type_sym.scope.add(dyn_array_sym.scope.find("clear")?) catch {};
        }
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    pub func add_or_get_tuple(mut self, types: []Type) -> TypeSym {
        unique_name := {
            mut sb := strings.Builder.from_string("(");
            for i, type in types {
                sb.write_string(type.to_qualstring());
                if i < types.len - 1 {
                    sb.write_string(", ");
                }
            }
            sb.write_byte(b')');
            sb.to_string()
        };
        if type_sym := self.scope.find(unique_name) {
            return @as(TypeSym, type_sym);
        }
        mut fields := @vec(Field);
        for i, type in types {
            fields.push(Field(name: i.to_string(), is_public: true, type: type));
        }
        type_sym := TypeSym(
            is_public: true, name: unique_name, info: .Tuple(types), fields: fields
        );
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    pub func add_or_get_func(mut self, func_sym: Func) -> TypeSym {
        unique_name := {
            mut sb := strings.Builder.from_string("func(");
            if func_sym.is_method {
                if func_sym.self_is_mut {
                    sb.write_string("mut ");
                } else if func_sym.self_is_ptr {
                    sb.write_string("&");
                }
                sb.write_string("self");
            }
            for i, arg in func_sym.args {
                sb.write_string(arg.type.to_qualstring());
                if i < func_sym.args.len - 1 {
                    sb.write_string(", ");
                }
            }
            sb.write_byte(b')');
            if func_sym.ret_type !is .Void {
                sb.write_fmt(" {}", func_sym.ret_type.to_qualstring());
            }
            sb.to_string()
        };
        if type_sym := self.scope.find(unique_name) {
            return @as(TypeSym, type_sym);
        }
        type_sym := TypeSym(is_public: true, name: unique_name, info: .Func(func_sym));
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    #[inline]
    pub func is_core(self) -> bool {
        return self.name == "core";
    }
}

#[boxed]
pub struct Const < Sym {
    pub mut expr: Expr;
    pub mut evaled_expr: Expr;
    pub mut has_evaled_size: bool;
    pub mut evaled_size: int;
    pub mut type: Type;
    pub pos: token.Pos;
}

pub enum VarLevel as uint8 {
    Module, // module-level, `var`
    Receiver, // receiver: `self`
    Argument, // argument-level
    Local // inside function
}

#[boxed]
pub struct Var < Sym {
    pub is_extern: bool;
    pub is_mut: bool;
    pub mut is_used: bool;
    pub mut is_changed: bool;
    pub mut is_hidden_ref: bool;
    pub mut mir_name: string; // used for duplicate objects in MIR
    pub level: VarLevel;
    pub mut type: Type;
    pub pos: token.Pos;
}

#[boxed]
pub struct Arg {
    pub name: string;
    pub is_mut: bool;
    pub is_self: bool;
    pub has_def_expr: bool;
    pub mut def_expr: Expr;
    pub pos: token.Pos;
    pub mut type: Type;
}

#[boxed]
pub struct Func < Sym {
    pub is_extern: bool;
    pub is_unsafe: bool;
    pub is_main: bool;
    pub is_method: bool;
    pub is_special_method: bool;
    pub is_variadic: bool;
    pub mut self_type: Type;
    pub self_is_mut: bool;
    pub self_is_ptr: bool;
    pub self_pos: token.Pos;
    pub args: []Arg;
    pub has_named_args: bool;
    pub has_body: bool;
    pub ret_type: Type;
    pub pos: token.Pos;

    pub func get_arg(self, idx: uint) -> Arg {
        arg := self.args[idx];
        return if arg.is_self {
            self.args[idx + 1]
        } else {
            arg
        };
    }

    pub func args_len(self) -> uint {
        mut len: uint := 0;
        for arg in self.args {
            if !(arg.is_self or arg.type is .Variadic) {
                len += 1;
            }
        }
        return len;
    }

    #[inline]
    pub func kind(self) -> string {
        return if self.is_method {
            if self.is_special_method {
                "special method"
            } else {
                "method"
            }
        } else {
            "function"
        };
    }

	#[inline]
    pub func type(self, mut universe: Module) -> Type {
        return .Func(
            is_method: self.is_method,
            self_is_mut: self.self_is_mut,
            self_is_ptr: self.self_is_ptr,
            args: self.args,
            ret_type: self.ret_type,
            sym: universe.add_or_get_func(self),
            has_sym: true,
            pos: token.noPos
        );
    }
}
