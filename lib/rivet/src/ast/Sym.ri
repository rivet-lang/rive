// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import std/{ traits, strings };

import ../token;

var mut sym_ids: uint := 0;

func new_sym_id() -> uint {
    res := sym_ids;
    sym_ids += 1;
    return res;
}

#[boxed]
pub struct DuplicateSymbolError < Throwable {
    msg: string;

    pub func to_string(^self) -> string {
        return self.msg;
    }
}

pub enum ABI as uint8 < traits.Stringable {
    Rivet,
    C;

    #[inline]
    pub func from_string(abi: string) -> ?ABI {
        return match abi {
            "C" -> .C,
            "Rivet" -> .Rivet,
            else -> none
        };
    }

    // TODO: remove, use `.to_lower()` instead
    #[inline]
    pub func lower(&self) -> string {
        return match self.* {
            .C -> "c",
            .Rivet -> "rivet"
        };
    }

    #[inline]
    pub func to_string(&self) -> string {
        return match self.* {
            .C -> "C",
            .Rivet -> "Rivet"
        };
    }
}

pub trait Sym {
    id: uint := new_sym_id();
    abi: ABI;
    mut is_public: bool;
    parent: ?^mut Sym;
    name: string;
    mut scope: ^mut Scope;
    mut mangled_name: string;
    mut qualified_name: string;
    is_universe: bool;
    is_root: bool;
    mut is_generated: bool;

    func module(^mut self) -> ?^mut Module {
        mut p := self;
        while {
            if p is Module(mut mod) {
                return mod;
            } else if p_ := p.parent {
                p = p_;
            } else {
                break;
            }
        }
        return none;
    }

    func has_access_to(^self, other: ^mut Sym) -> bool {
        self_mod := self.module() ?? return false;
        other_mod := other.module() ?? return false;
        return (
            other_mod.is_universe || self_mod.id == other.id
            || self_mod.id == other_mod.id || self_mod.id == other_mod.parent?.id
            || self_mod.parent?.id == other.parent?.id
        );
    }

    func type_of(^self) -> string {
        return match self is {
            SymRef(sym_ref) -> if sym_ref.ref_resolved {
                sym_ref.ref.type_of()
            } else {
                "alias"
            },
            Module -> "module",
            Const -> "constant",
            Var(obj) -> match obj.level {
                .Receiver, .Argument -> "argument",
                else -> "variable"
            },
            TypeSym(type_sym) -> if type_sym.info.is_compound() {
                type_sym.info.to_string()
            } else {
                "type"
            },
            Func(func_info) -> func_info.kind(),
            else -> "unknown symbol kind"
        };
    }

    func qualname(^self) -> string {
        return if parent := self.parent {
            if parent.is_universe {
                self.name
            } else {
                parent.qualname().concat(".", self.name)
            }
        } else {
            self.name
        };
    }

    func ==(^self, other: ^Self) -> bool {
        return self.id == other.id;
    }

    func !=(^self, other: ^Self) -> bool {
        return self.id != other.id;
    }
}

#[boxed]
pub struct InvalidSym < Sym {}

#[boxed]
pub struct SymRef < Sym {
    pub mut ref: ^mut Sym;
    pub mut ref_expr: ^mut Expr;
    pub mut ref_resolved: bool;
}

#[boxed]
pub struct Module < Sym {
    pub func add_or_get_slice(^mut self, elem_type: ^mut Type, is_mut: bool) -> ^mut TypeSym {
        unique_name := if is_mut { "[:]mut " } else { "[:]" }.concat(elem_type.to_qualstring());
        if type_sym := self.scope.find(unique_name) {
            return @as(^mut TypeSym, type_sym);
        }
        type_sym := ^mut TypeSym(
            is_public: true,
            name: unique_name,
            info: ^mut .Slice(elem_type, is_mut),
            fields: ^[
                ^mut Field(
                    name: "len", is_public: true,
                    type: ^mut .Basic(self.scope.find_type_symbol_by_index_or_panic(11))
                )
            ]
        );
        type_sym.scope.add(^Func(
            name: "to_dynamic_array",
            is_public: true,
            is_method: true,
            self_is_ptr: true,
            self_type: ^mut .Basic(type_sym),
            ret_type: ^mut .Basic(self.add_or_get_dyn_array(elem_type, is_mut)),
            has_body: true
        )) catch {};
        if slice_sym := self.scope.find("core")?.scope.find("Slice") {
            type_sym.scope.add(slice_sym.scope.find("is_empty")?) catch {};
        }
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    pub func add_or_get_array(^mut self, elem_typ: ^mut Type, size: uint, is_mut: bool) -> ^mut TypeSym {
        unique_name := if is_mut {
            "[{}]mut {}"
        } else {
            "[{}]{}"
        }.fmt(size, elem_typ.to_qualstring());
        if type_sym := self.scope.find(unique_name) {
            return @as(^mut TypeSym, type_sym);
        }
        type_sym := ^mut TypeSym(
            is_public: true,
            name: unique_name,
            info: ^mut .Array(elem_typ, size, is_mut),
            fields: ^[
                ^mut Field(
                    name: "len", is_public: true,
                    type: ^mut .Basic(self.scope.find_type_symbol_by_index_or_panic(11))
                )
            ]
        );
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    pub func add_or_get_dyn_array(^mut self, elem_type: ^mut Type, is_mut: bool) -> ^mut TypeSym {
        unique_name := if is_mut { "[]mut " } else { "[]" }.concat(elem_type.to_qualstring());
        if type_sym := self.scope.find(unique_name) {
            return @as(^mut TypeSym, type_sym);
        }
        type_sym := ^mut TypeSym(
            is_public: true,
            name: unique_name,
            info: ^mut .DynArray(elem_type, is_mut),
            fields: ^[
                ^mut Field(
                    name: "len", is_public: true,
                    type: ^mut .Basic(self.scope.find_type_symbol_by_index_or_panic(11))
                ),
                ^mut Field(
                    name: "cap", is_public: true,
                    type: ^mut .Basic(self.scope.find_type_symbol_by_index_or_panic(11))
                )
            ]
        );
        type_sym.scope.add(^Func(
            name: "push",
            is_public: true,
            is_method: true,
            self_type: ^mut .Basic(type_sym),
            self_is_mut: true,
            args: ^[^mut Arg("value", type: elem_type, pos: token.no_pos)],
            ret_type: ^mut .Void,
            has_body: true
        )) catch {};
        type_sym.scope.add(^Func(
            name: "pop",
            is_public: true,
            is_method: true,
            self_type: ^mut .Basic(type_sym),
            self_is_mut: true,
            ret_type: elem_type,
            has_body: true
        )) catch {};
        type_sym.scope.add(^Func(
            name: "clone",
            is_public: true,
            is_method: true,
            self_type: ^mut .Basic(type_sym),
            ret_type: ^mut .Basic(type_sym),
            has_body: true
        )) catch {};
        if dyn_array_sym := self.scope.find("core")?.scope.find("DynArray") {
            type_sym.scope.add(dyn_array_sym.scope.find("is_empty")?) catch {};
            type_sym.scope.add(dyn_array_sym.scope.find("delete")?) catch {};
            type_sym.scope.add(dyn_array_sym.scope.find("trim")?) catch {};
            type_sym.scope.add(dyn_array_sym.scope.find("clear")?) catch {};
        }
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    pub func add_or_get_tuple(^mut self, types: []^mut Type) -> ^mut TypeSym {
        unique_name := {
            mut sb := strings.Builder.from_string("(");
            for i, type in types {
                sb.write(type.to_qualstring());
                if i < types.len - 1 {
                    sb.write(", ");
                }
            }
            sb.write_byte(')');
            sb.to_string()
        };
        if type_sym := self.scope.find(unique_name) {
            return @as(^mut TypeSym, type_sym);
        }
        mut fields := []^Field();
        for i, type in types {
            fields.push(^Field(name: i.to_string(), is_public: true, type: type));
        }
        type_sym := ^mut TypeSym(
            is_public: true, name: unique_name, info: ^mut .Tuple(types), fields: fields
        );
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    pub func add_or_get_func(^mut self, func_sym: ^mut Func) -> ^mut TypeSym {
        unique_name := {
            mut sb := strings.Builder.from_string("func(");
            if func_sym.is_method {
                if func_sym.self_is_mut {
                    sb.write("mut ");
                } else if func_sym.self_is_ptr {
                    sb.write("&");
                }
                sb.write("self");
            }
            for i, arg in func_sym.args {
                sb.write(arg.type.to_qualstring());
                if i < func_sym.args.len - 1 {
                    sb.write(", ");
                }
            }
            sb.write_byte(')');
            if func_sym.ret_type !is .Void {
                sb.write_fmt(" {}", func_sym.ret_type.to_qualstring());
            }
            sb.to_string()
        };
        if type_sym := self.scope.find(unique_name) {
            return @as(^mut TypeSym, type_sym);
        }
        type_sym := ^mut TypeSym(is_public: true, name: unique_name, info: ^mut .Func(func_sym));
        self.scope.add(type_sym) catch {};
        return type_sym;
    }

    #[inline]
    pub func is_core(^self) -> bool {
        return self.name == "core";
    }
}

#[boxed]
pub struct Const < Sym {
    pub mut expr: ^mut Expr;
    pub mut evaled_expr: ^mut Expr;
    pub mut has_evaled_size: bool;
    pub mut evaled_size: int;
    pub mut type: ^mut Type;
    pub pos: token.Pos;
}

pub enum VarLevel as uint8 {
    Module, // module-level, `var`
    Receiver, // receiver: `self`
    Argument, // argument-level
    Local // inside function
}

#[boxed]
pub struct Var < Sym {
    pub is_extern: bool;
    pub is_mut: bool;
    pub mut is_used: bool;
    pub mut is_changed: bool;
    pub mut is_hidden_ref: bool;
    pub mut mir_name: string; // used for duplicate objects in MIR
    pub level: VarLevel;
    pub mut type: ^mut Type;
    pub pos: token.Pos;
}

#[boxed]
pub struct Arg {
    pub name: string;
    pub is_mut: bool;
    pub is_self: bool;
    pub has_def_expr: bool;
    pub mut def_expr: ^mut Expr;
    pub pos: token.Pos;
    pub mut type: ^mut Type;
}

#[boxed]
pub struct Func < Sym {
    pub is_extern: bool;
    pub is_unsafe: bool;
    pub is_main: bool;
    pub is_method: bool;
    pub is_special_method: bool;
    pub is_variadic: bool;
    pub mut self_type: ^mut Type;
    pub self_is_ptr: bool;
    pub self_is_boxed: bool;
    pub self_is_mut: bool;
    pub self_pos: token.Pos;
    pub args: []^mut Arg;
    pub has_named_args: bool;
    pub has_body: bool;
    pub ret_type: ^mut Type;
    pub pos: token.Pos;

    pub func get_arg(^self, idx: uint) -> ^Arg {
        arg := self.args[idx];
        return if arg.is_self {
            self.args[idx + 1]
        } else {
            arg
        };
    }

    pub func args_len(^self) -> uint {
        mut len: uint := 0;
        for arg in self.args {
            if !(arg.is_self || arg.type is .Variadic) {
                len += 1;
            }
        }
        return len;
    }

    #[inline]
    pub func kind(^self) -> string {
        return if self.is_method {
            if self.is_special_method {
                "special method"
            } else {
                "method"
            }
        } else {
            "function"
        };
    }

	#[inline]
    pub func type(^mut self, universe: ^mut Module) -> ^mut Type {
        return ^mut .Func(
            is_method: self.is_method,
            self_is_ptr: self.self_is_ptr,
            self_is_mut: self.self_is_mut,
            args: self.args,
            ret_type: self.ret_type,
            sym: universe.add_or_get_func(self),
            has_sym: true,
            pos: token.no_pos
        );
    }
}
