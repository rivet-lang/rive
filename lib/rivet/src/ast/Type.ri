// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import std/traits;
import { Builder } from std/strings;

import ../token;

public enum Type : traits.Stringable {
    Basic {
        mut sym: TypeSym;
        mut is_unresolved: bool;
        mut expr: Expr;
    },
    Void,
    Never: token.Pos,
    Option {
        inner: Type;
        pos: token.Pos;
    },
    Result {
        inner: Type;
        pos: token.Pos;
    },
    Tuple {
        inners: []Type;
        sym: TypeSym;
        pos: token.Pos;

        func ==(self, other: Self) bool {
            for i, inner in self.inners {
                if inner != other.inners[i] {
                    return false;
                }
            }
            return true;
        }
    },
    Variadic {
        inner: Type;
        is_mut: bool;
        sym: TypeSym;
        pos: token.Pos;
    },
    Vector {
        inner: Type;
        is_mut: bool;
        sym: TypeSym;
        pos: token.Pos;
    },
    Array {
        inner: Type;
        is_mut: bool;
        size: Expr;
        sym: TypeSym;
        pos: token.Pos;
    },
    Reference {
        inner: Type;
        is_mut: bool;
        pos: token.Pos;
    },
    Pointer {
        inner: Type;
        is_mut: bool;
        is_indexable: bool;
        pos: token.Pos;
    },
    Anyptr {
        is_mut: bool;
        pos: token.Pos;
    },
    Func {
        is_unsafe: bool;
        is_extern: bool;
        abi: ABI;
        is_method: bool;
        self_is_mut: bool;
        self_is_ref: bool;
        args: []Arg;
        is_variadic: bool;
        ret_type: Type;
        pos: token.Pos;
    };

    public func unalias(self) ?Self {
        // NOTE: `.unalias()` returns an option, so we use the same type without
        // unaliasing instead.
        switch self is {
            .Result as result => return .Result(result.inner.unalias() ?? result.inner),
            .Option as option => return .Option(option.inner.unalias() ?? option.inner),
            .Tuple as tuple_data => {
                unaliased_types := @vec(mut Type, tuple_data.inners.len);
                for i, tuple_type in tuple_data.inners {
                    unaliased_types[i] = tuple_type.unalias() ?? tuple_type;
                }
                return .Tuple(unaliased_types, tuple_data.sym);
            },
            .Vector as vector_data => return .Vector(
                vector_data.inner.unalias() ?? vector_data.inner, vector_data.is_mut
            ),
            .Array as array_data => return .Array(
                array_data.inner.unalias() ?? array_data.inner, ...self
            ),
            .Reference as reference_data => return .Reference(
                reference_data.inner.unalias() ?? reference_data.inner, ...self
            ),
            .Pointer as pointer_data => return .Pointer(
                pointer_data.inner.unalias() ?? pointer_data.inner, ...self
            ),
            .Anyptr => return self,
            .Func as func_data => {
                args := @vec(mut Arg, func_data.args.len);
                for i, arg in args {
                    args[i].type = arg.type.unalias() ?? arg.type;
                }
                return .Func(
                    args: args, ret_type: func_data.ret_type.unalias() ?? func_data.ret_type,
                    ...self
                );
            },
            .Basic as basic => return if basic.is_unresolved {
                none
            } else {
                if basic.sym.info is .Alias as alias_info {
                    .Basic(
                        (alias_info.parent.unalias() ?? alias_info.parent)
                            .symbol() ?? basic.sym
                    )
                } else {
                    .Basic(basic.sym)
                }
            },
            else => return none // for `.Void` or `.Never`
        }
    }

    [inline]
    public func symbol(self) ?TypeSym {
        // NOTE: `.Func` has no TypeSym, so it does not appear in this typeswitch,
        // the same is for `.Void`, `.Never` and `.Anyptr`.
        return switch self is {
            .Result as result => result.inner.symbol(),
            .Option as option => option.inner.symbol(),
            .Tuple as tuple_data => tuple_data.sym,
            .Variadic as variadic_data => variadic_data.sym,
            .Vector as vector_data => vector_data.sym,
            .Array as array_data => array_data.sym,
            .Reference as reference_data => reference_data.inner.symbol(),
            .Pointer as pointer_data => pointer_data.inner.symbol(),
            .Basic as basic if !basic.is_unresolved => basic.sym,
            else => none
        };
    }

    [inline]
    public func ==(self, rhs: Type) bool {
        return switch self is {
            .Void => rhs is .Void,
            .Never => rhs is .Never,
            .Result as result_lhs => if rhs is .Result as result_rhs {
                result_lhs.inner == result_rhs.inner
            } else {
                false
            },
            .Option as option_lhs => if rhs is .Option as option_rhs {
                option_lhs.inner == option_rhs.inner
            } else {
                false
            },
            .Tuple as tuple_lhs => if rhs is .Tuple as tuple_rhs {
                tuple_lhs == tuple_rhs
            } else {
                false
            },
            .Variadic as variadic_lhs => if rhs is .Variadic as variadic_rhs {
                variadic_lhs.inner == variadic_rhs.inner
            } else {
                false
            },
            .Vector as vector_lhs => if rhs is .Vector as vector_rhs {
                vector_lhs.inner == vector_rhs.inner and vector_lhs.is_mut == vector_rhs.is_mut
            } else {
                false
            },
            .Array as array_lhs => if rhs is .Array as array_rhs {
                array_lhs.inner == array_rhs.inner
                    /* and array_lhs.size == array_rhs.size */
                    and array_lhs.is_mut == array_rhs.is_mut
            } else {
                false
            },
            .Reference as ref_lhs => if rhs is .Reference as ref_rhs {
                if ref_lhs.is_mut and !ref_rhs.is_mut {
                    false
                } else {
                    ref_lhs.inner == ref_rhs.inner
                }
            } else {
                false
            },
            .Pointer as ptr_lhs => if rhs is .Pointer as ptr_rhs {
                if ptr_lhs.is_mut and !ptr_rhs.is_mut {
                    false
                } else if ptr_lhs.is_indexable and !ptr_rhs.is_indexable {
                    false
                } else {
                    ptr_lhs.inner == ptr_rhs.inner
                }
            } else {
                false
            },
            .Anyptr as anyptr_lhs => if rhs is .Anyptr as anyptr_rhs {
                anyptr_lhs.is_mut == anyptr_rhs.is_mut
            } else {
                false
            },
            .Func as func_lhs => if rhs is .Func as func_rhs {
                switch {
                    func_lhs.is_unsafe != func_rhs.is_unsafe,
                    func_lhs.is_extern != func_rhs.is_extern,
                    func_lhs.abi != func_rhs.abi,
                    func_lhs.is_method != func_rhs.is_method,
                    func_lhs.self_is_mut != func_rhs.self_is_mut,
                    func_lhs.self_is_ref != func_rhs.self_is_ref,
                    func_lhs.args.len != func_rhs.args.len => false,
                    else => {
                        mut final_res := func_lhs.ret_type == func_rhs.ret_type;
                        for i, arg_lhs in func_lhs.args {
                            arg_rhs := func_rhs.args[i];
                            if arg_lhs.is_mut != arg_rhs.is_mut {
                                final_res = false;
                                break;
                            }
                            if arg_lhs.type != arg_rhs.type {
                                final_res = false;
                                break;
                            }
                        }
                        final_res
                    }
                }
            } else {
                false
            },
            .Basic as basic_lhs => if rhs is .Basic as basic_rhs {
                !basic_lhs.is_unresolved and !basic_rhs.is_unresolved and
                basic_lhs.sym == basic_rhs.sym
            } else {
                false
            },
            else => false
        };
    }

    [inline]
    public func position(self) token.Pos {
        return switch self is {
            .Void => token.NO_POS,
            .Never as never_pos => never_pos,
            .Option as opt_t => opt_t.pos,
            .Result as res_t => res_t.pos,
            .Tuple as tuple_t => tuple_t.pos,
            .Variadic as variadic_t => variadic_t.pos,
            .Vector as vec_t => vec_t.pos,
            .Array as arr_t => arr_t.pos,
            .Reference as ref_t => ref_t.pos,
            .Pointer as ptr_t => ptr_t.pos,
            .Anyptr as anyptr_t => anyptr_t.pos,
            .Func as func_t => func_t.pos,
            .Basic as basic_t => basic_t.expr.position()
        };
    }

    public func to_string(self) string {
        return switch self is {
            .Never => "never",
            .Result as result => "!".concat(result.inner.to_string()),
            .Option as option => "?".concat(option.inner.to_string()),
            .Tuple as tuple_data => {
                mut sb := Builder.new(100);
                sb.write_byte(b'(');
                for i, inner in tuple_data.inners {
                    sb.write_string(inner.to_string());
                    if i < tuple_data.inners.len - 1 {
                        sb.write_string(", ");
                    }
                }
                sb.write_byte(b')');
                sb.to_string()
            },
            .Variadic as variadic_data => "...".concat(variadic_data.inner.to_string()),
            .Vector as vector_data => if vector_data.is_mut {
                "[]mut"
            } else {
                "[]"
            }.concat(vector_data.inner.to_string()),
            .Array as array_data => "[".concat(
                /* array_data.expr.to_string(), */ "]", if array_data.is_mut {
                    "mut "
                } else {
                    ""
                }, array_data.inner.to_string()
            ),
            .Reference as reference_data => if reference_data.is_mut {
                "&mut "
            } else {
                "&"
            }.concat(reference_data.inner.to_string()),
            .Pointer as pointer_data => {
                if pointer_data.is_mut {
                    if pointer_data.is_indexable {
                        "[*]mut "
                    } else {
                        "*mut "
                    }
                } else {
                    if pointer_data.is_indexable {
                        "[*]"
                    } else {
                        "*"
                    }
                }.concat(pointer_data.inner.to_string())
            },
            .Anyptr as anyptr_data => {
                if anyptr_data.is_mut {
                    "mut_anyptr"
                } else {
                    "anyptr"
                }
            },
            .Func as func_data => {
                mut sb := Builder.new(150);
                if func_data.is_extern {
                    sb.write_fmt("extern ({}) ", func_data.abi);
                }
                sb.write_string("func(");
                if func_data.is_method {
                    if func_data.self_is_mut {
                        sb.write_string("mut ");
                    } else if func_data.self_is_ref {
                        sb.write_string("&");
                    }
                    sb.write_string("self");
                    if func_data.args.len > 0 {
                        sb.write_string(", ");
                    }
                }
                if func_data.is_extern and func_data.is_variadic {
                    if func_data.args.len > 0 {
                        sb.write_string(", ");
                    }
                    sb.write_string("...");
                }
                sb.write_string(")");
                if func_data.ret_type !is .Void {
                    sb.write_string(" ");
                    sb.write_string(func_data.ret_type.to_string());
                }
                sb.to_string()
            },
            .Basic as basic if !basic.is_unresolved => basic.sym.qualname(),
            else => "" // TODO: handle unresolved type
        };
    }
}
