// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import std/traits;

pub struct EnumVariant {
    pub name: string;
    pub value: int;
    pub has_type: bool;
    pub type: +mut Type;
    pub has_fields: bool;
}

pub enum TypeInfo < traits.Stringable {
    Invalid,
    Placeholder,
    None,
    Bool,
    Rune,
    ComptimeInt,
    ComptimeFloat,
    Int,
    SizedInt {
        size: uint;
    },
    Uint,
    SizedUint {
        size: uint;
    },
    Float {
        size: uint;
    },
    String,
    Func(+Func),
    Alias {
        parent: +mut Type;
    },
    Array {
        elem_type: +mut Type;
        size: uint;
        is_mut: bool;
        has_contains_method: bool;
    },
    Slice {
        elem_type: +mut Type;
        is_mut: bool;
        has_contains_method: bool;
    },
    DynArray {
        elem_type: +mut Type;
        is_mut: bool;
        has_contains_method: bool;
    },
    Tuple {
        types: []+mut Type;
    },
    Trait {
        mut bases: []mut +mut TypeSym;
        mut implements: []mut +mut TypeSym;
        mut has_objects: bool;

        func index_of(&self, type_sym: +mut TypeSym) -> ?uint {
            for i, ts in self.implements {
                if ts.id == type_sym.id {
                    return i;
                }
            }
            return none;
        }

        func implement(&mut self, type_sym: +mut TypeSym) {
            if type_sym !in self.implements {
                self.implements.push(type_sym);
                for b in self.bases {
                    @as(+mut TypeInfo.Trait, b.info).implement(type_sym);
                }
            }
        }

        #[inline]
        func mark_has_objects(&mut self) {
            self.has_objects = true;
        }
    },
    Enum {
        underlying_type: +mut Type;
        mut variants: []mut EnumVariant;
        is_tagged: bool;
        is_boxed: bool;
        mut traits: []mut +mut TypeSym;

        #[inline]
        func add_variant(&mut self, variant: EnumVariant) {
            self.variants.push(variant);
        }

        func has_variant(&self, name: string) -> bool {
            return self.get_variant(name) != none;
        }

        func get_variant(&self, name: string) -> ?EnumVariant {
            for v in self.variants {
                if v.name == name {
                    return v;
                }
            }
            return none;
        }

        func get_variant_by_type(&self, type: +mut Type) -> ?EnumVariant {
            for v in self.variants {
                if v.has_type && v.type == type {
                    return v;
                }
            }
            return none;
        }

        func contains_trait(&self, type_sym: +mut TypeSym) -> bool {
            for trait_ in self.traits {
                if trait_.id == type_sym.id {
                    return true;
                }
            }
            return false;
        }
    },
    Struct {
        is_boxed: bool;
        is_opaque: bool;
        is_enum_variant: bool;
        mut parent: +mut TypeSym; // enum type symbol if this is a enum variant
        mut bases: []mut +mut TypeSym;
        mut traits: []mut +mut TypeSym;

        func contains_trait(&self, type_sym: +mut TypeSym) -> bool {
            for trait_ in self.traits {
                if trait_.id == type_sym.id {
                    return true;
                }
            }
            return false;
        }
    };

    #[inline]
    pub func elem_type(self) -> ?+Type {
        return match self {
            .Slice(slice) -> slice.elem_type,
            .Array(arr) -> arr.elem_type,
            .DynArray(vec) -> vec.elem_type,
            else -> none
        };
    }

    #[inline]
    pub func is_mut_array(self) -> bool {
        return match self {
            .Slice(slice) -> slice.is_mut,
            .Array(arr) -> arr.is_mut,
            .DynArray(vec) -> vec.is_mut,
            else -> false
        };
    }

    #[inline]
    pub func is_primitive(self) -> bool {
        return self is .Bool || self is .Rune || self is .SizedInt || self is .SizedUint
            || self is .Uint || self is .Int || self is .ComptimeInt
            || self is .ComptimeFloat || self is .ComptimeFloat || self is .Float;
    }

    #[inline]
    pub func is_compound(self) -> bool {
        return self is .Struct || self is .Trait || self is .Enum || self is .Tuple;
    }

    #[inline]
    pub func to_string(&self) -> string {
        return match self.* {
            .Invalid -> "invalid",
            .Placeholder -> "placeholder",
            .None -> "none",
            .Bool -> "bool",
            .Rune -> "rune",
            .Int -> "int",
            .SizedInt(int_info) -> "int{}".fmt(int_info.size),
            .Uint -> "uint",
            .SizedUint(uint_info) -> "uint{}".fmt(uint_info.size),
            .ComptimeInt -> "comptime_int",
            .ComptimeFloat -> "comptime_float",
            .Float(float_info) -> "float{}".fmt(float_info.size),
            .String -> "string",
            .Func -> "func",
            .Alias -> "alias",
            .Slice -> "slice",
            .Array -> "array",
            .DynArray -> "dynamic array",
            .Tuple -> "tuple",
            .Trait -> "trait",
            .Struct(struct_info) -> if struct_info.is_enum_variant {
                "enum variant"
            } else {
                "struct"
            },
            .Enum -> "enum"
        };
    }
}
