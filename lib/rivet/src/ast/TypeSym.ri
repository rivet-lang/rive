// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

#[boxed]
public struct Field {
    public is_public: bool;
    public name: string;
    public is_mut: bool;
    public has_def_expr: bool;
    public def_expr: Expr;
    public type: Type;
    public is_required: bool;
}

#[boxed]
public struct TypeSym < Sym {
    public mut fields: []Field;
    public mut full_fields_: []Field;
    public mut info: TypeInfo = .Invalid();
    public mut size: usize;
    public mut align: usize;
    public mut default_value: ?Expr;

    public func lookup_field(self, name: string) -> ?Field {
        for f in self.fields {
            if f.name == name {
                return f;
            }
        }
        if self.info is .Trait as trait_info {
            for b in trait_info.bases {
                if f := b.lookup_field(name) {
                    return f;
                }
            }
        } else if self.info is .Struct as struct_info {
            for t in struct_info.traits {
                if f := t.lookup_field(name) {
                    return f;
                }
            }
            for b in struct_info.bases {
                if f := b.lookup_field(name) {
                    return f;
                }
            }
        }
        return none;
    }

    #[inline]
    public func has_field(self, name: string) -> bool {
        return self.lookup_field(name) !is none;
    }

    #[inline]
    public func lookup(self, name: string) -> ?Sym {
        return if s := self.scope.lookup(name) {
            s
        } else {
            none
        };
    }

    public func find_in_base(self, name: string) -> ?Sym {
         if self.info is .Trait as trait_info {
            for b in trait_info.bases {
                if s := b.scope.find(name) {
                    return s;
                }
            }
        } else if self.info is .Struct as struct_info {
            for t in struct_info.traits {
                if s := t.scope.find(name) {
                    return s;
                }
            }
            for b in struct_info.bases {
                if s := b.scope.find(name) {
                    return s;
                }
            }
        } else if self.info is .Enum as enum_info {
            for t in enum_info.traits {
                if s := t.scope.find(name) {
                    return s;
                }
            }
        }
        return none;
    }

    #[inline]
    public func find_func(self, name: string) -> ?Func {
        return if sym := self.scope.find(name); sym is Func as func_ {
            if self.info !is .Trait and !func_.has_body {
                return none;
            }
            func_
        } else if sym := self.find_in_base(name); sym is Func as func_ {
            if self.info !is .Trait and !func_.has_body {
                return none;
            }
            func_
        } else {
            none
        };
    }

    #[inline]
    public func find_method(self, name: string) -> ?Func {
        return if func_ := self.find_func(name); func_.is_method {
            func_
        } else {
            none
        };
    }

    #[inline]
    public func has_func(self, name: string) -> bool {
        return self.find_func(name) !is none;
    }

    #[inline]
    public func has_method(self, name: string) -> bool {
        return self.find_method(name) !is none;
    }

    public func full_fields(mut self) -> []Field {
        if !self.full_fields_.is_empty() {
            return self.full_fields_;
        }
        mut fields := @vec(Field);
        if self.info is .Trait as trait_info {
            for mut b in trait_info.bases {
                for bf in b.full_fields() {
                    fields.push(bf);
                }
            }
        } else if self.info is .Struct as struct_info {
            for mut t in struct_info.traits {
                for tf in t.full_fields() {
                    fields.push(tf);
                }
            }
            for mut b in struct_info.bases {
                for bf in b.full_fields() {
                    fields.push(bf);
                }
            }
        }
        for f in self.fields {
            fields.push(f);
        }
        self.full_fields_ = fields;
        return fields;
    }

    #[inline]
    public func implement_trait(self, trait_sym: TypeSym) -> bool {
        return if trait_sym.info is .Trait as trait_info {
            self in trait_info.implements
        } else {
            false
        };
    }

    public func update(mut self, other: TypeSym) -> !bool {
        if self.info is .Placeholder {
            // update placeholder
            if other.info !is .Placeholder {
                self.is_public = other.is_public;
                self.info = other.info;
            }
            self.fields = other.fields;
            for s in other.scope.syms {
                self.scope.add(s)!;
            }
            return true;
        }
        return false;
    }

    #[inline]
    public func is_boxed(self) -> bool {
        return if self.info is .Enum as enum_info {
            enum_info.is_boxed
        } else if self.info is .Struct as struct_info {
            struct_info.is_boxed
        } else {
            self.info is .Trait or self.info is .String or self.info is .Vector
        };
    }

    #[inline]
    public func is_primitive(self) -> bool {
        return self.info.is_primitive();
    }

    #[inline]
    public func ==(self, rhs: Self) -> bool {
        return self.id == rhs.id;
    }

    #[inline]
    public func !=(self, rhs: Self) -> bool {
        return self.id != rhs.id;
    }
}
