// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import ../token;

public enum Node {
    Decl: Decl,
    Stmt: Stmt,
    Expr: Expr
}

[boxed]
public struct SourceFile {
    public file: string;
    public decls: []Decl;
    public mut mod: Module;
    public mut imported_symbols: ImportedSymbols;
}

[boxed]
public struct ImportedSymbol {
    public name: string;
    public sym: Sym;
    public pos: token.Pos;
    public mut is_used: bool;
}

public struct ImportedSymbols {
    public mut syms: []ImportedSymbol;

    [inline]
    public func add(mut self, name: string, sym: Sym, pos: token.Pos, is_used: bool = false) {
        self.syms.push(ImportedSymbol(name, sym, pos, is_used));
    }

    public func find(&self, name: string) ?ImportedSymbol {
        for imported_sym in self.syms {
            if name == imported_sym.name {
                return imported_sym;
            }
        }
        return none;
    }

    [inline]
    public func exists(&self, name: string) bool {
        return self.find(name) !is none;
    }
}

// Used in variable decls/stmts and guard exprs
[boxed]
public struct ObjectData {
    public name: string;
    public is_mut: bool;
    public is_ref: bool;
    public mut type: Type;
    public level: VarLevel;
    public pos: token.Pos;
    public mut sym: Var;
}

[boxed]
public struct AnnotationDuplicatedError : Error {
    name: string;

    public func to_string(self) string {
        return "duplicated annotation `{}`".fmt(self.name);
    }
}

[boxed]
public struct AnnotationArgument {
    public name: string;
    public expr: Expr;
    public is_named: bool;
}

[boxed]
public struct Annotation {
    public name: string;
    public args: []AnnotationArgument;
    public pos: token.Pos;

    public func find_argument(self, name: string) ?AnnotationArgument {
        for arg in self.args {
            if arg.name == name {
                return arg;
            }
        }
        return none;
    }
}

[boxed]
public struct Annotations {
    public mut annotations: []Annotation;

    public func add(mut self, annotation: Annotation) ! {
        if self.has(annotation.name) {
            return AnnotationDuplicatedError(annotation.name);
        }
        self.annotations.push(annotation);
    }

    public func find(self, name: string) ?Annotation {
        for annotation in self.annotations {
            if annotation.name == name {
                return annotation;
            }
        }
        return none;
    }

    [inline]
    public func has(self, name: string) bool {
        return self.find(name) !is none;
    }

    [inline]
    public func is_empty(self) bool {
        return self.annotations.is_empty();
    }
}
