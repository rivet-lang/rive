// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import "../token";

public var COMPTIME_CONSTANTS = [
    "_FILE_", "_LINE_", "_COLUMN_", "_FUNCTION_", "_RIVET_VERSION_",
    "_RIVET_COMMIT_"
];

[inline]
public func is_comptime_constant(name: string) bool {
    return name in COMPTIME_CONSTANTS;
}

[boxed]
public struct SourceFile {
    public file: string;
    public mod_sym: Sym;
    public decls: []Decl;
    public mut imported_symbols: ImportedSymbols;
}

public struct ImportedSymbols {
    mut syms: [](string, Sym);

    [inline]
    public func add(mut self, name: string, sym: Sym) {
        self.syms.push((name, sym));
    }

    public func get(&self, name: string) ?Sym {
        for pair in self.syms {
            if name == pair.0 {
                return pair.1;
            }
        }
        return none;
    }
}

// Used in variable decls/stmts and guard exprs
[boxed]
public struct ObjectData {
    public name: string;
    public is_mut: bool;
    public is_ref: bool;
    public type: Type;
    public level: VarLevel;
    public pos: token.Pos;
    public sym: Var;
}

[boxed]
public struct AnnotationDuplicatedError : Error {
    name: string;

    public func to_string(self) string {
        return "duplicated annotation `{}`".fmt(self.name);
    }
}

[boxed]
public struct AnnotationArgument {
    public name: string;
    public expr: Expr;
    public is_named: bool;
}

[boxed]
public struct Annotation {
    public name: string;
    public args: []AnnotationArgument;
    public pos: token.Pos;

    public func find_argument(self, name: string) ?AnnotationArgument {
        for arg in self.args {
            if arg.name == name {
                return arg;
            }
        }
        return none;
    }
}

[boxed]
public struct Annotations {
    public mut annotations: []Annotation;

    public func add(mut self, annotation: Annotation) ! {
        if self.has(annotation.name) {
            return AnnotationDuplicatedError(annotation.name);
        }
        self.annotations.push(annotation);
    }

    public func find(self, name: string) ?Annotation {
        for annotation in self.annotations {
            if annotation.name == name {
                return annotation;
            }
        }
        return none;
    }

    [inline]
    public func has(self, name: string) bool {
        return self.find(name) !is none;
    }

    [inline]
    public func is_empty(self) bool {
        return self.annotations.is_empty();
    }
}
