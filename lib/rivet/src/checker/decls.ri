// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import ../ast;
import ../report;

extend Checker {
    func check_global_vars(mut self, decls: []mut ast.Decl) {
        for mut decl in decls {
            old_sym := self.sym;
            if decl is .Const or decl is .Var {
                self.check_decl(decl);
            } else if decl_decls := decl.decls() {
                self.check_global_vars(decl_decls);
            }
            self.sym = old_sym;
        }
    }

    func check_decls(mut self, decls: []mut ast.Decl) {
        for mut decl in decls {
            if !(decl is .Const or decl is .Var) {
                self.check_decl(decl);
            }
        }
    }

    func check_decl(mut self, mut decl: ast.Decl) {
        old_sym := self.sym;
        defer self.sym = old_sym;
        switch decl is {
            .Import as import_decl => if import_decl.import_list.is_empty() and !import_decl.glob {
                if import_decl.has_custom_alias {
                    self.check_name_case(
                        .Snake, "import alias", import_decl.alias_name, import_decl.pos
                    );
                }
            } else {
                for import_info in import_decl.import_list {
                    if !import_info.has_custom_alias {
                        continue;
                    }
                    self.check_name_case(
                        .Snake, "import alias", import_info.alias_name, import_info.pos
                    );
                }
            },
            .Alias as alias_decl => {
                self.check_name_case(
                    if alias_decl.is_typealias or alias_decl.sym is ast.TypeSym
                        or (alias_decl.sym is ast.SymRef as sym_ref
                            and sym_ref.ref is ast.TypeSym) {
                        .Pascal
                    } else {
                        .Snake
                    }, "alias", alias_decl.name, alias_decl.pos
                )
            },
            .Extern as extern_decl => {
                self.inside_extern = true;
                self.check_decls(extern_decl.decls);
                self.inside_extern = false;
            },
            .Var as var_decl => {
                self.inside_var_decl = true;
                self.check_var_decl(
                    var_decl.lefts, var_decl.right, var_decl.scope, var_decl.pos
                );
                self.inside_var_decl = false;
            },
            .Const as const_decl => {
                self.check_name_case(.Upper, "constant", const_decl.name, const_decl.pos);
                if const_decl.has_type {
                    old_expected_type := self.expected_type;
                    self.expected_type = const_decl.type;
                    const_type := self.check_expr(const_decl.expr);
                    self.expected_type = old_expected_type;
                    self.check_types(const_type, const_decl.type) catch |err| {
                        report.error(err.to_string(), const_decl.expr.position());
                    };
                } else {
                    const_decl.type = self.check_expr(const_decl.expr);
                    @cast(ast.Const, const_decl.sym).type = const_decl.type;
                }
                if const_decl.type is .Option or const_decl.type is .None {
                    report.error(
                        "cannot assign option values to constants",
                        const_decl.expr.position()
                    );
                } else if type_sym := const_decl.type.symbol() {
                    if type_sym.is_boxed() {
                        report.error(
                            "cannot assign boxed values to constants",
                            const_decl.expr.position()
                        );
                    }
                }
            },
            .Trait as trait_decl => {
                self.check_name_case(.Pascal, "trait", trait_decl.name, trait_decl.pos);
                self.check_decls(trait_decl.decls);
            },
            .Enum as enum_decl => {
                self.check_name_case(.Pascal, "enum", enum_decl.name, enum_decl.pos);
                for base in enum_decl.bases {
                    if sym := base.symbol() {
                        if sym.info is .Trait {
                            self.check_trait_impl(
                                @cast(ast.TypeSym, enum_decl.sym), sym, enum_decl.pos
                            );
                        }
                    }
                }
                for variant in enum_decl.variants {
                    self.check_name_case(.Pascal, "enum variant", variant.name, variant.pos);
                    self.check_decls(variant.decls);
                }
                self.check_decls(enum_decl.decls);
            },
            .Struct as struct_decl => {
                self.check_name_case(.Pascal, "struct", struct_decl.name, struct_decl.pos);
                for base in struct_decl.bases {
                    if sym := base.symbol() {
                        if sym.info is .Trait {
                            self.check_trait_impl(
                                @cast(ast.TypeSym, struct_decl.sym), sym, struct_decl.pos
                            );
                        }
                    }
                }
                self.check_decls(struct_decl.decls);
            },
            .Field as field_decl => {
                self.check_name_case(.Snake, "field", field_decl.name, field_decl.pos);
                if field_decl.has_def_expr {
                    old_expected_type := self.expected_type;
                    self.expected_type = field_decl.type;
                    field_type := self.check_expr(field_decl.def_expr);
                    self.expected_type = old_expected_type;
                    self.check_types(field_type, field_decl.type) catch |err| {
                        report.error(err.to_string(), field_decl.def_expr.position());
                    };
                }
            },
            .Extend as extend_decl => {
                type_sym := extend_decl.type.symbol()?;
                for base in extend_decl.bases {
                    if sym := base.symbol() {
                        if sym.info is .Trait {
                            self.check_trait_impl(type_sym, sym, extend_decl.type.position());
                        }
                    }
                }
                self.check_decls(extend_decl.decls);
            },
            .Func as mut func_decl => self.check_func(func_decl),
            .Destructor as dtor_decl => {
                self.check_stmts(dtor_decl.stmts);
                dtor_decl.defer_stmts = self.defer_stmts;
                self.defer_stmts = [];
                self.check_scope_vars(dtor_decl.scope);
            },
            .Test as test_decl => {
                old_cur_fn := self.cur_fn;
                self.cur_fn = ast.Func(ret_type: .Void());
                self.inside_test = true;
                self.check_stmts(test_decl.stmts);
                self.inside_test = false;
                self.cur_fn = old_cur_fn;
                self.check_scope_vars(test_decl.scope);
            },
            else => {}
        }
    }

    func check_func(mut self, mut func_decl: ast.Decl.Func) {
        if !func_decl.is_operator {
            self.check_name_case(
                .Snake, if func_decl.is_method {
                    "method"
                } else {
                    "function"
                }, func_decl.name, func_decl.pos
            );
        }
        if func_decl.is_method and func_decl.self_is_ptr and func_decl.self_type.is_boxed() {
            report.error("cannot take the address of a boxed value as receiver", func_decl.self_pos);
        }
        for arg in func_decl.args {
            self.check_name_case(.Snake, "argument", arg.name, arg.pos);
            if arg.has_def_expr {
                if func_decl.abi != .Rivet {
                    report.error("extern functions cannot have default arguments", arg.pos);
                }
                old_expected_type := self.expected_type;
                self.expected_type = arg.type;
                def_expr_t := self.check_expr(arg.def_expr);
                self.expected_type = old_expected_type;
                self.check_types(def_expr_t, arg.type) catch |err| {
                    report.error(err.to_string(), arg.def_expr.position());
                };
            }
        }
        if func_decl.ret_type is .Pointer and func_decl.abi != .Rivet {
            mut err := report.error_builder(
                "function `{}` should return an optional pointer".fmt(func_decl.name),
                func_decl.ret_type.position()
            );
            err.add_note("this is because Rivet cannot ensure that the function does not always return a null pointer");
            err.emit();
        }
        self.cur_fn = @cast(ast.Func, func_decl.sym);
        self.check_stmts(func_decl.stmts);
        func_decl.defer_stmts = self.defer_stmts;
        self.defer_stmts = [];
        self.check_scope_vars(func_decl.scope);
        if func_decl.ret_type is .Never and !self.inside_extern {
            if func_decl.stmts.len > 0 {
                switch func_decl.stmts[func_decl.stmts.len - 1] is {
                    .While as while_stmt if while_stmt.is_inf
                        and while_stmt.stmt is .Expr as expr and expr is .Block as block => {
                        if !block.stmts.is_empty() {
                            report.error("infinite loop should be empty", while_stmt.pos);
                        }
                    },
                    .Expr as expr2 if expr2 is .Call as call_expr => {
                        if call_expr.type !is .Never {
                            report.error(
                                "`{}` is not a `never` function".fmt(
                                    call_expr.func_.name
                                ), call_expr.pos
                            );
                        }
                    },
                    .Expr as expr3 if expr3 is .Block as block1 and block1.stmts.len > 0 => {
                        last_stmt := block1.stmts[block1.stmts.len - 1];
                        if last_stmt is .Expr as last_expr and last_expr is .Call as call {
                            if call.type !is .Never {
                                report.error(
                                    "`{}` is not a `never` function".fmt(
                                        call_expr.func_.name
                                    ), call_expr.pos
                                );
                            }
                        } else {
                            mut err := report.error_builder(
                                "`never` functions cannot return",
                                func_decl.pos
                            );
                            err.add_note("`never` functions should end with a call to another `never` function, or with a `while {}`");
                            err.emit();
                        }
                    },
                    else => {
                        mut err := report.error_builder(
                            "`never` functions cannot return",
                            func_decl.pos
                        );
                        err.add_note("`never` functions should end with a call to another `never` function, or with a `while {}`");
                        err.emit();
                    }
                }
            } else {
                mut err := report.error_builder(
                    "`never` functions cannot be empty",
                    func_decl.pos
                );
                err.add_note("`never` functions should end with a call to another `never` function, or with a `while {}`");
                err.emit();
            }
        } else if func_decl.has_body and !func_decl.ret_type.is_void()
            and !self.has_return(func_decl.stmts) {
            report.error(
                "missing `return` statement at end of {} `{}`".fmt(
                    if func_decl.is_method { "method" } else { "function" },
                    func_decl.name
                ),
                func_decl.pos
            );
        }
    }
}
