// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import ../ast;
import ../utils;
import ../report;
import ../utils/maps;

extend Checker {
    const MATCH_EXHAUSTIVE_CUTOFF_LIMIT := 12;

    func check_match(mut self, mut match_expr: ast.Expr.Match) -> ast.Type {
        old_expected_type := self.expected_type;
        match_expr.type = .Void();
        if match_expr.branches.len == 1 and match_expr.has_else {
            report.error(
                "`match` must have at least one non `else` branch",
                match_expr.branches[0].pos
            );
            return match_expr.type;
        }

        is_guard := match_expr.expr is .Guard;
        old_inside_match_cond_expr := self.inside_match_cond_expr;
        self.inside_match_cond_expr = true;
        mut expr_type := self.check_expr(match_expr.expr);
        self.inside_match_cond_expr = old_inside_match_cond_expr;
        mut expr_sym := expr_type.symbol() ?? {
            if !is_guard {
                report.error(
                    "invalid type `{}` used as `match` condition".fmt(expr_type),
                    match_expr.expr.position()
                );
                return .Void();
            }
            ast.TypeSym()
        };
        if match_expr.expr is .Guard(guard_expr) {
            if guard_expr.vars.len == 1 {
                expr_type = guard_expr.vars[0].type;
                expr_sym = expr_type.symbol()?;
            } else {
                mut types := @dyn_array(mut ast.Type);
                for var_ in guard_expr.vars {
                    types.push(var_.type);
                }
                expr_sym = self.table.universe.add_or_get_tuple(types);
                expr_type = .Tuple(types, expr_sym);
            }
        } else {
            if match_expr.is_typematch and !(expr_sym.info is .Enum
                or expr_sym.info is .Trait) {
                mut err := report.error_builder(
                    "invalid type for type-match", match_expr.expr.position()
                );
                err.add_note("expected enum or trait type, found `{}`", expr_type);
                err.emit();
            } else if expr_sym.info is .Enum(enum_info) {
                if enum_info.is_boxed and !match_expr.is_typematch {
                    mut err := report.error_builder(
                        "cannot use `match` with a non-boxed enum type", match_expr.pos
                    );
                    err.add_note("use a type-match instead");
                    err.emit();
                } else if !enum_info.is_boxed and match_expr.is_typematch {
                    mut err := report.error_builder(
                        "cannot use a type-match with a non-boxed enum type",
                        match_expr.pos
                    );
                    err.add_note("use a simple `match` instead");
                    err.emit();
                }
            }
        }

        mut branch_exprs := maps.MapStringUint();
        match_expr.expected_type = self.expected_type;
        for ib, branch in match_expr.branches {
            if !branch.is_else {
                self.expected_type = expr_type;
                for i, mut case in branch.cases {
                    case_t := self.check_expr(case);
                    if i == 0 {
                        branch.var_type = case_t;
                    }
                    self.check_types(case_t, expr_type) catch |err| {
                        report.error(err.to_string(), case.position());
                    };
                    if !branch.has_cond {
                        key := case.to_string();
                        if val := branch_exprs.get(key) {
                            if val == 1 {
                                report.error(
                                    "match case is handled more than once", case.position()
                                );
                            } else {
                                branch_exprs.set(key, val + 1);
                            }
                        } else {
                            branch_exprs.set(key, 1);
                        }
                    }
                }
                if branch.has_var {
                    self.check_name_case(
                        .Snake, "branch variable", branch.var_name, branch.var_pos
                    );
                    if branch.var_is_mut {
                        self.check_expr_is_mut(match_expr.expr);
                    }
                    if branch.cases.len != 1 {
                        report.error("multiple cases cannot have variable", branch.var_pos);
                    } else if expr_sym.is_boxed() {
                        if expr_sym.info is .Enum {
                            case0 := @as(
                                ast.Expr.EnumLiteral, branch.cases[0]
                            ).variant;
                            if case0.has_type {
                                branch.var_type = case0.type;
                                match_expr.scope.update_is_hidden_ref(
                                    branch.var_name, branch.var_is_mut
                                );
                            } else {
                                report.error(
                                    "cannot use void expression as value",
                                    branch.cases[0].position()
                                );
                            }
                        }
                        match_expr.scope.update_type(branch.var_name, branch.var_type);
                    } else {
                        report.error("only boxed types can have vars", branch.var_pos);
                    }
                }
                if branch.has_cond and self.check_expr(branch.cond) != self.table.bool_t {
                    report.error(
                        "non-boolean expression use as `match` branch condition",
                        branch.cond.position()
                    );
                }
                self.expected_type = old_expected_type;
            }
            mut branch_t := ast.Type.Void();
            if ib == 0 {
                branch_t = self.check_expr(branch.expr);
                if match_expr.expected_type is .Void {
                    match_expr.expected_type = branch_t;
                }
                match_expr.type = branch_t;
            } else {
                old_expected_type2 := self.expected_type;
                self.expected_type = match_expr.expected_type;
                branch_t = self.check_expr(branch.expr);
                self.expected_type = old_expected_type2;
                self.check_types(branch_t, match_expr.expected_type) catch |err| {
                    report.error(err.to_string(), branch.expr.position());
                };
            }
            branch.type = branch_t;
            if self.scope_returns {
                self.scope_returns = false;
            }
        }

        // check that expressions are exhaustive, this is achieved either by putting
        // an `else` or when the `match` is on an enum by listing all variants.
        match_expr.is_exhaustive = true;
        mut unhandled := @dyn_array(string);
        if expr_type == self.table.bool_t and match_expr.branches.len == 1 {
            for v in ["true", "false"] {
                if !branch_exprs.contains(v) {
                    match_expr.is_exhaustive = false;
                    unhandled.push("`{}`".fmt(v));
                }
            }
        } else if expr_sym.info is .Enum(enum_info2) {
            for variant in enum_info2.variants {
                e := ".".concat(variant.name);
                if !branch_exprs.contains(e) {
                    match_expr.is_exhaustive = false;
                    unhandled.push("`{}`".fmt(e));
                }
            }
        } else {
            match_expr.is_exhaustive = false;
        }

        mut has_else := match_expr.has_else;
        mut else_branch := match_expr.branches[match_expr.branches.len - 1];
        if !has_else {
            for i, branch in match_expr.branches {
                if branch.is_else and i != match_expr.branches.len - 1 {
                    report.error("`else` must be the last branch of `match`", branch.pos);
                    else_branch = branch;
                    has_else = true;
                }
            }
        }
        if match_expr.is_exhaustive {
            if has_else {
                report.warn("`match` is exhaustive, `else` is unnecessary", else_branch.pos);
            }
            return match_expr.expected_type;
        }
        if has_else {
            return match_expr.expected_type;
        }

        mut err := report.error_builder("`match` must be exhaustive", match_expr.pos);
        if unhandled.len > 0 {
            mut err_details := "add `match` branches for: ";
            if unhandled.len < MATCH_EXHAUSTIVE_CUTOFF_LIMIT {
                err_details = err_details.concat(utils.join(unhandled, ", "));
            } else {
                err_details = err_details.concat(
                    utils.join(unhandled[..MATCH_EXHAUSTIVE_CUTOFF_LIMIT], ", ")
                );
                remaining := unhandled.len - MATCH_EXHAUSTIVE_CUTOFF_LIMIT;
                if remaining > 0 {
                    err_details = err_details.concat(", and {} others...".fmt(remaining));
                }
            }
            err.add_help(err_details);
        } else {
            err.add_help("add `else` branch at the end");
        }
        err.emit();
        return match_expr.expected_type;
    }
}
