// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ../{ ast, utils, report, utils/maps };

extend Checker {
    const MATCH_EXHAUSTIVE_CUTOFF_LIMIT := 12;

    func check_match(^mut self, match_expr: &mut ast.Expr.Match) -> ^mut ast.Type {
        old_expected_type := self.expected_type;
        match_expr.type = ^mut .Void;
        if match_expr.branches.len == 1 && match_expr.has_else {
            report.error(
                "`match` must have at least one non `else` branch",
                match_expr.branches[0].pos
            );
            return match_expr.type;
        }

        is_guard := match_expr.expr is .Guard;
        old_inside_match_cond_expr := self.inside_match_cond_expr;
        self.inside_match_cond_expr = true;
        mut expr_type := self.check_expr(match_expr.expr);
        self.inside_match_cond_expr = old_inside_match_cond_expr;
        mut expr_sym := expr_type.symbol() ?? {
            if !is_guard {
                report.error(
                    "invalid type `{}` used as `match` condition".fmt(expr_type),
                    match_expr.expr.position()
                );
                return ^mut .Void;
            }
            ^mut ast.TypeSym()
        };
        if match_expr.expr is .Guard(guard_expr) {
            if guard_expr.vars.len == 1 {
                expr_type = guard_expr.vars[0].type;
                expr_sym = expr_type.symbol()?;
            } else {
                mut types := []^mut ast.Type();
                for var_ in guard_expr.vars {
                    types.push(var_.type);
                }
                expr_sym = self.env.universe.add_or_get_tuple(types);
                expr_type = ^mut .Tuple(types, expr_sym);
            }
        } else {
            if match_expr.is_typematch && !(expr_sym.info is .Enum
                || expr_sym.info is .Trait) {
                mut err := report.error_builder(
                    "invalid type for type-match", match_expr.expr.position()
                );
                err.add_note("expected trait value, found `{}`", expr_type);
                err.emit();
            } else if expr_sym.info is .Enum(enum_info) {
                if enum_info.is_tagged && !match_expr.is_typematch {
                    match_expr.is_typematch = true;
                }
            }
        }

        mut branch_exprs := maps.MapStringUint();
        match_expr.expected_type = self.expected_type;
        for ib, &mut branch in match_expr.branches {
            if !branch.is_else {
                self.expected_type = expr_type;
                for i, pattern in branch.patterns {
                    pattern_t := self.check_expr(pattern);
                    if i == 0 {
                        branch.var_type = pattern_t;
                    }
                    self.check_types(pattern_t, expr_type) catch |err| {
                        report.error(err.to_string(), pattern.position());
                    };
                    if !branch.has_cond {
                        key := pattern.to_string();
                        if val := branch_exprs.get(key) {
                            if val == 1 {
                                report.error(
                                    "match pattern is handled more than once", pattern.position()
                                );
                            } else {
                                branch_exprs.set(key, val + 1);
                            }
                        } else {
                            branch_exprs.set(key, 1);
                        }
                    }
                }
                if branch_var := branch.branch_var {
                    self.check_name_case(
                        .Snake, "branch variable", branch_var.name, branch_var.pos
                    );
                    if branch_var.is_mut {
                        self.check_expr_is_mut(match_expr.expr);
                    }
                    branch_var.type = branch.var_type;
                    if branch.patterns.len == 1 {
                        if expr_sym.info is .Enum {
                            pattern0 := @as(
                                ast.Expr.EnumLiteral, branch.patterns[0]
                            ).variant;
                            if pattern0.has_type {
                                branch_var.type = pattern0.type;
                                if branch_var.is_ref {
                                    if branch_var.type is .Pointer {
                                        report.error("cannot take the address of other pointer", branch_var.pos);
                                    } else if branch_var.type.is_boxed() {
                                        report.error("cannot take the address of a boxed value", branch_var.pos);
                                    }
                                    branch_var.type = ^mut .Pointer(branch_var.type, branch_var.is_mut);
                                } else {
                                    branch.scope.update_is_hidden_ref(
                                        branch_var.name, branch_var.is_mut
                                    );
                                }
                            } else {
                                report.error(
                                    "cannot use void expression as value",
                                    branch.patterns[0].position()
                                );
                            }
                        } else if branch_var.is_ref && branch_var.type.is_boxed() {
                            report.error("cannot take the address of a boxed value", branch_var.pos);
                        }
                        branch.scope.update_type(branch_var.name, branch_var.type);
                    } else {
                        report.error("multiple patterns cannot have variable", branch_var.pos);
                    }
                }
                if branch.has_cond && self.check_expr(branch.cond) != self.env.bool_t {
                    report.error(
                        "non-boolean expression use as `match` branch condition",
                        branch.cond.position()
                    );
                }
                self.expected_type = old_expected_type;
            }
            mut branch_t := ^mut ast.Type.Void;
            if ib == 0 {
                branch_t = self.check_expr(branch.expr);
                if match_expr.expected_type is .Void {
                    match_expr.expected_type = branch_t;
                }
                match_expr.type = branch_t;
            } else {
                old_expected_type2 := self.expected_type;
                self.expected_type = match_expr.expected_type;
                branch_t = self.check_expr(branch.expr);
                self.expected_type = old_expected_type2;
                self.check_types(branch_t, match_expr.expected_type) catch |err| {
                    report.error(err.to_string(), branch.expr.position());
                };
            }
            branch.type = branch_t;
            if self.scope_returns {
                self.scope_returns = false;
            }
        }

        // check that expressions are exhaustive, this is achieved either by putting
        // an `else` or when the `match` is on an enum by listing all variants.
        match_expr.is_exhaustive = true;
        mut unhandled := []string();
        if expr_type == self.env.bool_t && match_expr.branches.len == 1 {
            for v in ["true", "false"] {
                if !branch_exprs.contains(v) {
                    match_expr.is_exhaustive = false;
                    unhandled.push("`{}`".fmt(v));
                }
            }
        } else if expr_sym.info is .Enum(enum_info2) {
            for variant in enum_info2.variants {
                e := ".".concat(variant.name);
                if !branch_exprs.contains(e) {
                    match_expr.is_exhaustive = false;
                    unhandled.push("`{}`".fmt(e));
                }
            }
        } else {
            match_expr.is_exhaustive = false;
        }

        mut has_else := match_expr.has_else;
        mut else_branch := match_expr.branches[match_expr.branches.len - 1];
        if !has_else {
            for i, branch in match_expr.branches {
                if branch.is_else && i != match_expr.branches.len - 1 {
                    report.error("`else` must be the last branch of `match`", branch.pos);
                    else_branch = branch;
                    has_else = true;
                }
            }
        }
        if match_expr.is_exhaustive {
            if has_else {
                report.warn("`match` is exhaustive, `else` is unnecessary", else_branch.pos);
            }
            return match_expr.expected_type;
        }
        if has_else {
            return match_expr.expected_type;
        }

        mut err := report.error_builder("`match` must be exhaustive", match_expr.pos);
        if unhandled.len > 0 {
            mut err_details := "add `match` branches for: ";
            if unhandled.len < MATCH_EXHAUSTIVE_CUTOFF_LIMIT {
                err_details = err_details.concat(utils.join(unhandled[:], ", "));
            } else {
                err_details = err_details.concat(
                    utils.join(unhandled[:MATCH_EXHAUSTIVE_CUTOFF_LIMIT], ", ")
                );
                remaining := unhandled.len - MATCH_EXHAUSTIVE_CUTOFF_LIMIT;
                if remaining > 0 {
                    err_details = err_details.concat(", and {} others...".fmt(remaining));
                }
            }
            err.add_help(err_details);
        } else {
            err.add_help("add `else` branch at the end");
        }
        err.emit();
        return match_expr.expected_type;
    }
}
