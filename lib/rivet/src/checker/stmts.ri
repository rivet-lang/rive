// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ../{ ast, token, report };

extend Checker {
    func check_stmts(mut self, mut stmts: []+mut ast.Stmt) {
        mut unreachable_pos := token.no_pos;
        for mut stmt in stmts {
            if self.scope_returns && unreachable_pos.pos == 0 {
                unreachable_pos = stmt.position();
            }
            self.check_stmt(stmt);
        }
        if unreachable_pos.pos > 0 {
            report.warn("unreachable code detected", unreachable_pos);
        }
        self.scope_returns = false;
    }

    func check_stmt(mut self, mut stmt: +mut ast.Stmt) {
        match stmt {
            .ComptimeIf(mut comptime_if) -> {
                mut stmts := ast.nodes_to_stmts(
                    self.env.evalue_comptime_if(comptime_if)
                );
                self.check_stmts(stmts);
            },
            .VarDecl(mut var_decl) -> self.check_var_decl(
                var_decl.lefts, var_decl.right, var_decl.scope, var_decl.pos
            ),
            .For(mut for_stmt) -> self.check_for(for_stmt),
            .While(mut while_stmt) -> self.check_while(while_stmt),
            .Defer(mut defer_stmt) -> self.check_defer(defer_stmt),
            .Expr(mut expr) -> self.check_expr_evaluated_but_not_used(
                self.check_expr(expr), expr.position()
            ),
            else -> {}
        }
    }

    func check_for(mut self, mut for_stmt: +mut ast.Stmt.For) {
        prev_loop_control_tracker := self.loop_control_tracker;
        self.loop_control_tracker = .AnyLoop;
        defer self.loop_control_tracker = prev_loop_control_tracker;

        iterable_t := self.check_expr(for_stmt.iterable);
        iterable_sym := iterable_t.symbol()?;
        if iterable_sym.info is .Array || iterable_sym.info is .DynArray
            || iterable_sym.info is .Slice {
            mut elem_type := self.env.comptime_number_to_type(
                iterable_sym.info.elem_type()?
            );
            elem_sym := elem_type.symbol()?;
            if for_stmt.values.len == 1 {
                value0 := for_stmt.values[0];
                if value0.is_mut {
                    if !iterable_sym.info.is_mut_array() {
                        report.error(
                            "cannot modify immutable {}".fmt(iterable_sym.info),
                            for_stmt.iterable.position()
                        );
                    } else {
                        self.check_expr_is_mut(for_stmt.iterable);
                    }
                } else if value0.is_ref {
                    if elem_sym.is_boxed() {
                        report.error("cannot take the address of a boxed value", value0.pos);
                    }
                    elem_type = .Pointer(elem_type);
                }
            }
            if for_stmt.has_index {
                self.check_name_case(
                    .Snake, "index variable", for_stmt.index.name, for_stmt.index.pos
                );
                for_stmt.index.sym.type = self.env.uint_t;
            }
            if for_stmt.values.len == 1 {
                mut value0 := for_stmt.values[0];
                self.check_name_case(.Snake, "variable", value0.name, value0.pos);
                value0.sym.type = elem_type;
                value0.sym.is_hidden_ref = value0.is_mut;
            } else {
                types := @as(ast.TypeInfo.Tuple, elem_type.symbol()?.info).types;
                for i, mut value in for_stmt.values {
                    self.check_name_case(.Snake, "variable", value.name, value.pos);
                    if value.is_ref && elem_sym.is_boxed() {
                        report.error("cannot take the address of a boxed value", value.pos);
                    }
                    value.sym.type = types[i];
                    value.sym.is_hidden_ref = value.is_mut;
                }
            }
            self.check_stmt(for_stmt.stmt);
        } else {
            mut err := report.error_builder(
                "`{}` is not an iterable type".fmt(iterable_t),
                for_stmt.iterable.position()
            );
            err.add_note("expected array value");
            err.emit();
        }
    }

    func check_while(mut self, mut while_stmt: +mut ast.Stmt.While)  {
        prev_loop_control_tracker := self.loop_control_tracker;
        self.loop_control_tracker = .AnyLoop;
        defer self.loop_control_tracker = prev_loop_control_tracker;

        if !while_stmt.is_inf && self.check_expr(
            while_stmt.cond
        ) != self.env.bool_t {
            if while_stmt.cond !is .Guard {
                report.error(
                    "non-boolean expression used as `while` condition",
                    while_stmt.cond.position()
                );
            }
        }
        self.check_stmt(while_stmt.stmt);
        if while_stmt.has_continue_expr {
            _ = self.check_expr(while_stmt.continue_expr);
        }
        if while_stmt.has_else_stmt {
            self.check_stmt(while_stmt.else_stmt);
        }
    }

    func check_defer(mut self, mut defer_stmt: +mut ast.Stmt.Defer) {
        prev_return_tracker := self.return_tracker;
        self.return_tracker = .Defer;
        defer self.return_tracker = prev_return_tracker;

        prev_loop_control_tracker := self.loop_control_tracker;
        self.loop_control_tracker = .Defer;
        defer self.loop_control_tracker = prev_loop_control_tracker;

        if self.cur_func.ret_type is .Never {
            report.error("`never` functions cannot use `defer` statements", defer_stmt.pos);
        }

        self.check_expr_evaluated_but_not_used(
            self.check_expr(defer_stmt.expr), defer_stmt.expr.position()
        );
        self.defer_stmts.push(defer_stmt);
    }
}
