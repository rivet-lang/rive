// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ./mir;
import ../ast;

extend Codegen {
    func gen_decls(mut self, mut decls: []+mut ast.Decl) {
        for mut decl in decls {
            self.gen_decl(decl);
        }
    }

    func gen_decl(mut self, mut decl: +mut ast.Decl) {
        match decl {
            .Extern(mut extern_decl) -> self.gen_decls(extern_decl.decls),
            .Var(var_decl) -> {
                for left in var_decl.lefts {
                    self.mir.globals.push(mir.Global(
                        is_extern: left.is_extern,
                        name: left.name,
                        type: self.type_to_mir(left.type)
                    ));
                }
            },
            .Trait(mut trait_decl) -> self.gen_decls(trait_decl.decls),
            .Enum(mut enum_decl) -> self.gen_decls(enum_decl.decls),
            .Struct(mut struct_decl) -> self.gen_decls(struct_decl.decls),
            .Extend(mut extend_decl) -> self.gen_decls(extend_decl.decls),
            .Func(mut func_decl) -> self.gen_func(func_decl),
            else -> {}
        }
    }

    func gen_func(mut self, mut func_decl: +mut ast.Decl.Func) {
        ret_type_is_void := func_decl.ret_type.is_void();
        self.cur_func = mir.Func(
            name: if func_decl.is_extern && func_decl.abi != .Rivet {
                func_decl.name
            } else {
                self.mangle_symbol(func_decl.sym)
            },
            args_len: func_decl.args.len + if func_decl.is_method { 1 } else { 0 },
            ret_type: self.type_to_mir(func_decl.ret_type),
            is_extern: func_decl.is_extern
        );
        self.add_locals(func_decl.scope);
        if !ret_type_is_void {
            self.cur_func.add_local(mir.Var(
                dbg_name: "_return_value",
                level: .Local,
                type: self.type_to_mir(func_decl.ret_type)
            ));
        }
        self.cur_func.cur_block = mir.Block();
        self.cur_func.add_block(self.cur_func.cur_block);
        self.gen_stmts(func_decl.stmts);
        self.cur_func.add_block(
            mir.Block(stmts: +[
                if ret_type_is_void {
                    .Return()
                } else {
                    .Return(true, .Ident(self.cur_func.locals[self.cur_func.locals.len - 1]))
                }
            ])
        );
        if self.cur_func.is_extern {
            self.mir.externs.push(self.cur_func);
        } else {
            self.mir.funcs.push(self.cur_func);
        }
    }
}
