// Copyright (C) 2023 Jose Mendoza. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import std/traits;
import std/strings;
import std/process;

#[boxed; default_value(.Void)]
pub enum Type < traits.Stringable {
    Void,
    Never,
    SizedInt {
        bits: uint;
        size: uint;
    },
    SizedUint {
        bits: uint;
        size: uint;
    },
    Basic {
        name: string;
        size: uint;
        is_primitive: bool;
    },
    Rawptr(uint),
    Pointer {
        inner: Type;
        is_managed: bool;
        size: uint;
    },
    Array {
        elem_type: Type;
        count: uint;
        size: uint;
    },
    Func {
        args: []Type;
        ret_type: Type;
        size: uint;
    };

    pub func size(self) -> uint {
        return match self {
            .SizedInt(int) -> int.size,
            .SizedUint(uint) -> uint.size,
            .Basic(basic) -> basic.size,
            .Rawptr(size) -> size,
            .Pointer(ptr) -> ptr.size,
            .Array(arr) -> arr.size,
            .Func(func_) -> func_.size,
            .Void, .Never -> process.panic("codegen.mir.Type.size: `void` or `never` type use")
        };
    }

    pub func to_string(self) -> string {
        return match self {
            .Void -> "void",
            .Never -> "never",
            .SizedInt(int) -> "int{}".fmt(int.bits),
            .SizedUint(uint) -> "uint{}".fmt(uint.bits),
            .Basic(basic) -> if basic.is_primitive {
                basic.name
            } else {
                "@".concat(basic.name)
            },
            .Rawptr -> "rawptr",
            .Pointer(ptr) -> if ptr.is_managed {
                "^"
            } else {
                "*"
            }.concat(ptr.inner.to_string()),
            .Array(arr) -> "[{}]{}".fmt(arr.count, arr.elem_type),
            .Func(fn) -> {
                mut sb := strings.Builder.from_string("*func(");
                for i, arg in fn.args {
                    sb.write_string(arg.to_string());
                    if i < fn.args.len - 1 {
                        sb.write_string(", ");
                    }
                }
                sb.write_fmt(") -> {}", fn.ret_type);
                sb.to_string()
            }
        };
    }
}
