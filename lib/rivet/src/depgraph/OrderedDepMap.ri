// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import std/process;

import ../utils/maps;

#[boxed]
pub struct OrderedDepMap {
    mut keys: []string;
    mut data: maps.MapStringArrayOfStrings; // TODO: replace with map type

    pub func new() -> Self {
        return Self();
    }

    pub func set(mut self, name: string, deps: []string) {
        if !self.data.contains(name) {
            self.keys.push(name);
        }
        self.data.set(name, deps);
    }

    pub func add(mut self, name: string, deps: []string) {
        mut d := self.get(name);
        for dep in deps {
            if dep !in d {
                d.push(dep);
            }
        }
        self.set(name, d);
    }

    #[inline]
    pub func get(self, name: string) -> []string {
        return if res := self.data.get(name) {
            res
        } else {
            []
        };
    }

    pub func delete(mut self, name: string) {
        if !self.data.contains(name) {
            process.panic("OrderedDepMap.delete(): no such key: `{}`", name);
        }
        for i, _ in self.keys {
            if self.keys[i] == name {
                self.keys.delete(i, no_slices: true);
                break;
            }
        }
        self.data.delete(name);
    }

    pub func apply_diff(mut self, name: string, deps: []string) {
        mut diff := @vec(string);
        deps_of_name := self.get(name);
        for dep in deps_of_name {
            if dep !in deps {
                diff.push(dep);
            }
        }
        self.set(name, diff);
    }

    pub func size(self) -> uint {
        return self.data.len();
    }
}
