// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this 
// source code is governed by an MIT license that can be found in the LICENSE 
// file.

import ../ast;

extend Formatter {
    func format_attributes(mut self, attrs: ast.Attributes) {
        if attrs.is_empty() {
            return;
        }
        self.write("#[");
        for i, attr in attrs.attributes {
            self.write(attr.name);
            if !attr.args.is_empty() {
                self.write("(");
                for i2, arg in attr.args {
                    if arg.is_named {
                        self.write_fmt("{}: ", arg.name);
                    }
                    self.format_expr(arg.expr);
                    if i2 < attr.args.len - 1 {
                        self.write(", ");
                    }
                }
                self.write(")");
            }
            if i < attrs.attributes.len - 1 {
                self.write("; ");
            }
        }
        self.writeln("]");
    }
}
