// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ../ast;

extend Formatter {
    enum CommentsLevel as uint8 {
        Keep,
        Indent
    }

    // Optional arguments defines the way comments are going to be written
    // - has_nl: adds an newline at the end of a list of comments
    // - same_line: line comments will be on the same line as the last statement
    // - level:  either .Keep (don't indent), or .Indent (increment indentation)
    // - prev_line: the line number of the previous token to save linebreaks
    func format_comment(
        ^mut self, comment: ast.Comment, same_line: bool := false,
        level: CommentsLevel := .Keep, has_nl: bool := false
    ) {
        if level == .Indent {
            self.indent += 1;
        }
        if !comment.is_multiline() {
            is_separate_line := !same_line || comment.text.starts_with("\x01");
            s := comment.text.trim_left("\x01").trim_right(" ");
            mut out_s := if comment.is_doc { "///" } else { "//" };
            if s != "" {
                if s[0].is_alnum() {
                    out_s = out_s.concat(" ");
                }
                out_s = out_s.concat(s);
            }
            if !is_separate_line && self.indent > 0 {
                _ = self.remove_new_line(); // delete the generated \n
                self.write(" ");
            }
            self.write(out_s);
            if has_nl {
                self.writeln();
            }
        } else {
            lines := comment.text.split_into_lines();
            self.write("/*");
            for i, line in lines {
                if i == lines.len - 1 {
                    self.empty_line = true;
                    if comment.text[comment.text.len - 1] == '\n' {
                        self.writeln(line);
                    } else {
                        self.write(line);
                    }
                    self.write("*/");
                } else {
                    self.empty_line = false;
                    self.writeln(line.trim_right(" "));
                }
            }
        }
        if level == .Indent {
            self.indent -= 1;
        }
    }

    func format_comments(
        ^mut self, comments: []ast.Comment, has_nl: bool := true,
        same_line: bool := false, level: CommentsLevel := .Keep, prev_line_: ?uint := none
    ) {
        mut prev_line := prev_line_ ?? 0;
        for i, comment in comments {
            if prev_line_ != none
                && (
                    (
                        comment.pos.line > prev_line && self.output.len() > 1
                        && self.output.last_n(1) != "\n"
                    ) || (
                        comment.pos.line > prev_line + 1 && self.output.len() > 2
                        && self.output.last_n(2) != "\n\n"
                    )
                ) {
                self.writeln();
            }
            if i == 0 && self.output.len() > 1 && !self.output.last_n(1)[0].is_space() {
                self.write(" ");
            }
            self.format_comment(comment, same_line, level);
            if i < comments.len - 1 || has_nl {
                self.writeln();
            }
            prev_line = comment.pos.end_line;
        }
    }
}
