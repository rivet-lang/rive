// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import std/traits;
import std/console;
import std/process;
import { Path, Directory } from std/fs;

import ./ast;
import ./utils;
import ./prefs;
import ./token;
import ./parser;
import ./report;
import ./checker;
import ./codegen;
import ./resolver;
import ./depgraph;

#[boxed]
pub struct Compiler {
    mut table: ast.Table;
    mut prefs: prefs.Prefs;

    mut resolver: resolver.Resolver;
    mut checker: checker.Checker;
    mut codegen: codegen.Codegen;

    pub func new(args: []string, is_test: bool, run_output: bool, is_check: bool) -> ! {
        prefs_ := prefs.Prefs.new(args, is_test, run_output, is_check)!;
        report.reportTable.prefs = prefs_;
        table_ := ast.Table.new(prefs_);
        Self(
            table: table_, prefs: prefs_,
            resolver: resolver.Resolver(table_, prefs_),
            checker: checker.Checker(table_, prefs_),
            codegen: codegen.Codegen(table_, prefs_)
        ).run()!;
    }

    func run(mut self) -> ! {
        self.make_rivet_directory()!;

        _ = self.load_module("core", "core", "", token.noPos)!;
        self.load_root_module()!;
        self.import_modules()!;

        if !self.prefs.check_syntax {
            self.vlog("resolving files...");
            self.resolver.resolve_files(self.table.source_files);
            if report.total_errors() > 0 {
                self.abort();
            }

            self.vlog("checking files...");
            self.checker.check_files(self.table.source_files);
            if report.total_errors() > 0 {
                self.abort();
            }

            if !self.prefs.check {
                self.vlog("generating MIR from AST...");
                self.codegen.generate_mirs(self.table.source_files);
                if report.total_errors() > 0 {
                    self.abort();
                }
            }
        }

        total_warns := report.total_warns();
        if total_warns > 0 {
            utils.warn(
                "module `{}` generated {} {}", self.prefs.mod_name, total_warns,
                if total_warns == 1 {
                    "warning"
                } else {
                    "warnings"
                }
            );
        }
    }

    func make_rivet_directory(self) -> ! {
        if !Path.exists(prefs.rivetDir) {
            self.vlog("making Rivet directory");
            Directory.make(prefs.rivetDir)!;
            Directory.make(prefs.objDir)!;
            Directory.make(prefs.libDir)!;
        }
    }

    func load_root_module(mut self) -> ! {
        files := if Path.is_directory(self.prefs.input) {
            mut filtered_files := self.table.filter_files(
                Directory.walk(self.prefs.input, ".ri")!
            );
            src_dir := Path.join(self.prefs.input, "src")!;
            if Path.is_directory(src_dir) { // support `src/` directory
                for file in self.table.filter_files(Directory.walk(src_dir, ".ri")!) {
                    filtered_files.push(file);
                }
            }
            filtered_files
        } else {
            [self.prefs.input]
        };
        if files.is_empty() {
            utils.error("no input received");
        } else if self.prefs.is_verbose {
            self.vlog("Compiler.load_root_module(): loaded root module files:");
            for file in files {
                self.vlog("   > {}", file);
            }
        }
        root_sym := ast.Module(name: self.prefs.mod_name, is_root: true);
        self.table.universe.scope.add(root_sym) catch |err| utils.error(
            "Compiler.load_root_module(): {}", err
        );
        self.parse_files(root_sym, files);
    }

    func import_modules(mut self) -> ! {
        for sf in self.table.source_files {
            for d in sf.decls {
                if d is .Import as import_decl {
                    if self.table.universe.scope.exists(import_decl.info.full_name) {
                        continue;
                    }
                    import_decl.info = self.load_module(
                        import_decl.path, import_decl.alias_name, sf.path,
                        import_decl.path_pos
                    )!;
                    import_decl.alias_name = import_decl.info.alias_name;
                }
            }
        }
        self.vlog("loaded modules...");
        self.resolve_deps();
        if report.total_errors() > 0 {
            self.abort();
        }
    }

    func load_module(
        mut self, pathx: string, alias_name: string, file_path: string, pos: token.Pos
    ) -> !ast.ImportedMod {
        mod := self.load_module_files(pathx, alias_name, file_path, pos)!;
        if mod.found {
            mod.mod = if mod_mod_ := self.table.universe.scope.find(mod.full_name) {
                @as(ast.Module, mod_mod_)
            } else {
                mod_sym := ast.Module(name: mod.full_name);
                self.table.universe.scope.add(mod_sym) catch |err| {
                    report.error(err.to_string(), pos)
                };
                self.parse_files(mod_sym, mod.files);
                mod_sym
            };
        }
        return mod;
    }

    func load_module_files(
        self, pathx: string, alias_name: string, file_path: string, pos: token.Pos
    ) -> !ast.ImportedMod {
        self.vlog("Compiler.load_module_files(): searching module `{}`", pathx);
        mut found := false;
        mut name := "";
        mut full_name := "";
        mut abspath := "";
        mut files := @vec(string);
        is_super := pathx.starts_with("../");
        if pathx.starts_with("./") or is_super {
            self.vlog("    searching module in local path");
            pathx2 := pathx[if is_super { 3 } else { 2 }..];
            name = if last_idx := pathx2.last_index_of_byte(b'/') {
                pathx2[last_idx + 1..]
            } else {
                pathx2
            };
            dir_name := Path.resolve(Path.dir_name(file_path) ?? file_path)!;
            old_wd := process.get_cwd()!;
            process.set_cwd(dir_name)!;
            if Path.is_directory(pathx) {
                found = true;
                abspath = Path.resolve(pathx)!;
                mut mod_basedir := Path.dir_name(abspath) ?? abspath;
                if mod_basedir.ends_with("/src") {
                    mod_basedir = mod_basedir[..mod_basedir.len - 4]; // skip `/src`
                } else if mod_basedir.contains("/src") {
                    first_part := mod_basedir[..mod_basedir.last_index_of_byte(b'/') ?? 0];
                    mod_basedir = mod_basedir[..first_part.last_index_of_byte(b'/') ?? 0];
                }
                names := abspath[(mod_basedir.last_index_of_byte(b'/') ?? 0) + 1..].split(
                    "/"
                );
                full_name = if "src" in names {
                    src_idx := utils.index_of(names, "src");
                    utils.join(names[..src_idx], ".").concat(
                        ".", utils.join(names[src_idx + 1..], ".")
                    )
                } else {
                    utils.join(names, ".")
                };
            }
            if found {
                self.vlog("      > found");
                files = self.table.filter_files(Directory.walk(pathx, ".ri")!);
            }
            process.set_cwd(old_wd)!;
        } else {
            self.vlog("    searching module in global path");
            name = if last_idx := pathx.last_index_of_byte(b'/') {
                pathx[last_idx + 1..]
            } else {
                pathx
            };
            full_name = pathx.replace("/", ".");
            for l in self.prefs.library_path {
                self.vlog("        searching module in `{}`", l);
                mod_path := Path.resolve(Path.join(l, pathx)!)!;
                if Path.is_directory(mod_path) {
                    found = true;
                    files = self.table.filter_files(Directory.walk(mod_path, ".ri")!);
                }
                // support `src/` directory
                src_dir := if pathx.contains("/") {
                    slash_idx := (pathx.index_of_byte(b'/') ?? 0) + 1;
                    Path.join(l, pathx[..slash_idx], "src", pathx[slash_idx..])!
                } else {
                    Path.join(mod_path, "src")!
                };
                if Path.is_directory(src_dir) {
                    if !found {
                        found = true;
                    }
                    files = self.table.filter_files(Directory.walk(src_dir, ".ri")!);
                }
                if found {
                    self.vlog("        > found");
                    break;
                }
            }
        }
        if !found {
            self.vlog("        > module not found", pathx);
            report.error("module `{}` not found".fmt(pathx), pos);
        } else if files.is_empty() {
            self.vlog("        > module contains no rivet files", pathx);
            report.error("module `{}` contains no rivet files".fmt(pathx), pos);
        } else {
            self.vlog("    module `{}` loaded", pathx);
        }
        return ast.ImportedMod(
            found: found, name: name,
            alias_name: if alias_name.is_empty() { name } else { alias_name },
            full_name: full_name,
            files: files
        );
    }

    func parse_files(mut self, mod_sym: ast.Module, files: []string) {
        for sf in parser.Parser(table: self.table, prefs: self.prefs).parse_module(
            mod_sym, files
        ) {
            self.table.source_files.push(sf);
        }
        if report.total_errors() > 0 {
            self.abort();
        }
    }

    func resolve_deps(mut self) {
        g := self.import_graph();
        g_resolved := g.resolve();
        self.vlog(
            "-----= resolved dependencies graph =-----\n".concat(g_resolved.display())
        );
        self.vlog("-----------------------------------------");
        cycles := g_resolved.display_cycles();
        if cycles.len > 1 {
            utils.error("import cycle detected between the following modules:\n{}", cycles);
        }
        self.vlog("----------= imported modules =-----------");
        for node in g_resolved.nodes {
            self.vlog("> {}", node.name);
        }
        self.vlog("-----------------------------------------");
        source_files := self.table.source_files;
        self.table.source_files = @vec(ast.SourceFile, source_files.len);
        for node in g_resolved.nodes {
            for pf in source_files {
                if pf.mod.name == node.name {
                    self.table.source_files.push(pf);
                }
            }
        }
        self.vlog("module dependencies resolved...");
    }

    func import_graph(self) -> depgraph.DepGraph {
        mut g := depgraph.DepGraph.new();
        for pf in self.table.source_files {
            mut deps := @vec(string);
            if pf.mod.name !in ["c.ctypes", "c.libc", "c", "core"] {
                deps.push("core");
            }
            for d in pf.decls {
                if d is .Import as import_decl {
                    if !import_decl.info.found {
                        continue; // module not found
                    }
                    if import_decl.info.full_name == pf.mod.name {
                        report.error("import cycle detected", import_decl.pos);
                        continue;
                    }
                    deps.push(import_decl.info.full_name);
                }
            }
            g.add(pf.mod.name, deps);
        }
        return g;
    }

    func vlog(self, msg: string, args: ...traits.Stringable) {
        if self.prefs.is_verbose {
            console.writeln(utils.bold(utils.green("[rivet-log] ")).concat(msg), args);
        }
    }

    func abort(self) {
        mut msg := if report.total_errors() == 1 {
            "could not compile module `{}`, aborting due to previous error".fmt(self.prefs.mod_name)
        } else {
            "could not compile module `{}`, aborting due to {} previous errors".fmt(
                self.prefs.mod_name, report.total_errors()
            )
        };
        total_warns := report.total_warns();
        if total_warns > 0 {
            msg = msg.concat("; {} {} emitted").fmt(total_warns, if total_warns == 1 {
                "warning"
            } else {
                "warnings"
            });
        }
        utils.error(msg);
    }
}
