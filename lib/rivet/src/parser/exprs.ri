// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import ../ast;
import ../report;
import ../token;

extend Parser {
    [inline]
    func parse_expr(mut self) ast.Expr {
        return self.parse_or_expr();
    }

    func parse_or_expr(mut self) ast.Expr {
        mut left := self.parse_and_expr();
        while self.accept(.KwOr) {
            right := self.parse_and_expr();
            left = .Binary(
                left: left,
                right: right,
                op: .KwOr,
                pos: left.position() + right.position()
            );
        }
        return left;
    }

    func parse_and_expr(mut self) ast.Expr {
        mut left := self.parse_equality_expr();
        while self.accept(.KwAnd) {
            right := self.parse_equality_expr();
            left = .Binary(
                left: left,
                right: right,
                op: .KwAnd,
                pos: left.position() + right.position()
            );
        }
        return left;
    }

    func parse_equality_expr(mut self) ast.Expr {
        mut left := self.parse_relational_expr();
        while {
            if self.tok.kind in [.Eq, .Ne] {
                op := self.tok.kind;
                self.next();
                right := self.parse_relational_expr();
                left = .Binary(
                    left: left,
                    right: right,
                    op: op,
                    pos: left.position() + right.position()
                );
            } else {
                break;
            }
        }
        return left;
    }

    func parse_relational_expr(mut self) ast.Expr {
        mut left := self.parse_shift_expr();
        while {
            left = switch self.tok.kind {
                .Gt, .Lt, .Ge, .Le, .OrElse, .KwIn, .KwNotIn => {
                    op := self.tok.kind;
                    self.next();
                    right := self.parse_shift_expr();
                    .Binary(
                        left: left,
                        right: right,
                        op: op,
                        pos: left.position() + right.position()
                    )
                },
                .KwIs, .KwNotIs => {
                    if self.inside_switch_header and self.peek_tok.kind == .Lbrace {
                        break;
                    }
                    op := self.tok.kind;
                    self.next();
                    pos := self.tok.pos;
                    right := if self.accept(.Dot) {
                        ast.Expr.EnumLiteral(
                            self.parse_name(), from_is_cmp: true,
                            pos: pos + self.prev_tok.pos
                        )
                    } else if self.accept(.KwNone) {
                        .Type(.None())
                    } else {
                        .Type(self.parse_type())
                    };
                    if self.accept(.KwAs) {
                        .Binary(
                            left: left,
                            right: right,
                            op: op,
                            has_var_obj: true,
                            var_obj: self.parse_var_decl(support_ref: false),
                            scope: self.scope,
                            pos: left.position() + self.prev_tok.pos
                        )
                    } else {
                        .Binary(
                            left: left,
                            right: right,
                            op: op,
                            pos: left.position() + self.prev_tok.pos
                        )
                    }
                },
                else => break
            };
        }
        return left;
    }

    func parse_shift_expr(mut self) ast.Expr {
        mut left := self.parse_additive_expr();
        while {
            left = switch self.tok.kind {
                .Lt, .Gt => {
                    op: token.Kind := if self.tok.kind == .Lt { .Lshift } else { .Rshift };
                    if self.tok.pos.pos + 1 == self.peek_tok.pos.pos {
                        self.advance(2);
                        right := self.parse_additive_expr();
                        .Binary(
                            left: left,
                            right: right,
                            op: op,
                            pos: left.position() + right.position()
                        )
                    } else {
                        break
                    }
                },
                .Amp, .Pipe, .Xor => {
                    op := self.tok.kind;
                    self.next();
                    right := self.parse_additive_expr();
                    .Binary(
                        left: left,
                        right: right,
                        op: op,
                        pos: left.position() + right.position()
                    )
                },
                else => break
            };
        }
        return left;
    }

    func parse_additive_expr(mut self) ast.Expr {
        mut left := self.parse_multiplicative_expr();
        while {
            switch self.tok.kind {
                .Plus, .Minus => {
                    op := self.tok.kind;
                    self.next();
                    right := self.parse_multiplicative_expr();
                    left = .Binary(
                        left: left,
                        right: right,
                        op: op,
                        pos: left.position() + right.position()
                    );
                },
                else => break
            }
        }
        return left;
    }

    func parse_multiplicative_expr(mut self) ast.Expr {
        mut left := self.parse_unary_expr();
        while {
            switch self.tok.kind {
                .Mul, .Div, .Mod => {
                    op := self.tok.kind;
                    self.next();
                    right := self.parse_unary_expr();
                    left = .Binary(
                        left: left,
                        right: right,
                        op: op,
                        pos: left.position() + right.position()
                    );
                },
                else => break
            }
        }
        return left;
    }

    [inline]
    func parse_unary_expr(mut self) ast.Expr {
        return switch self.tok.kind {
            .Amp, .Bang, .BitNot, .Minus => {
                op := self.tok.kind;
                pos := self.tok.pos;
                self.next();
                is_ref_mut := op == .Amp and self.accept(.KwMut);
                right := self.parse_unary_expr();
                .Unary(right, op, is_ref_mut, pos + right.position())
            },
            else => self.parse_primary_expr()
        };
    }

    func parse_primary_expr(mut self) ast.Expr {
        mut expr: ast.Expr := switch {
            self.tok.kind in [
                .KwTrue, .KwFalse, .Char, .Number, .String,
                .KwNone, .KwSelf, .KwSelfTy
            ] => self.parse_literal(),
            self.accept(.At) => {
                pos := self.prev_tok.pos;
                if self.peek_token(1).kind == .Lparen { // builtin call
                    name := self.parse_name();
                    mut args := @vec(ast.CallArg);
                    self.expect(.Lparen);
                    mut vec_is_mut := false;
                    if name in ["vec", "cast", "size_of", "align_of"] {
                        vec_is_mut = name == "vec" and self.accept(.KwMut);
                        arg_pos := self.tok.pos;
                        args.push(
                            ast.CallArg(
                                expr: .Type(self.parse_type()),
                                pos: arg_pos + self.prev_tok.pos
                            )
                        );
                        if self.tok.kind != .Rparen {
                            self.expect(.Comma);
                        }
                    }
                    if self.tok.kind != .Rparen {
                        while {
                            arg := self.parse_expr();
                            args.push(ast.CallArg(expr: arg, pos: arg.position()));
                            if !self.accept(.Comma) {
                                break;
                            }
                        }
                    }
                    self.expect(.Rparen);
                    .BuiltinCall(name, args, vec_is_mut, pos + self.prev_tok.pos)
                } else {
                    self.parse_ident(true) // builtin variable
                }
            },
            self.tok.kind == .Dot and self.peek_tok.kind == .Name => {
                pos := self.tok.pos;
                self.next();
                .EnumLiteral(self.parse_name(), pos: pos + self.prev_tok.pos)
            },
            self.tok.kind in [.KwContinue, .KwBreak] => {
                op := self.tok.kind;
                pos := self.tok.pos;
                self.next();
                .Branch(op, pos)
            },
            self.accept(.KwReturn) => {
                pos := self.prev_tok.pos;
                has_expr := self.tok.kind !in [.Comma, .Semicolon, .Rbrace];
                return_expr := if has_expr {
                    self.parse_expr()
                } else {
                    .Empty(self.tok.pos)
                };
                .Return(return_expr, has_expr, pos + return_expr.position())
            },
            self.tok.kind == .KwIf => self.parse_if_expr(),
            self.accept(.KwSwitch) => self.parse_switch_expr(),
            self.tok.kind in [.KwUnsafe, .Lbrace] => self.parse_block_expr(),
            self.tok.kind == .Lparen => {
                mut pos := self.tok.pos;
                self.next();
                e := self.parse_expr();
                if self.accept(.Comma) { // tuple
                    mut exprs: []mut ast.Expr := [e];
                    while {
                        exprs.push(self.parse_expr());
                        if !self.accept(.Comma) {
                            break;
                        }
                    }
                    pos += self.tok.pos;
                    self.expect(.Rparen);
                    if exprs.len > 8 {
                        report.error("tuples can have a maximum of 8 expressions", pos);
                    }
                    .TupleLiteral(exprs, pos)
                } else {
                    self.expect(.Rparen);
                    .Paren(e, pos + e.position() + self.prev_tok.pos)
                }
            },
            self.tok.kind == .Lbracket => {
                mut elems := @vec(mut ast.Expr);
                mut pos := self.tok.pos;
                self.next();
                if self.tok.kind != .Rbracket {
                    while {
                        elems.push(self.parse_expr());
                        if !self.accept(.Comma) {
                            break;
                        }
                    }
                }
                pos += self.tok.pos;
                self.expect(.Rbracket);
                .VectorLiteral(elems, self.accept(.Bang), pos)
            },
            self.tok.kind == .Name => if self.peek_tok.kind == .Char {
                if self.tok.lit == "b" {
                    self.parse_character_literal()
                } else {
                    report.error(
                        "only `b` is recognized as a valid prefix for a character literal",
                        self.tok.pos
                    );
                    self.next();
                    .Empty(self.prev_tok.pos)
                }
            } else if self.peek_tok.kind == .String {
                if self.tok.lit in ["c", "b", "r"] {
                    self.parse_string_literal()
                } else {
                    report.error(
                        "only `c`, `b` and `r` are recognized as valid prefixes for a string literal",
                        self.tok.pos
                    );
                    self.next();
                    .Empty(self.prev_tok.pos)
                }
            } else {
                self.parse_ident()
            },
            else => .Empty(self.tok.pos)
        };
        while {
            expr = switch {
                self.tok.kind.is_assign() => {
                    // assignment
                    op := self.tok.kind;
                    pos := self.tok.pos;
                    self.expect(op);
                    right := self.parse_expr();
                    .Assign(expr, op, right, pos + right.position())
                },
                self.tok.kind == .Lparen and !self.decl_operator_is_used() => {
                    self.next();
                    mut args := @vec(ast.CallArg);
                    mut spread_expr := ast.Expr.Empty(self.tok.pos);
                    mut has_spread_expr := false;
                    if self.tok.kind != .Rparen {
                        mut expecting_named_arg := false;
                        while {
                            if self.tok.kind == .Name and self.peek_tok.kind == .Colon {
                                // named argument
                                name := self.parse_name();
                                name_p := self.tok.pos;
                                self.expect(.Colon);
                                expr2 := self.parse_expr();
                                args.push(ast.CallArg(name, expr2, !name.is_empty(), name_p));
                                expecting_named_arg = true;
                            } else if self.accept(.Ellipsis) {
                                mut spread_pos := self.prev_tok.pos;
                                spread_expr = self.parse_expr();
                                spread_pos += spread_expr.position();
                                has_spread_expr = true;
                                if self.tok.kind == .Comma {
                                    report.error("spread expression must be the final argument", spread_pos);
                                }
                            } else {
                                if expecting_named_arg {
                                    report.error("expected named argument, found single expression", self.tok.pos);
                                }
                                expr2 := self.parse_expr();
                                args.push(ast.CallArg(expr: expr2, pos: expr2.position()));
                            }
                            if !self.accept(.Comma) {
                                break;
                            }
                        }
                    }
                    self.expect(.Rparen);
                    mut err_handler_pos := self.tok.pos;
                    mut is_propagate := false;
                    mut varname := "";
                    mut varname_pos := self.tok.pos;
                    mut has_varname := false;
                    mut err_expr := ast.Expr.Empty(self.tok.pos);
                    mut has_err_expr := false;
                    if self.accept(.Bang) {
                        // check result value, if error propagate
                        is_propagate = true;
                        err_handler_pos = self.prev_tok.pos;
                    } else if self.accept(.KwCatch) {
                        if self.accept(.Pipe) {
                            has_varname = true;
                            varname_pos = self.tok.pos;
                            varname = self.parse_name();
                            self.expect(.Pipe);
                        }
                        err_expr = self.parse_expr();
                        has_err_expr = true;
                    }
                    .Call(
                        left: expr,
                        args: args,
                        spread_expr: spread_expr,
                        has_spread_expr: has_spread_expr,
                        err_handler: ast.CallErrorHandler(
                            varname: varname,
                            varname_pos: varname_pos,
                            has_varname: has_varname,
                            expr: err_expr,
                            has_expr: has_err_expr,
                            is_propagate: is_propagate,
                            scope: self.scope,
                            pos: err_handler_pos
                        ),
                        pos: expr.position() + self.prev_tok.pos
                    )
                },
                self.accept(.Lbracket) => {
                    index: ast.Expr := if self.accept(.DotDot) {
                        if self.tok.kind == .Rbracket {
                            .Range(pos: expr.position() + self.tok.pos)
                        } else {
                            .Range(
                                end: self.parse_expr(), has_end: true,
                                pos: expr.position() + self.prev_tok.pos
                            )
                        }
                    } else {
                        start := self.parse_expr();
                        if self.accept(.DotDot) {
                            if self.tok.kind == .Rbracket {
                                .Range(
                                    start: start, has_start: true,
                                    pos: expr.position() + self.tok.pos
                                )
                            } else {
                                end := self.parse_expr();
                                .Range(
                                    start: start, end: end,
                                    has_start: true, has_end: true,
                                    pos: expr.position() + end.position()
                                )
                            }
                        } else {
                            start
                        }
                    };
                    self.expect(.Rbracket);
                    .Index(
                        left: expr, index: index,
                        pos: expr.position() + self.prev_tok.pos
                    )
                },
                // avoid `expr` + `.EnumLiteral` concatenation
                self.prev_tok.pos.line != self.tok.pos.line and self.tok.kind == .Dot
                and self.peek_tok.kind == .Name and self.peek_tok.lit[0].is_upper() => break,
                self.accept(.Dot) => if self.accept(.Mul) {
                    .Indirect(
                        left: expr,
                        pos: expr.position() + self.prev_tok.pos
                    )
                } else {
                    self.parse_selector_expr(expr)
                },
                self.accept(.Question) => {
                    // check option value, panic if is `none`: `x?`
                    .OptionCheck(
                        left: expr,
                        pos: expr.position() + self.prev_tok.pos
                    )
                },
                else => break
            }
        }
        return expr;
    }

    func parse_if_expr(mut self) ast.Expr {
        mut branches := @vec(ast.IfBranch);
        mut has_else := false;
        mut pos := self.tok.pos;
        while self.tok.kind in [.KwIf, .KwElse] {
            branch_pos := self.tok.pos;
            if self.accept(.KwElse) and self.tok.kind != .KwIf {
                branches.push(
                    ast.IfBranch(
                        .Empty(self.tok.pos), self.parse_block_expr(),
                        true, pos: branch_pos + self.prev_tok.pos
                    )
                );
                has_else = true;
                break;
            }
            is_else_if := self.prev_tok.kind == .KwElse;
            self.expect(.KwIf);
            cond := if self.decl_operator_is_used() {
                self.open_scope();
                self.parse_guard_expr()
            } else {
                self.parse_expr()
            };
            branches.push(
                ast.IfBranch(
                    cond, self.parse_block_expr(), is_else_if: is_else_if,
                    pos: branch_pos + self.prev_tok.pos
                )
            );
            if cond is .Guard {
                self.close_scope();
            }
            if self.tok.kind != .KwElse {
                break;
            }
        }
        pos += self.prev_tok.pos;
        return .If(branches, has_else, pos);
    }

    func parse_switch_expr(mut self) ast.Expr {
        mut has_else := false;
        mut branches := @vec(ast.SwitchBranch);
        pos := self.prev_tok.pos;
        old_inside_switch_header := self.inside_switch_header;
        self.inside_switch_header = true;
        expr := if self.tok.kind == .Lbrace {
            ast.Expr.BoolLiteral(true, pos)
        } else {
            if self.decl_operator_is_used() {
                self.open_scope();
                self.parse_guard_expr()
            } else {
                self.parse_expr()
            }
        };
        is_typeswitch := expr !is .Guard and self.prev_tok.kind != .KwSwitch
            and self.accept(.KwIs);
        self.expect(.Lbrace);
        self.inside_switch_header = old_inside_switch_header;
        while {
            branch_pos := self.tok.pos;
            mut cases := @vec(mut ast.Expr);
            mut has_var := false;
            mut var_is_mut := false;
            mut var_name := "";
            mut var_pos := token.noPos;
            mut has_cond := false;
            mut cond := ast.Expr.Empty(self.tok.pos);
            is_else := self.accept(.KwElse);
            if is_else {
                has_else = true;
            } else {
                while {
                    if is_typeswitch {
                        name_pos := self.tok.pos;
                        cases.push(if self.accept(.Dot) {
                            .EnumLiteral(
                                self.parse_name(), pos: name_pos + self.prev_tok.pos,
                                from_is_cmp: true
                            )
                        } else {
                            .Type(self.parse_type())
                        });
                    } else {
                        mut case_expr := self.parse_expr();
                        if self.accept(.DotDot) or self.accept(.Ellipsis) {
                            if self.prev_tok.kind == .DotDot {
                                mut err := report.error_builder(
                                    "`switch` only supports inclusive ranges, not exclusive",
                                    self.prev_tok.pos
                                );
                                err.add_help("use `...` instead of `..`");
                                err.emit();
                            }
                            case_expr = .Range(
                                case_expr, self.parse_expr(), true, true, true,
                                case_expr.position() + self.prev_tok.pos
                            );
                        }
                        cases.push(case_expr);
                    }
                    if !self.accept(.Comma) {
                        break;
                    }
                }
                if self.accept(.KwAs) {
                    has_var = true;
                    var_pos = self.tok.pos;
                    var_is_mut = self.accept(.KwMut);
                    if var_is_mut {
                        var_pos += self.tok.pos;
                    }
                    var_name = self.parse_name();
                }
                if self.accept(.KwIf) {
                    has_cond = true;
                    cond = self.parse_expr();
                }
            }
            self.expect(.Arrow);
            branches.push(ast.SwitchBranch(
                cases: cases,
                has_var: has_var,
                var_is_mut: var_is_mut,
                var_name: var_name,
                var_pos: var_pos,
                has_cond: has_cond,
                cond: cond,
                expr: self.parse_expr(),
                is_else: is_else,
                pos: branch_pos + self.prev_tok.pos
            ));
            if !self.accept(.Comma) {
                break;
            }
        }
        if expr is .Guard {
            self.close_scope();
        }
        self.expect(.Rbrace);
        return .Switch(
            expr, branches, is_typeswitch, has_else, self.scope, pos + self.prev_tok.pos
        );
    }

    func parse_block_expr(mut self) ast.Expr {
        // block expression
        self.open_scope();
        sc := self.scope;
        old_inside_block := self.inside_block;
        self.inside_block = true;
        pos := self.tok.pos;
        is_unsafe := self.accept(.KwUnsafe);
        self.expect(.Lbrace);
        mut stmts := @vec(mut ast.Stmt);
        mut has_expr := false;
        mut expr := ast.Expr.Empty(self.tok.pos);
        while !self.accept(.Rbrace) {
            stmt := self.parse_stmt();
            if stmt is .Expr as stmt_expr and self.prev_tok.kind != .Semicolon
                and self.tok.kind == .Rbrace {
                expr = stmt_expr;
                has_expr = true;
            } else {
                stmts.push(stmt);
            }
        }
        self.close_scope();
        self.inside_block = old_inside_block;
        return .Block(is_unsafe, stmts, expr, has_expr, sc, pos + self.prev_tok.pos);
    }

    func parse_guard_expr(mut self) ast.Expr {
        pos := self.tok.pos;
        mut vars := @vec(ast.ObjectData);
        if self.accept(.Lparen) {
            while {
                vars.push(self.parse_var_decl(support_type: false));
                if !self.accept(.Comma) {
                    break;
                }
            }
            self.expect(.Rparen);
        } else {
            vars.push(self.parse_var_decl(support_type: false));
        }
        self.expect(.DeclAssign);
        expr := self.parse_expr();
        mut has_cond := false;
        cond := if self.accept(.Semicolon) {
            has_cond = true;
            self.parse_expr()
        } else {
            .Empty(self.tok.pos)
        };
        return .Guard(vars, expr, has_cond, cond, self.scope, pos + self.prev_tok.pos);
    }

    [inline]
    func parse_literal(mut self) ast.Expr {
        return switch {
            self.tok.kind in [.KwTrue, .KwFalse] => {
                value := self.tok.kind == .KwTrue;
                pos := self.tok.pos;
                self.next();
                .BoolLiteral(value, pos)
            },
            self.tok.kind == .Char => self.parse_character_literal(),
            self.tok.kind == .Number => self.parse_integer_literal(),
            self.tok.kind == .String => self.parse_string_literal(),
            self.accept(.KwNone) => .NoneLiteral(self.prev_tok.pos),
            self.accept(.KwSelf) => .SelfLiteral(self.scope, self.prev_tok.pos),
            self.accept(.KwSelfTy) => .SelfTy(self.scope, self.prev_tok.pos),
            else => {
                report.error("expected literal, found {}".fmt(self.tok), self.tok.pos);
                .Empty(self.tok.pos)
            }
        };
    }

    func parse_integer_literal(mut self) ast.Expr {
        value := self.tok.lit;
        pos := self.tok.pos;
        self.next();
        return if value.len > 2 and value[..2] !in ["0x", "0o", "0b"] and value.index_of_any(
            ".eE"
        ) !is none {
            .FloatLiteral(value, pos)
        } else {
            .IntegerLiteral(value, pos)
        };
    }

    func parse_character_literal(mut self) ast.Expr {
        is_byte := if self.accept(.Name) {
            self.prev_tok.lit == "b"
        } else {
            false
        };
        value := self.tok.lit;
        pos := self.tok.pos;
        self.expect(.Char);
        return .CharLiteral(value, is_byte, pos);
    }

    func parse_string_literal(mut self) ast.Expr {
        mut is_raw := false;
        mut is_bytestr := false;
        mut is_cstr := false;
        mut has_prefix := false;
        mut pos := if self.accept(.Name) {
            has_prefix = true;
            is_raw = self.prev_tok.lit == "r";
            is_bytestr = self.prev_tok.lit == "b";
            is_cstr = self.prev_tok.lit == "c";
            self.prev_tok.pos
        } else {
            self.tok.pos
        };
        mut value := self.tok.lit;
        if has_prefix {
            pos += self.tok.pos;
        }
        self.expect(.String);
        while self.accept(.String) {
            value = value.concat(self.prev_tok.lit);
            pos += self.prev_tok.pos;
        }
        return .StringLiteral(
            value: value,
            is_raw: is_raw,
            is_bytestr: is_bytestr,
            is_cstr: is_cstr,
            pos: pos
        );
    }

    func parse_ident(mut self, is_comptime: bool = false) ast.Expr {
        pos := if is_comptime {
            self.prev_tok.pos + self.tok.pos
        } else {
            self.tok.pos
        };
        return .Ident(
            name: self.parse_name(),
            scope: self.scope,
            pos: pos,
            is_comptime: is_comptime
        );
    }

    [inline]
    func parse_selector_expr(mut self, left: ast.Expr) ast.Expr {
        field_pos := self.tok.pos;
        return .Selector(
            left: left,
            field_name: if self.tok.kind == .Number {
                self.next();
                self.prev_tok.lit
            } else {
                self.parse_name()
            },
            field_pos: field_pos,
            pos: left.position() + self.prev_tok.pos
        );
    }
}
