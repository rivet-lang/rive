// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ../{ ast, report };

extend Parser {
    func parse_stmt(^mut self) -> ^mut ast.Stmt {
        return match {
            self.tok.kind == .Comment -> .Comment(self.parse_comment()),
            self.accept(.Dollar) -> match self.tok.kind {
                .KwIf -> ^mut .ComptimeIf(self.parse_comptime_if(.Stmt)),
                else -> {
                    report.error("invalid comptime construction", self.tok.pos);
                    ^mut .Empty(self.tok.pos)
                }
            },
            self.accept(.KwWhile) -> {
                pos := self.prev_tok.pos;
                mut is_inf := false;
                mut cond := ^mut ast.Expr.Empty(self.tok.pos);
                mut has_continue_expr := false;
                mut continue_expr := ^mut ast.Expr.Empty(self.tok.pos);
                if self.tok.kind == .Lbrace {
                    is_inf = true;
                    cond = ^mut .BoolLiteral(true, self.prev_tok.pos);
                } else {
                    cond = if self.decl_operator_is_used() {
                        self.open_scope();
                        self.parse_guard_expr()
                    } else {
                        self.parse_expr()
                    };
                    is_inf = cond.clean_paren() is .BoolLiteral;
                    if self.accept(.Colon) {
                        has_continue_expr = true;
                        continue_expr = self.parse_expr();
                    }
                }
                stmt := self.parse_stmt();
                if cond is .Guard {
                    self.close_scope();
                }
                mut has_else_stmt := false;
                else_stmt := if self.accept(.KwElse) {
                    has_else_stmt = true;
                    self.parse_stmt()
                } else {
                    ^mut .Empty(self.tok.pos)
                };
                ^mut .While(
                    cond, continue_expr, has_continue_expr, stmt, else_stmt,
                    has_else_stmt, is_inf, pos + self.prev_tok.pos
                )
            },
            self.accept(.KwFor) -> {
                pos := self.prev_tok.pos;
                self.open_scope();
                sc := self.scope;
                // single or 2 variables
                mut has_index := false;
                mut index := ^mut ast.ObjectData();
                if self.peek_token(1).kind == .Comma {
                    has_index = true;
                    index = self.parse_var_decl(support_mut: false, support_type: false);
                    self.expect(.Comma);
                }
                mut values := []^mut ast.ObjectData();
                if self.accept(.Lparen) {
                    while {
                        values.push(
                            self.parse_var_decl(support_ref: true, support_type: false)
                        );
                        if !self.accept(.Comma) {
                            break;
                        }
                    }
                    self.expect(.Rparen);
                } else {
                    values.push(self.parse_var_decl(support_ref: true, support_type: false));
                }
                self.expect(.KwIn);
                iterable := self.parse_expr();
                stmt := self.parse_stmt();
                self.close_scope();
                ^mut .For(
                    index: index, has_index: has_index, values: values,
                    iterable: iterable, stmt: stmt, pos: pos + self.prev_tok.pos,
                    scope: sc
                )
            },
            self.accept(.KwDefer) -> {
                mut defer_mode := ast.Stmt.Defer.Mode.Normal;
                if self.accept(.Lparen) {
                    defer_mode_name_pos := self.tok.pos;
                    defer_mode_name := self.parse_name();
                    defer_mode = match defer_mode_name {
                        "error" -> .Error,
                        "success" -> .Success,
                        else -> {
                            report.error(
                                "invalid `defer` mode: `{defer_mode_name}`".fmt(defer_mode_name),
                                defer_mode_name_pos
                            );
                            .Normal
                        }
                    };
                    self.expect(.Rparen);
                }
                pos := self.prev_tok.pos;
                expr := self.parse_expr();
                if !(expr is .If || expr is .Match || expr is .Block) {
                    self.expect(.Semicolon);
                }
                ^mut .Defer(expr, defer_mode, pos)
            },
            self.tok.kind in [.Lparen, .KwMut, .Name]
                && self.peek_tok.kind !in [.Dot, .Lbracket, .Lparen]
                && self.decl_operator_is_used() -> {
                // variable declarations
                mut pos := self.tok.pos;
                mut lefts := []^mut ast.ObjectData();
                if self.accept(.Lparen) {
                    // multiple variables
                    while {
                        lefts.push(self.parse_var_decl(false));
                        if !self.accept(.Comma) {
                            break;
                        }
                    }
                    self.expect(.Rparen);
                } else {
                    lefts.push(self.parse_var_decl(false));
                }
                self.expect(.DeclAssign);
                right := self.parse_expr();
                pos += self.prev_tok.pos;
                self.expect(.Semicolon);
                ^mut .VarDecl(lefts, right, self.scope, pos)
            },
            else -> {
                expr := self.parse_expr();
                if !((self.inside_block && self.tok.kind == .Rbrace)
                    || (expr is .If || expr is .Match || expr is .Block)) {
                    self.expect(.Semicolon);
                }
                ^mut .Expr(expr)
            }
        };
    }
}
