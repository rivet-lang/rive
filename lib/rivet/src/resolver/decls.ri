// Copyright (C) 2023-2024 Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ../{ ast, report };

extend Resolver {
    func resolve_decls(^mut self, decls: []^mut ast.Decl) {
        for decl in decls {
            old_sym := self.sym;
            old_self_sym := self.self_sym;
            old_self_sym_is_set := self.self_sym_is_set;
            match decl.^ {
                // .Empty(empty_pos) -> report.error("BUG: empty declaration found", empty_pos),
                .ComptimeIf(&mut comptime_if) -> {
                    ct_decls := ast.nodes_to_decls(
                        self.env.evalue_comptime_if(comptime_if)
                    );
                    self.resolve_decls(ct_decls);
                },
                .Extern(extern_decl) -> self.resolve_decls(extern_decl.decls),
                .Alias(alias_decl) -> if alias_decl.is_typealias {
                    _ = self.resolve_type(alias_decl.parent_type);
                } else {
                    sym_ref := @as(^mut ast.SymRef, alias_decl.sym);
                    self.resolve_expr(sym_ref.ref_expr);
                    if sym_ref.ref_expr is .Ident(ident) && ident.found && ident.is_sym {
                        sym_ref.ref = ident.sym;
                        sym_ref.ref_resolved = true;
                    } else if sym_ref.ref_expr is .Selector(selector) && selector.found {
                        sym_ref.ref = selector.sym;
                        sym_ref.ref_resolved = true;
                    }
                },
                .Const(const_decl) -> if self.resolve_type(const_decl.type) {
                    self.resolve_expr(const_decl.expr);
                },
                .Var(var_decl) -> {
                    for left in var_decl.lefts {
                        self.check_variable_shadowing(left.name, left.pos);
                        _ = self.resolve_type(left.type);
                    }
                    if !var_decl.is_extern {
                        self.resolve_expr(var_decl.right);
                    }
                },
                .Trait(trait_decl) -> {
                    self.self_sym = @as(^mut ast.TypeSym, trait_decl.sym);
                    self.self_sym_is_set = true;
                    mut trait_info := @as(ast.TypeInfo.Trait, self.self_sym.info);
                    for base in trait_decl.bases {
                        if self.resolve_type(base) {
                            base_sym := base.symbol()?;
                            if base_sym.info is .Trait(&mut base_trait_info) {
                                trait_info.bases.push(base_sym);
                                for impl in base_trait_info.implements {
                                    if impl.info is .Struct(&mut struct_info) {
                                        if !struct_info.contains_trait(base_sym) {
                                            base_trait_info.implement(impl);
                                            struct_info.traits.push(base_sym);
                                        }
                                    }
                                }
                            } else {
                                report.error(
                                    "type `{}` is not a trait".fmt(base_sym.name),
                                    base.position()
                                );
                            }
                        }
                    }
                    self.resolve_decls(trait_decl.decls);
                },
                .Enum(enum_decl) -> {
                    if enum_decl.is_tagged || self.resolve_type(enum_decl.underlying_type) {
                        self.self_sym = @as(^mut ast.TypeSym, enum_decl.sym);
                        self.self_sym_is_set = true;
                        mut enum_info := @as(ast.TypeInfo.Enum, self.self_sym.info);
                        for variant in enum_info.variants {
                            if !self.resolve_type(variant.type) {
                                continue;
                            }
                        }
                        for base in enum_decl.bases {
                            if self.resolve_type(base) {
                                base_sym := base.symbol()?;
                                if base_sym.info is .Trait(&mut trait_info) {
                                    enum_info.traits.push(base_sym);
                                    trait_info.implement(self.self_sym);
                                } else {
                                    report.error(
                                        "type `{}` is not a trait".fmt(
                                            base_sym.name
                                        ), base.position()
                                    );
                                }
                            }
                        }
                    }
                    for variant in enum_decl.variants {
                        if !variant.decls.is_empty() {
                            old_self_sym2 := self.self_sym;
                            self.self_sym = variant.type.symbol() ?? self.self_sym;
                            self.resolve_decls(variant.decls);
                            self.self_sym = old_self_sym2;
                        }
                    }
                    if default_value := enum_decl.attributes.find("default_value") {
                        self.self_sym.default_value = default_value.args[0].expr;
                        self.resolve_expr(self.self_sym.default_value?);
                    }
                    self.resolve_decls(enum_decl.decls);
                },
                .Struct(struct_decl) -> {
                    self.self_sym = @as(^mut ast.TypeSym, struct_decl.sym);
                    self.self_sym_is_set = true;
                    for base in struct_decl.bases {
                        if self.resolve_type(base) {
                            mut struct_info := @as(ast.TypeInfo.Struct, self.self_sym.info);
                            base_sym := base.symbol()?;
                            if base_sym.info is .Trait(&mut trait_info) {
                                struct_info.traits.push(base_sym);
                                trait_info.implement(self.self_sym);
                            } else if base_sym.info is .Struct(&mut struct_info2) {
                                struct_info.bases.push(base_sym);
                                for b_trait in struct_info2.traits {
                                    @as(ast.TypeInfo.Trait, b_trait.info).implement(
                                        self.self_sym
                                    );
                                }
                            } else {
                                report.error(
                                    "type `{}` is not a trait or struct".fmt(
                                        base_sym.name
                                    ), base.position()
                                );
                            }
                        }
                    }
                    self.resolve_decls(struct_decl.decls);
                },
                .Extend(extend_decl) -> if self.resolve_type(extend_decl.type) {
                    self.self_sym = extend_decl.type.symbol()?;
                    self.self_sym_is_set = true;
                    for base in extend_decl.bases {
                        if self.resolve_type(base) {
                            base_sym := base.symbol()?;
                            if base_sym.info is .Trait(&mut trait_info) {
                                trait_info.implement(self.self_sym);
                            } else if self.self_sym.info is .Struct(&mut struct_info)
                                && base_sym.info is .Struct {
                                struct_info.bases.push(base_sym);
                            } else {
                                report.error(
                                    "type `{}` is not a trait or a struct".fmt(
                                        base_sym.name
                                    ), base.position()
                                );
                            }
                        }
                    }
                    self.resolve_decls(extend_decl.decls);
                },
                .Field(field_decl) -> if self.resolve_type(field_decl.type) {
                    if field_decl.has_def_expr {
                        self.resolve_expr(field_decl.def_expr);
                    }
                    if sym := self.self_sym.scope.find(field_decl.name) {
                        report.error(
                            "{} `{}` has field and {} named `{}`".fmt(
                                self.self_sym.type_of(), self.self_sym.name, sym.type_of(), field_decl.name
                            ),
                            field_decl.pos
                        );
                    }
                },
                .Func(&mut func_decl) -> {
                    if func_decl.is_method {
                        mut self_type := ^mut ast.Type.Basic(self.self_sym);
                        if func_decl.self_is_ptr {
                            self_type = ^mut .Pointer(self_type, func_decl.self_is_mut);
                        } else if func_decl.self_is_boxed {
                            self_type = ^mut .Boxed(self_type, func_decl.self_is_mut);
                        }
                        is_used := self.self_sym.info is .Trait && !func_decl.has_body;
                        func_decl.scope.add_local(^mut ast.Var(
                            name: "self",
                            is_mut: func_decl.self_is_mut && !func_decl.self_is_ptr && !func_decl.self_is_boxed,
                            is_used: is_used,
                            is_changed: is_used,
                            level: .Receiver,
                            type: self_type,
                            pos: func_decl.self_pos
                        )) catch |err| report.error(
                            err.to_string(), func_decl.pos
                        );
                        func_decl.self_type = self_type;
                        @as(^mut ast.Func, func_decl.sym).self_type = self_type;
                    }
                    for arg in func_decl.args {
                        if self.resolve_type(arg.type) {
                            self.check_variable_shadowing(arg.name, arg.pos);
                            is_used := self.self_sym_is_set && self.self_sym.info is .Trait
                                && !func_decl.has_body;
                            func_decl.scope.add_local(^mut ast.Var(
                                is_mut: arg.is_mut,
                                is_used: is_used,
                                is_changed: is_used,
                                name: arg.name,
                                level: .Argument,
                                pos: arg.pos,
                                type: arg.type
                            )) catch |err| report.error(
                                err.to_string(), arg.pos
                            );
                        }
                        if arg.has_def_expr {
                            self.resolve_expr(arg.def_expr);
                        }
                    }
                    _ = self.resolve_type(func_decl.ret_type);
                    for stmt in func_decl.stmts {
                        self.resolve_stmt(stmt);
                    }
                },
                .Test(test_decl) -> {
                    for stmt in test_decl.stmts {
                        self.resolve_stmt(stmt);
                    }
                },
                else -> {}
            }
            self.sym = old_sym;
            self.self_sym = old_self_sym;
            self.self_sym_is_set = old_self_sym_is_set;
        }
    }
}
