// Copyright (C) 2023 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import ../ast;
import ../prefs;
import ../token;
import ../report;

import { Path } from std/fs;

struct Prelude {
    name: string;
    sym: ast.Sym;
}

#[boxed]
pub struct Resolver {
    mut table: ast.Table;
    mut prefs: prefs.Prefs;

    mut sym: ast.Sym := ast.InvalidSym();
    mut self_sym: ast.TypeSym;
    mut preludes: []Prelude;

    mut self_sym_is_set: bool;

    mut source_file: ast.SourceFile;
    mut source_file_abi: ast.ABI;
    mut different_abi_usage_count: uint;

    pub func resolve_files(mut self, source_files: []ast.SourceFile) {
        Register(self.table, self.prefs).walk_files(source_files);
        if report.total_errors() > 0 {
            return;
        }
        self.preludes = [
            Prelude("Throwable", self.table.throwable_sym)
        ];
        for sf in source_files {
            self.sym = sf.mod;
            self.source_file = sf;
            self.detect_source_file_abi();
            self.different_abi_usage_count = 0;
            self.resolve_decls(self.source_file.decls);
            if self.source_file_abi == .Rivet and self.different_abi_usage_count > 0 {
                report.warn("filename requires ABI specification", sf.pos);
            }
        }
    }

    func detect_source_file_abi(mut self) {
        filename := Path.file_name(self.source_file.path);
        if filename.ends_with(".c.ri") {
            self.source_file_abi = .C;
        } else {
            self.source_file_abi = .Rivet;
        }
    }

    func check_vis(self, sym: ast.Sym, pos: token.Pos) {
        if !sym.is_public and !self.source_file.mod.has_access_to(sym) {
            report.error("{} `{}` is private".fmt(sym.type_of(), sym.name), pos);
        }
    }

    func eval_size(mut self, mut expr: ast.Expr) -> ?int {
        return match expr is {
            .Paren(paren) -> self.eval_size(paren.expr),
            .IntegerLiteral(int_lit) -> int_lit.value.to_int() catch return none,
            .Ident(mut ident) -> {
                self.resolve_ident(ident);
                if ident.found {
                    self.eval_sym(ident.sym, ident.pos)
                } else {
                    none
                }
            },
            .Selector(mut selector) -> {
                self.resolve_selector(selector);
                if selector.found {
                    self.eval_sym(selector.sym, selector.pos)
                } else {
                    none
                }
            },
            .Binary(binary) -> if left := self.eval_size(binary.left) {
                if right := self.eval_size(binary.right) {
                    match binary.op {
                        .Plus -> left + right,
                        .Minus -> left - right,
                        .Mul -> left * right,
                        .Div -> left / right,
                        .Mod -> left % right,
                        .Amp -> left & right,
                        .Pipe -> left | right,
                        .Xor -> left ^ right,
                        .Lshift -> @as(int, @as(uint, left) << right),
                        .Rshift -> left >> right,
                        else -> none
                    }
                } else {
                    none
                }
            } else {
                none
            },
            .BuiltinCall(builtin_call) if builtin_call.name in ["size_of", "align_of"] -> {
                if builtin_call.args[0].expr is .Type(mut type) and self.resolve_type(type) {
                    (size, align) := self.table.type_size(type);
                    if builtin_call.name == "size_of" {
                        @as(int, size)
                    } else {
                        @as(int, align)
                    }
                } else {
                    0
                }
            },
            else -> none
        };
    }

    func eval_sym(mut self, sym: ast.Sym, pos: token.Pos) -> ?int {
        if sym is ast.Const(const_) {
            if !const_.has_evaled_size {
                const_.evaled_size = self.eval_size(const_.expr)?;
            }
            return const_.evaled_size;
        }
        report.error("`{}` is not a constant value".fmt(sym.name), pos);
        return none;
    }

    func check_variable_shadowing(self, name: string, pos: token.Pos) {
        if self.source_file.imported_symbols.exists(name) {
            report.error(
                "name `{}` hide an imported symbol".fmt(name), pos
            );
        }
    }
}
