// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import ../{ ast, report };

extend Resolver {
    func resolve_type(mut self, mut type: ast.Type) -> bool {
        return match type {
            .Void, .Never, .Rawptr, .Boxedptr -> true,
            .Option(mut opt) -> self.resolve_type(opt.inner),
            .Result(mut res) -> self.resolve_type(res.inner),
            .Variadic(mut variadic) -> if self.resolve_type(variadic.inner) {
                mut elem_sym := variadic.inner.symbol()?;
                if elem_sym.info is .Trait(mut trait_info) {
                    trait_info.has_objects = true;
                }
                variadic.sym = self.env.universe.add_or_get_slice(variadic.inner, false);
                true
            } else {
                false
            },
            .Slice(mut slice) -> if self.resolve_type(slice.inner) {
                slice.sym = self.env.universe.add_or_get_slice(slice.inner, slice.is_mut);
                true
            } else {
                false
            },
            .DynArray(mut dyn_arr) -> if self.resolve_type(dyn_arr.inner) {
                dyn_arr.sym = self.env.universe.add_or_get_dyn_array(dyn_arr.inner, dyn_arr.is_mut);
                true
            } else {
                false
            },
            .Array(mut arr) -> if self.resolve_type(arr.inner) {
                if arr_size := self.eval_size(arr.size) {
                    if arr_size <= 0 {
                        mut err := report.error_builder(
                            "array size cannot be zero or negative", arr.size.position()
                        );
                        err.add_note("array size {}", arr_size);
                        err.emit();
                        false
                    } else {
                    	arr.size_value = @as(uint, arr_size);
                        arr.sym = self.env.universe.add_or_get_array(
                            arr.inner, arr.size_value, arr.is_mut
                        );
                        true
                    }
                } else {
                    report.error(
                        "array size cannot use non-constant value", arr.size.position()
                    );
                    false
                }
            } else {
                false
            },
            .Pointer(mut ptr) -> self.resolve_type(ptr.inner),
            .Tuple(mut tuple) -> {
                mut resolved := false;
                for mut inner in tuple.inners {
                    resolved = self.resolve_type(inner);
                }
                if resolved {
                    tuple.sym = self.env.universe.add_or_get_tuple(tuple.inners);
                }
                resolved
            },
            .Func(mut func_t) -> {
                mut res1 := false;
                for mut arg in func_t.args {
                    res1 = self.resolve_type(arg.type);
                }
                res1 = self.resolve_type(func_t.ret_type);
                func_t.sym = self.env.universe.add_or_get_func(func_t.symbol());
                func_t.has_sym = true;
                res1
            },
            .Basic(mut basic) -> if basic.is_unresolved {
                match basic.expr {
                    .SelfTy -> if self.self_sym_is_set {
                        basic.is_unresolved = false;
                        basic.sym = self.self_sym;
                        true
                    } else {
                        false
                    },
                    .Ident(mut ident) -> {
                        self.resolve_ident(ident);
                        if ident.found {
                            if ident.sym is ast.TypeSym(mut type_sym) {
                                basic.is_unresolved = false;
                                basic.sym = type_sym;
                                if type_sym.info is .Alias(mut alias_info) {
                                    if self.resolve_type(alias_info.parent) {
                                        type.unalias_in_place();
                                    }
                                }
                            } else {
                                report.error(
                                    "expected type, found {}".fmt(ident.sym.type_of()),
                                    ident.pos
                                );
                            }
                        }
                        ident.found
                    },
                    .Selector(mut selector) -> {
                        self.resolve_selector(selector);
                        if selector.found {
                            if selector.sym is ast.TypeSym(mut type_sym) {
                                if type_sym.info is .Placeholder {
                                    report.error(
                                        "cannot find type `{}` in {} `{}`".fmt(
                                            type_sym.name, selector.left_sym.type_of(),
                                            selector.left_sym.name
                                        ),
                                        selector.field_pos
                                    );
                                    return false;
                                } else {
                                    basic.is_unresolved = false;
                                    basic.sym = type_sym;
                                    if type_sym.info is .Alias(mut alias_info) {
                                        if self.resolve_type(alias_info.parent) {
                                            type.unalias_in_place();
                                        }
                                    }
                                    return true;
                                }
                            } else {
                                report.error(
                                    "expected type, found {}".fmt(selector.sym.type_of()),
                                    selector.pos
                                );
                                return false;
                            }
                        }
                        selector.found
                    },
                    else -> {
                        report.error("expected type, found expression", basic.expr.position());
                        false
                    }
                }
            } else {
                true
            },
            else -> false
        };
    }
}
