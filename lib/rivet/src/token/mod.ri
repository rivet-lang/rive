// Copyright (C) 2022 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import "std/traits";

public var NO_POS: Pos = Pos();

public struct Pos : traits.Stringable {
    public file: string;
    public line: usize;
    public col: usize;
    public mut len: usize;
    public pos: usize;

    public func +(&self, other: &Pos) Pos {
		return Pos(len: other.pos - self.pos + other.len, ...self.*);
    }

    public func to_string(&self) string {
        return "{}:{}:{}".fmt(self.file, self.line + 1, self.col);
    }
}

public struct Token : traits.Stringable {
    public lit: string;
    public kind: Kind;
    public len: usize;
    public pos: Pos;

    public func to_debug_string(&self) string {
        return "rivet.token.Token(kind: '{}', lit: '{}', pos: {})".fmt(
            self.kind, self.lit, self.pos
        );
    }

    public func to_string(&self) string {
        mut str := self.kind.to_string();
        if !str[0].is_alnum() {
            return "token `{}`".fmt(str);
        }
        if is_keyword(self.lit) {
            str = "keyword";
        }
        if self.kind != .DocComment and self.lit != "" {
            str = str.concat(" `{}`".fmt(self.lit));
        }
        return str;
    }
}
