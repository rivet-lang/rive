// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

class Vec {
    mut ptr: *mut void;
    elem_size: usize;
    mut len: usize;
    mut cap: usize;
    is_ref: bool;

    #[unsafe]
    fn new(elem_size: usize, cap: usize) Self {
        return Self(
            unsafe { mem_zeroed(cap * elem_size) catch unreachable!() },
            elem_size, 0, cap
        );
    }

    #[unsafe]
    fn from_array(arr: *mut void, elem_size: usize, len: usize) Self {
        let mut vec = Self(
            unsafe { mem_zeroed(len * elem_size) catch unreachable!() },
            elem_size, len, len
        );
        unsafe {
            mem_copy(vec.ptr, arr, elem_size * len);
        }
        return vec;
    }

    #[unsafe]
    fn from_array_no_alloc(arr: *mut void, elem_size: usize, len: usize) Self {
        return Self(unsafe { arr }, elem_size, len, len, is_ref: true);
    }

    fn get(self, idx: usize) *mut void {
        if idx >= self.len {
            process_panic("vector index out of range (index: {}, len: {})", idx, self.len);
        }
        return unsafe { ptr_add!(as(*mut u8, self.ptr), idx * self.elem_size) };
    }

    #[unsafe]
    fn raw_get(self, idx: usize) *mut void {
        return unsafe { ptr_add!(as(*mut u8, self.ptr), idx * self.elem_size) };
    }

    fn set(self, idx: usize, val: *void) {
        if idx >= self.len {
            process_panic("vector index out of range (index: {}, len: {})", idx, self.len);
        }
        unsafe {
            mem_copy(
                ptr_add!(as(*mut u8, self.ptr), self.elem_size * idx), val,
                self.elem_size
            );
        }
    }

    fn push(mut self, val: *void) {
        if self.len >= self.cap {
            self.reserve(self.len + 1);
        }
        unsafe {
            mem_copy(
                ptr_add!(self.ptr, self.elem_size * self.len), val,
                self.elem_size
            );
        }
        self.len += 1;
    }

    /// Returns the last element of the vector, and removes it. If the
    /// `vector` is empty, this will process_panic.
    fn pop(mut self) *void {
        if self.len == 0 {
            process_panic("Vec.pop: vector is empty");
        }
        let new_len = self.len - 1;
        unsafe {
            let last_elem = ptr_add!(self.ptr, new_len * self.elem_size);
            self.len = new_len;
            return last_elem;
        }
    }

    fn is_empty(self) bool {
        return self.len == 0;
    }

    fn ==(self, rhs: Self) bool {
        if self.len != rhs.len {
            return false;
        }
        return unsafe { mem_cmp(self.ptr, rhs.ptr, self.len) == 0 };
    }

    fn !=(self, rhs: Self) bool {
        return !(self == rhs);
    }

    fn reserve(mut self, required: usize) {
        if required <= self.cap {
            return;
        }
        let mut cap = if self.cap > 0 { self.cap } else { 2 };
        while required > cap {
            cap *= 2;
        }
        unsafe {
            let new_size = cap * self.elem_size;
            let new_ptr = mem_alloc(new_size) catch unreachable!();
            mem_copy(new_ptr, self.ptr, self.len * self.elem_size);
            self.ptr = new_ptr;
        }
        self.cap = cap;
    }

    fn slice(self, start: usize, end: usize) Self {
        if start > end or end > self.len {
            process_panic(
                "slice index out of range (range: {}..{}, len: {})", start, end,
                self.len
            );
        }
        let len = end - start;
        if len == self.len {
            return self;
        }
        return Self(
            unsafe { ptr_add!(as(*mut u8, self.ptr), start * self.elem_size) },
            self.elem_size, len, len, true
        );
    }

    fn slice_from(self, start: usize) Self {
        return self.slice(start, self.len);
    }

    /// Returns an independent copy of `self`.
    pub fn clone(self) Self {
        let mut size = self.cap * self.elem_size;
        if size == 0 {
            size += 1;
        }
        let mut vec = Self(
            mem_zeroed(size) catch unreachable!(),
            self.elem_size, self.len, self.cap
        );
        unsafe {
            mem_copy(vec.ptr, self.ptr, size);
        }
        return vec;
    }

    ~Self(mut self) {
        unsafe {
            if !self.is_ref {
                mem_dealloc(self.ptr);
            }
        }
    }
}
