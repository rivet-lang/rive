// Copyright (C) 2022 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import "c/libc";

extern (Rivet) {
    public let mut ARGS: []string;

    func init_string_lits();
    func init_globals();
    func drop_globals();
}

extern (C) func init_args(_argc: usize, _argv: [*]*uint8) {
    unsafe {
        ARGS = @vec(string, _argc);
        let mut i: usize = 0;
        while i < _argc : i += 1 {
            ARGS.push(string.from_raw(_argv[i]));
        }
    }
}

func main(
    _argc: int32, _argv: [*]*uint8,
#if _TESTS_
    mut tr: TestRunner
#else
    pkg_main: func()
#endif
) {
    unsafe {
        _ = libc.signal(11, segfault_handler);
        init_args(@as(usize, _argc), _argv);
#if !_TESTS_ // `init_string_lits` is called after
        init_string_lits();
#endif
        init_globals();
        runtime_started = true;
#if _TESTS_
        if tr.tests.len > 0 {
            tr.run();
            tr.print_summary_tests();
        }
#else
        pkg_main();
#endif
        drop_globals();
    }
}
