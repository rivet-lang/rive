// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

import "libc";

fn segfault_handler(_: i32) {
    Console::eprintln("panic: segmentation fault detected");
    bt_print(3);
    process_exit(1);
}

fn panic_if(cond: bool, msg: string) {
    if cond {
        Console::eprintln("panic: {}", msg);
        bt_print(2);
        process_exit(1);
    }
}

fn assert(cond: bool, msg: string) {
    if !cond {
        Console::eprintln("panic: assertion failed: {}", msg);
        bt_print(2);
        process_exit(1);
    }
}

pub fn errno() i32 {
    return unsafe {
#if _LINUX_
        libc::__errno_location().*
#else
        libc::_errno().*
#endif
    };
}

/// Returns error code representation in string.
pub fn strerr(code: i32 = errno()) string {
    unsafe {
        let s = libc::strerror(code);
        return if s == nil { "" } else { string::from_raw(s) };
    }
}

fn internal_dup(src: *void, sz: usize) *mut void {
    unsafe {
        return mem_dup(src, sz) catch {
            process_panic("internal error: cannot memdup memory");
        };
    }
}
