// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

const MAX_ONE_B: u32 = 0x80;
const MAX_TWO_B: u32 = 0x800;
const MAX_THREE_B: u32 = 0x10000;

fn cstr_runes_count(s: *u8, size: usize) usize {
	let (mut l: usize, mut i: usize) = (0, 0);
	while i < size {
		l += 1;
		i += unsafe { s[i] }.len_utf8();
	}
	return l;
}

extend rune : ToString {
    /// The highest valid codepoint a `rune` can have.
    pub const MAX: rune = 0x10FFFF;

    /// `U+FFFD REPLACEMENT CHARACTER` (ï¿½) is used in Unicode to represent a
    /// decoding error.
    pub const REPLACEMENT_CHARACTER: rune = 0xFFFD;

    pub fn from_u32(i: u32) !rune {
        if (i > as(u32, Self::MAX)) or (i >= 0xD800 and i <= 0xDFFF) {
            return RuneFromU32Error();
        }
        return as(rune, i);
    }

    /// Converts a digit in the given radix to a `rune`.
    ///
    /// A 'radix' here is sometimes also called a 'base'. A radix of two
    /// indicates a binary number, a radix of ten, decimal, and a radix of
    /// sixteen, hexadecimal, to give some common values. Arbitrary
    /// radices are supported.
    ///
    /// `rune_from_digit()` will return `nil` if the input is not a digit
    /// in the given radix.
    ///
    /// Panics if given a radix larger than 36.
    pub fn from_digit(num: u32, radix: u32) ?rune {
        if radix > 36 {
            process_panic("rune::from_digit: radix is too high (maximum 36)");
        }
        if num < radix {
            let num_ = as(u8, num);
            if num_ < 10 {
                return as(rune, b'0' + num_);
            } else {
                return as(rune, b'a' + num_ - 10);
            }
        }
        return nil;
    }

    fn len_utf8(self) usize {
        return len_utf8(as(u32, self));
    }

    fn to_string(&self) string {
        let len = self.len_utf8();
        let res = as(*mut u8, mem_alloc(len + 1) catch unreachable!());
        _ = unsafe { utf32_decode_to_buffer(as(u32, self.*), res) };
        return string(unsafe { res }, len);
    }
}

fn utf32_decode_to_buffer(code: u32, buffer: *mut u8) i32 {
    let icode = as(i32, code);
	unsafe {
		switch {
            (icode <= 127) => { // 0x7F
                buffer[0] = as(u8, icode);
                return 1;
            },
            (icode <= 2047) => { // 0x7FF
                buffer[0] = as(u8, 192) | as(u8, icode >> 6); // 0xC0 - 110xxxxx
                buffer[1] = as(u8, 128) | as(u8, icode & 63); // 0x80 - 0x3F - 10xxxxxx
                return 2;
            },
            (icode <= 65535) => { // 0xFFFF
                buffer[0] = as(u8, 224) | as(u8, icode >> 12); // 0xE0 - 1110xxxx
                buffer[1] = as(u8, 128) | (as(u8, icode >> 6) & 63); // 0x80 - 0x3F - 10xxxxxx
                buffer[2] = as(u8, 128) | as(u8, icode & 63); // 0x80 - 0x3F - 10xxxxxx
                return 3;
            },
            (icode <= 1114111) => { // 0x10FFFF
                buffer[0] = as(u8, 240) | as(u8, icode >> 18); // 0xF0 - 11110xxx
                buffer[1] = as(u8, 128) | (as(u8, icode >> 12) & 63); // 0x80 - 0x3F - 10xxxxxx
                buffer[2] = as(u8, 128) | (as(u8, icode >> 6) & 63); // 0x80 - 0x3F - 10xxxxxx
                buffer[3] = as(u8, 128) | as(u8, icode & 63); // 0x80 - 0x3F - 10xxxxxx
                return 4;
            },
            else => return 0
		}
	}
}

fn len_utf8(code: u32) usize {
    return if code < MAX_ONE_B {
        1
    } else if code < MAX_TWO_B {
        2
    } else if code < MAX_THREE_B {
        3
    } else {
        4
    };
}
