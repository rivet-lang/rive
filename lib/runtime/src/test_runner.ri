// Copyright (C) 2022 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

#if _TESTS_

let SUPPORT_COLOR: bool = console_is_atty(1);

let OK_STATUS: string = bold(green("-> OK"));
let FAILED_STATUS: string = bold(red("-> FAIL"));
let SKIP_STATUS: string = bold(yellow("-> SKIP"));

extern (Rivet) {
    let test_runner: TestRunner;
}

pub fn bold(msg: string) string {
    if !SUPPORT_COLOR {
        return msg;
    }
    return "\x1b[1m{}\x1b[22m".fmt(msg);
}

pub fn green(msg: string) string {
    if !SUPPORT_COLOR {
        return msg;
    }
    return "\x1b[32m{}\x1b[39m".fmt(msg);
}

pub fn red(msg: string) string {
    if !SUPPORT_COLOR {
        return msg;
    }
    return "\x1b[31m{}\x1b[39m".fmt(msg);
}

pub fn yellow(msg: string) string {
    if !SUPPORT_COLOR {
        return msg;
    }
    return "\x1b[33m{}\x1b[39m".fmt(msg);
}

fn assert_test(cond: bool, msg: string, pos: string, mut test_: Test) {
    if !cond {
        test_.result = .Failed;
        test_.err_msg = "assertion failed: {}".fmt(msg);
        test_.err_pos = pos;
    }
}

fn test_error_returned(err: Error, pos: string, mut test_: Test) {
    test_.result = .ErrorReturned;
    test_.err = err;
    test_.err_pos = pos;
}

enum TestResult as u8 {
    Ok,
    Skipped,
    Failed,
    ErrorReturned
}

struct Test {
    name: string;
    func : fn (*Test);
    mut result: TestResult;
    mut err_pos: string;
    mut err_msg: string;
    mut err: Error;
}

pub struct TestRunner {
    tests: [Test];
    mut ok_tests: u64;
    mut fail_tests: u64;
    mut skip_tests: u64;

    fn run(mut self) {
        for i, test_ in self.tests {
            console_eprint(" [{}/{}] {} ", i + 1, self.tests.len, test_.name);
            (test_.func)(unsafe { @addr_of(test_) });
            switch test_.result is {
                .Ok => {
                    self.print_ok();
                },
                .Skipped => {
                    self.print_skip();
                },
                .Failed => {
                    self.print_failed(test_.err_pos, test_.err_msg);
                },
                .ErrorReturned => {
                    self.print_error_returned(test_.err_pos, test_.err);
                }
            }
        }
        self.print_summary_tests();
        process_exit(as(i32, self.fail_tests != 0));
    }

    fn print_summary_tests(&self) {
        console_eprint(bold("Summary for all tests: "));
        if self.ok_tests > 0 {
            console_eprint("{}, ", bold(green("{} passed".fmt(self.ok_tests))));
        }
        if self.fail_tests > 0 {
            console_eprint("{}, ", bold(red("{} failed".fmt(self.fail_tests))));
        }
        if self.skip_tests > 0 {
            console_eprint("{}, ", bold(yellow("{} skipped".fmt(self.skip_tests))));
        }
        console_eprintln(bold("{} total.".fmt(self.tests.len)));
    }

    fn print_ok(mut self) {
        console_eprintln(OK_STATUS);
        self.ok_tests += 1;
    }

    fn print_skip(mut self) {
        console_eprintln(SKIP_STATUS);
        self.skip_tests += 1;
    }

    fn print_failed(mut self, pos: string, msg: string) {
        console_eprintln(FAILED_STATUS);
        console_eprintln("     at {}: {}", bold(pos), msg);
        self.fail_tests += 1;
    }

    fn print_error_returned(mut self, pos: string, err: Error) {
        console_eprintln(FAILED_STATUS);
        console_eprintln("     at {}: error returned: {}", bold(pos), err);
        self.fail_tests += 1;
    }
}
#endif
