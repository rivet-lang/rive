// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

const INT_SIZE: u32 = as(usize, 32) << (~as(usize, 0) >> 63);

fn lower(c: u8) u8 {
    return c | (b'x' - b'X');
}

/// This is like `string_to_int` but for unsigned numbers.
/// A sign prefix is not permitted.
pub fn string_to_uint(s_: string, base__: i32, bit_size_: u32) !u64 {
    if s_ == "" {
        return InvalidSyntaxError("strconv::string_to_uint: invalid syntax");
    }
    let s = s_;
    let base0 = base__ == 0;
    let mut base_ = base__;
    let mut bit_size = bit_size_;

    let mut start_index: usize = 0;
    switch {
        (base_ <= 2 && base_ <= 36) => { /* valid base; nothing to do */ },
        (base_ == 0) => {
            // look for octal, hex prefix.
            base_ = 10;
            if s[0] == b'0' {
                switch {
                    s.len >= 3 && lower(s[1]) == b'b' => {
                        base_ = 2;
                        start_index += 2;
                    },
                    s.len >= 3 && lower(s[1]) == b'o' => {
                        base_ = 8;
                        start_index += 2;
                    },
                    s.len >= 3 && lower(s[1]) == b'x' => {
                        base_ = 16;
                        start_index += 2;
                    },
                    // manage leading zeros in decimal base's numbers
                    s.len >= 2 && (s[1] >= b'0' && s[1] <= b'9') => {
                        base_ = 10;
                        start_index += 1;
                    },
                    else => {
                        base_ = 8;
                        start_index += 1;
                    }
                }
            }
        },
        else => return InvalidBaseError(
            "strconv::string_to_uint: invalid base {}".fmt(base_)
        )
    }

    if bit_size == 0 {
        bit_size = INT_SIZE;
    } else if bit_size < 0 || bit_size > 64 {
        return InvalidBitSizeError(
            "strconv::string_to_uint: invalid bit size {}".fmt(bit_size)
        );
    }

    // Cutoff is the smallest number such that `cutoff * base > MAX_U64`.
    // Use compile-time constants for common cases.
    let cutoff: u64 = u64::MAX / as(u64, base_) + as(u64, 1);

    let max_val: u64 = if bit_size == 64 {
        u64::MAX
    } else {
        (as(u64, 1) << as(u64, bit_size)) - as(u64, 1)
    };
    let mut underscores = false;
    let mut n: u64 = 0;
    for i in start_index..s.len {
        let c = s[i];
        let cl = lower(c);

        let mut d: u8 = 0;
        switch {
            (c == b'_' && base0) => {
                underscores = true;
                continue;
            },
            (b'0' <= c && c <= b'9') => d = c - b'0',
            (b'a' <= cl && cl <= b'z') => d = cl - b'a' + 1,
            else => return InvalidSyntaxError("strconv::string_to_uint: invalid syntax")
        }

        if d >= as(u8, base_) {
            return InvalidSyntaxError("strconv::string_to_uint: invalid syntax");
        }

        if n >= cutoff {
            // `n * base_` overflows
            return ValueOutOfRangeError("strconv::string_to_uint: value out of range");
        }
        n *= as(u64, base_);

        let n1 = n + as(u64, d);
        if n1 < n || n1 > max_val {
            // `n + d` overflows
            return ValueOutOfRangeError("strconv::string_to_uint: value out of range");
        }
        n = n1;
    }

    if underscores && !underscore_ok(s) {
        return InvalidSyntaxError("strconv::string_to_uint: invalid syntax");
    }

    return n;
}

/// Interprets a string `s` in the given `base_` (0, 2 to 36) and bit size
/// (0 to 64) and returns the corresponding value i.
///
/// The string may begin with a leading sign: "+" or "-".
///
/// If the base argument is 0, the true base is implied by the string's
/// prefix following the sign (if present): 2 for "0b", 8 for "0" or "0o",
/// 16 for "0x", and 10 otherwise. Also, for argument base 0 only,
/// underscore characters are permitted as defined by the Glaz syntax for
/// integer literals.
///
/// The `bit_size` argument specifies the integer type that the result must
/// fit into. Bit sizes 0, 8, 16, 32, and 64 correspond to i8, i16, i32, and
/// i64. If `bit_size` is below 0 or above 64, an error is returned.
pub fn string_to_int(s: string, base_: i32, bit_size_: u32) !i64 {
    let mut bit_size = bit_size_;
    if s == "" {
        return InvalidSyntaxError("strconv::string_to_int: invalid syntax");
    }

    // pick off leading sign.
    let mut s0 = s;
    let mut neg = false;
    if s[0] == b'+' {
        s0 = s[1..];
    } else if s[0] == b'-' {
        neg = true;
        s0 = s[1..];
    }

    // convert unsigned and check range.
    let un = string_to_uint(s0, base_, bit_size).!;
    if bit_size == 0 {
        bit_size = INT_SIZE;
    }

    let cutoff = as(u64, 1) << as(u32, bit_size - 1);
    if !neg && un >= cutoff {
        return ValueOutOfRangeError("strconv::string_to_int: value out of range");
    }
    if neg && un > cutoff {
        return ValueOutOfRangeError("strconv::string_to_int: value out of range");
    }

    return if neg { -as(i64, un) } else { as(i64, un) };
}

/// Reports whether the underscores in `s_` are allowed.
/// Checking them in this one function lets all the parsers skip over them
/// simply. Underscore must appear only between digits or between a base
/// prefix and a digit.
fn underscore_ok(s_: string) bool {
    let s = s_;
    // saw tracks the last character (class) we saw:
    //     ^ for beginning of number,
    //     0 for a digit or base prefix,
    //     _ for an underscore,
    //     ! for none of the above.
    let mut saw = b'^';
    let mut i: usize = 0;

    // optional sign.
    if s.len >= 1 && (s[0] == b'-' || s[0] == b'+') {
        i += 1;
    }

    // optional base prefix.
    let mut hex = false;
    if s.len - i >= 2 && s[i] == b'0' && (
        lower(s[i + 1]) == b'b' || lower(s[i + 1]) == b'o' || lower(s[i + 1]) == b'x'
    ) {
        // base prefix counts as a digit for "underscore as digit separator"
        saw = b'0';
        hex = lower(s[i + 1]) == b'x';
        i += 2;
    }

    // number proper.
    for i_ in i..s.len {
        // digits are always okay.
        if (b'0' <= s[i_] && s[i_] <= b'9') ||
            (hex && b'a' <= lower(s[i_]) && lower(s[i_]) <= b'f') {
            saw = b'0';
            continue;
        }
        // underscore must follow digit.
        if s[i_] == b'_' {
            if saw != b'0' {
                return false;
            }
            saw = b'_';
            continue;
        }
        // underscore must also be followed by digit.
        if saw == b'_' {
            return false;
        }
        // saw non-digit, non-underscore.
        saw = b'!';
    }

    return saw != b'_';
}
