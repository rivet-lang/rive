// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

$if _LINUX_ {
    #![link_library("dl")]
}

import c/libc;

pub var shared_lib_extension := get_shared_library_extension();

/// Returns a library name with the operating system specific extension for shared
/// libraries.
#[inline]
pub func library_name(libname: string) -> string {
    return if libname.ends_with(shared_lib_extension) {
        libname
    } else {
        libname.concat(shared_lib_extension)
    };
}

#[boxed]
pub struct SymbolNotFoundError < Throwable {
    msg: string;

    pub func to_string(self) -> string {
        return self.msg;
    }
}

#[boxed]
pub struct CannotLoadLibraryError < Throwable {
    msg: string;

    pub func to_string(self) -> string {
        return self.msg;
    }
}

pub func load(path: string, global_symbols: bool := false) -> !Library {
    flags: int32 := if global_symbols { libc.RTLD_NOW | libc.RTLD_GLOBAL } else { libc.RTLD_NOW };
    if ptr := unsafe { libc.dlopen(path.ptr, flags) } {
        return Library(path, ptr);
    }
    if dl_error := dlerror() {
        throw CannotLoadLibraryError("cannot load '{}': {}".fmt(path, dl_error));
    }
    throw CannotLoadLibraryError("cannot load '{}'".fmt(path));
}

#[boxed]
struct Library {
    pub path: string;
    ptr: rawptr;

    pub func address_of(self, symbol: string) -> !rawptr {
        if ptr := unsafe { libc.dlsym(self.ptr, symbol.ptr) } {
            return ptr;
        }
        if dl_error := dlerror() {
            throw SymbolNotFoundError(
                "cannot found symbol '{}' in library '{}': {}".fmt(symbol, self.path, dl_error)
            );
        } else {
            throw SymbolNotFoundError("cannot found symbol '{}' in library '{}'".fmt(symbol, self.path));
        }
    }

    #[inline]
    pub func exists(self, symbol: string) -> bool {
        return if _ := self.address_of(symbol) { true } else { false };
    }

    #[inline]
    pub func close(self) {
        _ = unsafe { libc.dlclose(self.ptr) };
    }

    func __destroy__(self) {
        self.close();
    }
}

func dlerror() -> ?string {
    if err := unsafe { libc.dlerror() } {
        return unsafe { string.from_raw(err) }.clone();
    }
    return none;
}

#[inline]
func get_shared_library_extension() -> string {
    return $if _LINUX_ {
        ".so"
    } else if _WINDOWS_ {
        ".dll"
    } else {
        ".dylib"
    };
}
