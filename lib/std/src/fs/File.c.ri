// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import c/libc;

import ../{ mem, traits.Stringable };

#[boxed]
pub struct OperationFailedError < Throwable {
    msg: string;

    pub func to_string(^self) -> string {
        return self.msg;
    }
}

#[boxed]
pub struct File {
    f: &mut libc.FILE;
    mut is_opened: bool;

    pub func open(path: string, mode: string := "r") -> !^mut File {
        unsafe {
            if f := libc.fopen(path.ptr, mode.ptr) {
                return ^mut Self(f, true);
            }
        }
        throw OperationFailedError("File.open: failed to open file '{}'".fmt(path));
    }

    /// Writes the byte `b` into the file.
    #[inline]
    pub func write_byte(^mut self, b: uint8) {
        @ignore_not_mutated_warn(self);
        unsafe {
            _ = libc.fputc(@as(int32, b), self.f);
        }
    }

    /// Writes the string `s` into the file.
    #[inline]
    pub func write(^mut self, s: string) {
        @ignore_not_mutated_warn(self);
        unsafe {
            _ = libc.fputs(s.ptr, self.f);
        }
    }

    /// Writes the string `s` into the file and appends a '\n' character.
    pub func writeln(^mut self, s: string) {
        self.write(s);
        self.write_byte('\n');
    }

    #[inline]
    pub func write_string_fmt(^mut self, s: string, args: ...^Stringable) {
        self.write(s.fmt(args));
    }

    pub func writeln_fmt(^mut self, s: string, args: ...^Stringable) {
        self.write(s.fmt(args));
        self.write_byte('\n');
    }

    /// Returns the current file contents.
    pub func read_all(^self) -> !string {
        if !self.is_opened {
            return "";
        }
        unsafe {
            seek := libc.fseek(self.f, 0, 2);
            if seek != 0 {
                throw OperationFailedError("fseek failed");
            }

            fsize := libc.ftell(self.f);
            if fsize < 0 {
                throw OperationFailedError("ftell failed");
            }

            libc.rewind(self.f);

            // On some systems `ftell` can return values in the 64-bit range
            // that, when cast to `int32`, can result in values below 0.
            allocate := @as(int32, fsize);
            if @as(int64, allocate) < fsize {
                throw OperationFailedError(
                    "{} cast to `int32` results in {}".fmt(fsize, @as(int32, fsize))
                );
            }

            res := @as([&]mut uint8, mem.boxed_alloc(@as(uint, allocate) + 1));
            nelements := libc.fread(res, 1, @as(uint, allocate), self.f);

            if self.is_eof() && libc.ferror(self.f) != 0 {
                mem.boxed_release(res);
                throw OperationFailedError("fread failed");
            }

            res[nelements] = 0;
            return string.from_raw_with_len(res, nelements);
        }
    }

    /// Returns true if the file is at end.
    #[inline]
    pub func is_eof(^self) -> bool {
        return unsafe { libc.feof(self.f) != 0 };
    }

    /// Closes the current file.
    pub func close(^mut self) {
        if !self.is_opened {
            return;
        }
        unsafe {
            _ = libc.fflush(self.f);
            _ = libc.fclose(self.f);
        }
        self.is_opened = false;
    }

    pub func __destroy__(^mut self) {
        self.close();
    }
}
