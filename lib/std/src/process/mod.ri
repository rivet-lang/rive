// Copyright (C) 2022 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import "c";
import "c/libc";
import {
    self,
    pub process_id as id,
    pub process_panic as panic,
    pub process_abort as abort,
    pub process_exit as exit,
    pub process_executable as executable,
    pub process_set_cwd as set_cwd,
    pub process_get_cwd as get_cwd
} from "runtime";

import { Builder } from "../strings";

pub let ARGS: [string] = unsafe { runtime.ARGS };

struct Result {
	pub output: string;
	pub exit_code: i32;
}

/// Starts the specified command, waits for it to complete, and returns
/// both its output and the exit code.
pub fn execute(cmd: string) Result {
    let pcmd = if cmd.contains("2>") { cmd } else { "{} 2>&1".fmt(cmd) };
    unsafe {
        if let f = libc.popen(pcmd.ptr, c"r") {
            let fd = libc.fileno(f);
            let mut buf: [mut u8; 4096] = []!;
            let pbuf: *mut u8 = @addr_of_mut(buf[0]);
            let mut output = Builder.new(1024);
            while {
                let len = libc.read(fd, pbuf, 4096);
                if len == 0 or len == -1 {
                    break;
                }
                output.write_raw_with_len(pbuf, @as(usize, len));
            }
            return Result(output.to_string(), libc.pclose(f));
        }
        return Result("execute( {} ) failed".fmt(cmd), -1);
    }
}
