// Copyright (C) 2022 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

import "c";
import "c/libc";
import "core";

import "../fs/path";
import { Builder } from "../strings";

public alias id = core.process_id;
public alias panic = core.process_panic;
public alias abort = core.process_abort;
public alias exit = core.process_exit;
public alias executable = core.process_executable;
public alias set_cwd = core.process_set_cwd;
public alias get_cwd = core.process_get_cwd;

public var ARGS: []string = unsafe { core.ARGS };
public var WD_AT_STARTUP: string = get_cwd() catch ".";
public var EXECUTABLE_DIR: string = path.dirname(executable() catch WD_AT_STARTUP);

struct Result {
	public output: string;
	public exit_code: int32;
}

/// Starts the specified command, waits for it to complete, and returns
/// both its output and the exit code.
public func execute(cmd: string) Result {
    pcmd := if cmd.contains("2>") { cmd } else { "{} 2>&1".fmt(cmd) };
    unsafe {
        if f := libc.popen(pcmd.ptr, c"r") {
            fd := libc.fileno(f);
            mut buf: [4096]mut uint8 := []!;
            pbuf: [*]mut uint8 := &mut buf[0];
            mut output := Builder.new(1024);
            while {
                len := libc.read(fd, pbuf, 4096);
                if len == 0 or len == -1 {
                    break;
                }
                output.write_raw_with_len(pbuf, @cast(usize, len));
            }
            return Result(output.to_string(), libc.pclose(f));
        }
        return Result("execute( {} ) failed".fmt(cmd), -1);
    }
}
