// Copyright (C) 2023-present Jose Mendoza - All rights reserved. Use of this
// source code is governed by an MIT license that can be found in the LICENSE
// file.

import std/traits;

/// Represents the different types of version increments.
pub enum Increment as uint8 {
	Major,
	Minor,
	Patch
}

#[boxed]
pub struct EmptyInputError < Throwable {
    pub func to_string(^self) -> string {
        _ = self;
        return "empty input";
    }
}

#[boxed]
pub struct InvalidVersionFormatError < Throwable {
    input: string;

    pub func to_string(^self) -> string {
        return "invalid version format for input '{}'".fmt(self.input);
    }
}

#[boxed]
pub struct InvalidVersionError < Throwable {
    input: string;

    pub func to_string(^self) -> string {
        return self.input;
    }
}

/// Represents a semantic version in semver format.
#[boxed]
pub struct Version < traits.Stringable {
    pub major: uint;
    pub minor: uint;
    pub patch: uint;
    pub prerelease: string;
    pub metadata: string;

    /// Returns a `Version` structure parsed from `input` `string`.
    pub func from_string(input: string) -> !^Self {
        if input.is_empty() {
            throw ^EmptyInputError();
        }
        raw_version := RawVersion.parse(input);
        if validated := raw_version.validate() {
            return validated;
        }
        throw ^InvalidVersionFormatError(input: input);
    }

    /// Returns a `Version` structure with given `major`, `minor` and `patch` versions.
    #[inline]
    pub func build(major: uint, minor: uint, patch: uint) -> ^Self {
        // TODO: check if versions are greater than zero.
        return ^Self(major, minor, patch, "", "");
    }

    /// Returns a `Version` structure with incremented values.
    #[inline]
    pub func increment(^self, typ: Increment) -> ^Self {
        mut major := self.major;
        mut minor := self.minor;
        mut patch := self.patch;
        match typ {
            .Major -> {
                major += 1;
                minor = 0;
                patch = 0;
            },
            .Minor -> {
                minor += 1;
                patch = 0;
            },
            .Patch -> {
                patch += 1;
            }
        }
        return ^Self(major, minor, patch, self.prerelease, self.metadata);
    }

    /// Satisfies returns `true` if the `input` expression can be validated to `true`
    /// when run against this `Version`.
    #[inline]
    pub func satisfies(^self, input: string) -> bool {
        return if range := Range.parse(input) {
            range.satisfies(self)
        } else {
            false
        };
    }

    #[inline]
    pub func ==(^self, v2: ^Self) -> bool {
        return self.major == v2.major && self.minor == v2.minor && self.patch == v2.patch
            && self.prerelease == v2.prerelease;
    }

    #[inline]
    pub func !=(^self, v2: ^Self) -> bool {
        return !(self == v2);
    }

    pub func >(^self, v2: ^Self) -> bool {
        if self.major < v2.major {
            return false;
        } else if self.major > v2.major {
            return true;
        } else if self.minor < v2.minor {
            return false;
        } else if self.minor > v2.minor {
            return true;
        }
        return self.patch > v2.patch;
    }

    pub func <(^self, v2: ^Self) -> bool {
        if self.major > v2.major {
            return false;
        } else if self.major < v2.major {
            return true;
        } else if self.minor > v2.minor {
            return false;
        } else if self.minor < v2.minor {
            return true;
        }
        return self.patch < v2.patch;
    }

    #[inline]
    pub func >=(^self, v2: ^Self) -> bool {
        return self == v2 || self > v2;
    }

    #[inline]
    pub func <=(^self, v2: ^Self) -> bool {
        return self == v2 || self < v2;
    }

    pub func to_string(^self) -> string {
        common_string := "{}.{}.{}".fmt(self.major, self.minor, self.patch);
        prerelease_string := if self.prerelease.is_empty() { "" } else {
            "-".concat(self.prerelease)
        };
        metadata_string := if self.metadata.is_empty() { "" } else {
            "+".concat(self.metadata)
        };
        return common_string.concat(prerelease_string, metadata_string);
    }
}

/// Converts the `input` version to a `Version` struct.
/// coerce will strip any contents *after* the parsed version string.
#[inline]
pub func coerce(input: string) -> !^Version {
    if version := RawVersion.parse(input).coerce() {
        return version;
    }
    throw ^InvalidVersionError("invalid version for input '{}'".fmt(input));
}

/// Returns `true` if the `input` `string` can be converted to a semantic `Version`
/// struct.
#[inline]
pub func is_valid(input: string) -> bool {
	return RawVersion.parse(input).is_valid();
}
