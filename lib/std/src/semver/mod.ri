// Copyright (C) 2022 The Rivet Developers. All rights reserved.
// Use of this source code is governed by an MIT license that can
// be found in the LICENSE file.

/// Represents the different types of version increments.
public enum Increment as uint8 {
	Major,
	Minor,
	Patch
}

public class EmptyInputError : Error {
    #[override; inline]
    public func to_string(self) string {
        return "empty input";
    }
}

public class InvalidVersionFormatError : Error {
    input: string;

    #[override; inline]
    public func to_string(self) string {
        return "invalid version format for input '{}'".fmt(self.input);
    }
}

/// Represents a semantic version in semver format.
public class Version {
    public major: uint64;
    public minor: uint64;
    public patch: uint64;
    public prerelease: string;
    public metadata: string;

    /// Returns a `Version` structure parsed from `input` `string`.
    public func from_string(input: string) !Self {
        if input.is_empty() {
            return EmptyInputError();
        }
        let raw_version = RawVersion.parse(input);
        if let validated = raw_version.validate() {
            return validated;
        }
        return InvalidVersionFormatError(input: input);
    }

    /// Returns a `Version` structure with given `major`, `minor` and `patch` versions.
    #[inline]
    public func build(major: uint64, minor: uint64, patch: uint64) Self {
        // TODO: check if versions are greater than zero.
        return Version(major, minor, patch, "", "");
    }

    /// Returns a `Version` structure with incremented values.
    #[inline]
    public func increment(self, typ: Increment) Self {
        let mut major = self.major;
        let mut minor = self.minor;
        let mut patch = self.patch;
        switch typ {
            .Major => {
                major += 1;
                minor = 0;
                patch = 0;
            },
            .Minor => {
                minor += 1;
                patch = 0;
            },
            .Patch => {
                patch += 1;
            }
        }
        return Version(major, minor, patch, self.prerelease, self.metadata);
    }

    /// Satisfies returns `true` if the `input` expression can be validated to `true`
    /// when run against this `Version`.
    #[inline]
    public func satisfies(self, input: string) bool {
        return if let range = Range.parse(input) {
            range.satisfies(self)
        } else {
            false
        };
    }

    #[inline]
    public func ==(self, v2: Self) bool {
        return self.major == v2.major and self.minor == v2.minor and self.patch == v2.patch
            and self.prerelease == v2.prerelease;
    }

    #[inline]
    public func !=(self, v2: Self) bool {
        return !(self == v2);
    }

    public func >(self, v2: Self) bool {
        if self.major < v2.major {
            return false;
        } else if self.major > v2.major {
            return true;
        } else if self.minor < v2.minor {
            return false;
        } else if self.minor > v2.minor {
            return true;
        }
        return self.patch > v2.patch;
    }

    public func <(self, v2: Self) bool {
        if self.major > v2.major {
            return false;
        } else if self.major < v2.major {
            return true;
        } else if self.minor > v2.minor {
            return false;
        } else if self.minor < v2.minor {
            return true;
        }
        return self.patch < v2.patch;
    }

    #[inline]
    public func >=(self, v2: Self) bool {
        return self == v2 or self > v2;
    }

    #[inline]
    public func <=(self, v2: Self) bool {
        return self == v2 or self < v2;
    }

    public func to_string(self) string {
        let common_string = "{}.{}.{}".fmt(self.major, self.minor, self.patch);
        let prerelease_string = if self.prerelease.is_empty() { "" } else {
            "-".concat(self.prerelease)
        };
        let metadata_string = if self.metadata.is_empty() { "" } else {
            "+".concat(self.metadata)
        };
        return common_string.concat(prerelease_string, metadata_string);
    }
}

/// Converts the `input` version to a `Version` struct.
/// coerce will strip any contents *after* the parsed version string.
#[inline]
public func coerce(input: string) !Version {
    if let version = RawVersion.parse(input).coerce() {
        return version;
    }
    return Error("invalid version for input '{}'".fmt(input));
}

/// Returns `true` if the `input` `string` can be converted to a semantic `Version`
/// struct.
#[inline]
public func is_valid(input: string) bool {
	return RawVersion.parse(input).is_valid();
}
