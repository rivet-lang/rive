// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

let SUPPORT_COLOR: bool = Console::can_show_color_on_stdout();

let OK_STATUS: string = bold(green(" [  OK  ] "));
let FAILED_STATUS: string = bold(red(" [ FAIL ] "));
let SKIP_STATUS: string = bold(yellow(" [ SKIP ] "));
let DIVIDER: string = bold(header("", "-"));

extern (Rivet) {
    let test_runner: TestRunner;
}

fn header(txt: string, sep: string) string {
    if SUPPORT_COLOR {
        // TODO: return Console::header(txt, sep);
    }
    return txt;
}

fn bold(txt: string) string {
    if SUPPORT_COLOR {
        return Console::bold(txt);
    }
    return txt;
}

fn green(txt: string) string {
    if SUPPORT_COLOR {
        return Console::green(txt);
    }
    return txt;
}

fn red(txt: string) string {
    if SUPPORT_COLOR {
        return Console::red(txt);
    }
    return txt;
}

fn yellow(txt: string) string {
    if SUPPORT_COLOR {
        return Console::yellow(txt);
    }
    return txt;
}

enum TestResult : u8 {
    Ok,
    Skipped,
    ErrorRaised,
    Failed
}

struct Test {
    name: string;
    test_fn : fn (*Test);
    result: TestResult;
    err_pos: string;
    err: Error;
}

struct TestRunner {
    tests: [Test];
    mut ok_tests: u64;
    mut fail_tests: u64;
    mut skip_tests: u64;

    fn run(mut self) {
        for i in 0..self.tests.len() {
            let mut test_ = self.tests[i];
            (test_.test_fn)(unsafe { addr_of!(test_) });
            switch test_.result {
                .Ok => {
                    self.print_test_ok(test_.name);
                },
                .Skipped => {
                    self.print_test_skip(test_.name);
                },
                .Failed => {
                    self.print_test_failed(test_.name, test_.err_pos);
                },
                .ErrorRaised => {
                    self.print_test_failed(test_.name, test_.err_pos);
                    Console::eprintln("    Error raised: {}", test_.err);
                }
            }
        }
        self.print_summary_tests();
        process_exit(as(i32, self.fail_tests != 0));
    }

    fn print_header(&self) {
        Console::println(bold(header("Rivet TestRunner", "-")));
    }

    fn print_summary_tests(&self) {
        Console::println(DIVIDER);
        Console::print(bold("Summary for all tests: "));
        if self.ok_tests > 0 {
            Console::print("{}, ", bold(green("{} passed".fmt(self.ok_tests))));
        }
        if self.fail_tests > 0 {
            Console::print("{}, ", bold(red("{} failed".fmt(self.fail_tests))));
        }
        if self.skip_tests > 0 {
            Console::print("{}, ", bold(yellow("{} skipped".fmt(self.skip_tests))));
        }
        Console::println(bold("{} total.".fmt(self.tests.len())));
        Console::println(DIVIDER);
    }

    fn print_test_ok(&self, name: string) {
        Console::println(OK_STATUS.concat(name));
        self.ok_tests += 1;
    }

    fn print_test_skip(&self, name: string) {
        Console::eprintln(SKIP_STATUS.concat(name));
        self.skip_tests += 1;
    }

    fn print_test_failed(&self, name: string, pos: string) {
        Console::eprintln(FAILED_STATUS.concat(name));
        Console::eprintln(pos);
        self.fail_tests += 1;
    }
}
