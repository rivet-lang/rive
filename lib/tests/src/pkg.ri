// Copyright (C) 2022 The Rivet Team. All rights reserved.
// Use of this source code is governed by an MIT license
// that can be found in the LICENSE file.

extern pkg std;

use std::process { exit };
use std::console { eprintln, print, println };

let SUPPORT_COLOR: bool = console::can_show_color_on_stdout();

let OK_STATUS: string = bold(green(" [  OK  ] "));
let FAIL_STATUS: string = bold(red(" [ FAIL ] "));
let SKIP_STATUS: string = bold(yellow(" [ SKIP ] "));
let DIVIDER: string = bold(header("", "-"));

extern (C) {
    let test_runner: TestRunner;
}

func header(txt: string, sep: string) string {
    if (SUPPORT_COLOR) {
        // TODO: return console::header(txt, sep);
    }
    return txt;
}

func bold(txt: string) string {
    if (SUPPORT_COLOR) {
        return console::bold(txt);
    }
    return txt;
}

func green(txt: string) string {
    if (SUPPORT_COLOR) {
        return console::green(txt);
    }
    return txt;
}

func red(txt: string) string {
    if (SUPPORT_COLOR) {
        return console::red(txt);
    }
    return txt;
}

func yellow(txt: string) string {
    if (SUPPORT_COLOR) {
        return console::yellow(txt);
    }
    return txt;
}

enum TestResult : u8 {
    Ok,
    Skipped,
    ErrorRaised,
    Failed
}

struct Test {
    name: string;
    test_func : func (&mut Test);
    result: TestResult;
    err_pos: string;
    err: core::_Error;
}

struct TestRunner {
    tests: [Test];
    mut ok_tests: i32;
    mut fail_tests: i32;
    mut skip_tests: i32;

    func print_header(self) {
        println(bold(header("Rivet TestRunner", "-")));
    }

    func print_test_ok(self, name: string) {
        println(OK_STATUS.concat(name));
        self.ok_tests++;
    }

    func print_test_skip(self, name: string) {
        eprintln(SKIP_STATUS.concat(name));
        self.skip_tests++;
    }

    func print_test_fail(self, name: string, pos: string) {
        eprintln(FAIL_STATUS.concat(name));
        eprintln(pos);
        self.fail_tests++;
    }

    func print_summary_tests(self) {
        println(DIVIDER);
        print(bold("Summary for all tests: "));
        if (self.ok_tests > 0) {
            print("{}, ", bold(green("{} passed".fmt(self.ok_tests))));
        }
        if (self.fail_tests > 0) {
            print("{}, ", bold(red("{} failed".fmt(self.fail_tests))));
        }
        if (self.skip_tests > 0) {
            print("{}, ", bold(yellow("{} skipped".fmt(self.skip_tests))));
        }
        println(bold("{} total.".fmt(self.tests.len)));
        println(DIVIDER);
    }

    func run(mut self) {
        for (test_ in self.tests) {
            test_.test_func (&mut test_);
            switch (test_.result) {
                .Ok => {
                    self.print_test_ok(test_.name);
                }
                .Skipped => {
                    self.print_test_skip(test_.name);
                }
                .Fail => {
                    self.print_test_fail(test_.name, test_.err_pos);
                }
                .ErrorRaised => {
                    self.print_test_fail(test_.name, test_.err_pos);
                    eprintln("    Error raised: {}", test_.err);
                }
            }
        }
        self.print_summary_tests();
        exit(if (self.fail_tests > 0) 1 else 0);
    }
}
