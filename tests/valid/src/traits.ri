trait Reader {
    proc read(self) -> uint8;
}

trait Writer {
    proc write(self, b: uint8) -> bool;
}

trait ReaderWriter < Reader, Writer {
    proc write_and_read(self, b: uint8) -> uint8;
}

struct SomeStream < ReaderWriter {
    pub proc read(&self) -> uint8 {
        _ = self;
        return 'A';
    }

    pub proc write(&self, _b: uint8) -> bool {
        _ = self;
        return true;
    }

    pub proc write_and_read(&self, b: uint8) -> uint8 {
        _ = self.write(b);
        return self.read();
    }
}

proc writer(w: Writer) -> bool {
    return w.write('A');
}

proc reader(r: Reader) -> uint8 {
    return r.read();
}

proc reader_writer(rw: ReaderWriter) -> uint8 {
    return rw.write_and_read('A');
}

test "traits: inheritance" {
    ss := SomeStream();
    @assert(writer(ss));
    @assert(reader(ss) == 'A');
    @assert(reader_writer(ss) == 'A');
}

trait Eq {
    x: string;

    proc equal(self, x: Self) -> bool;

    proc nequal(self, x: Self) -> bool {
        return !self.equal(x);
    }
}

#[boxed]
struct Poketrait < Eq {
    pub proc equal(self, x: Eq) -> bool {
        return self.x == x.x;
    }
}

proc ship(a: Eq, b: Eq) -> bool {
    return !a.nequal(b);
}

test "traits: default method" {
    @assert(ship( Poketrait("Shell"), Poketrait("Shell") ));
}

#[default_value(IntegerValue(5))]
trait Integer {}

struct IntegerValue {
    int: int32;
}

struct IntegerStruct {
    i: Integer;
}

test "traits with `default_value` attribute" {
    is_ := IntegerStruct();
    @assert(is_.i is IntegerValue(int_v) && int_v.int == 5);
}
